// Copyright 2024 Virgil Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Arm64 instructions are ints that look like <arg2><arg1><am><code>
// where each part of the instruction is a byte

// masks
def MASK_CODE = 0xff;
def MASK_AM = 0xff00;
def MASK_ARG1 = 0xff0000;
def MASK_ARG2 = 0xff000000;

// shifts
def SHIFT_CODE: byte = 0;
def SHIFT_AM: byte = 8;
def SHIFT_ARG1: byte = 16;
def SHIFT_ARG2: byte = 24;

// codes
def I_ADDD: byte = 0x01;	def I_ADDQ: byte = 0x11;
def I_SUBD: byte = 0x02;	def I_SUBQ: byte = 0x12;
def I_MOVZ: byte = 0x30;
def I_MOVK: byte = 0x31;
def I_LDR: byte = 0x32;
def I_STR: byte = 0x33;

def I_QD_DIFF: byte = I_ADDQ - I_ADDD;

// addressing modes
def AM_NONE: byte = 0x00;
def AM_R_R_I_I: byte = 0x01;
def AM_R_R_R_SH_I: byte = 0x02;
def AM_R_R_R_EX_I: byte = 0x03;

// arguments
def ARG_NONE: byte = 0x00;
def ARG_SH_NONE: byte = 0x01;
def ARG_SH_LSL: byte = 0x02;
def ARG_SH_LSR: byte = 0x03;
def ARG_SH_ASR: byte = 0x04;

def makeOpcode(code: byte, am: byte, arg1: byte, arg2: byte) -> int {
	return (int.view(code) << SHIFT_CODE) | (int.view(am) << SHIFT_AM)
		 | (int.view(arg1) << SHIFT_ARG1) | (int.view(arg2) << SHIFT_ARG2);
}

def getCode(opcode: int) -> byte {
	return byte.view((opcode & MASK_CODE) >> SHIFT_CODE);
}

def getAM(opcode: int) -> byte {
	return byte.view((opcode & MASK_AM) >> SHIFT_AM);
}

def getArg1(opcode: int) -> byte {
	return byte.view((opcode & MASK_ARG1) >> SHIFT_ARG1);
}

def getArg2(opcode: int) -> byte {
	return byte.view((opcode & MASK_ARG2) >> SHIFT_ARG2);
}

// useful constants
def MAX_IMM16_MOV = 0xffff;
def MIN_IMM16_MOV = 0xffff0000;

def MRegs: Arm64RegSet;
def Regs: Arm64Regs;
def Conds: Arm64Conds; // TODO

// Code generation for the Arm64 backend
class SsaArm64Gen extends SsaMachGen {
	def asm: Arm64Assembler; // TODO
	def m = SsaInstrMatcher.new();
	def dwarf: Dwarf; // What is this?

	new(context: SsaContext, mach: MachProgram, asm, w: MachDataWriter, dwarf) super(context, mach, Arm64RegSet.SET, w) {}

	// Overidden Architecture Specific Routines
	def visitApply(block: SsaBlock, i: SsaApplyOp) {
		match (i.op.opcode) {
			IntAdd => emitIntBinop(I_ADDD, i);
			IntSub => emitIntBinop(I_SUBD, i);
			_ => return context.fail1("unexpected opcode %s", i.op.opcode.name);
		}
	}

	def visitThrow(block: SsaBlock, i: SsaThrow) { unimplemented(); }
	def visitIf(block: SsaBlock, i: SsaIf) { unimplemented(); }
	def visitSwitch(block: SsaBlock, i: SsaSwitch) { unimplemented(); }
	def visitGoto(block: SsaBlock, target: SsaGoto) { unimplemented(); }

	// Regalloc callbacks to add moves
	def genRestoreLocal(v: VReg, reg: int) { unimplemented(); }
	def genMoveLocLoc(src: Operand.Use, dst: Operand.Def, regClass: RegClass) { unimplemented(); }
	def genMoveConstReg(val: SsaConst, dst: Operand.Def, regClass: RegClass) { unimplemented(); }
	def genMoveConstStack(val: SsaConst, dst: Operand.Def, regClass: RegClass) { unimplemented(); }

	def assemble(opcode: int, a: Array<Operand>) {
		if (opcode < 0) {
			match (opcode) {
				ArchInstrs.ARCH_ENTRY => {
					var adjust = frameAdjust();
					// allocate frame
					if (adjust > 0) asm.subq_r_r_u12_u1(Regs.SP, Regs.SP, u12.view(adjust), 0);
				}
				ArchInstrs.ARCH_BLOCK => return; // TODO
				ArchInstrs.ARCH_RET => {
					var adjust = frameAdjust();
					// deallocate frame
					if (adjust > 0) asm.addq_r_r_u12_u1(Regs.SP, Regs.SP, u12.view(adjust), 0);
					asm.ret();
					return;
				}
				ArchInstrs.ARCH_BLOCK_END => return; //TODO
				ArchInstrs.ARCH_END => return;
				_ => unimplemented();
			}
			return;
		}

		def am = getAM(opcode);
		match (getAM(opcode)) {
			AM_R_R_I_I => {
				def imm = u12.view(toB32(toImm(a[2])));
				def lsl12 = u1.view(getArg1(opcode));
				assemble_r_r_u12_u1(toGpr(a[0]), toGpr(a[1]), imm, lsl12, opcode);
			}
			AM_R_R_R_SH_I => {
				def sh = toRegShift(getArg1(opcode));
				def imm = u6.view(getArg2(opcode));
				assemble_r_r_r_sh_u6(toGpr(a[0]), toGpr(a[1]), toGpr(a[2]), sh, imm, opcode);
			}
			_ => return context.fail1("unknown addressing mode %d", am);
		}
	}

	def assemble_r_r_u12_u1(rd: Arm64Gpr, rn: Arm64Gpr, imm: u12, lsl12: u1, opcode: int) {
		match (getCode(opcode)) {
			I_ADDD => asm.addd_r_r_u12_u1(rd, rn, imm, lsl12);
			I_ADDQ => asm.addq_r_r_u12_u1(rd, rn, imm, lsl12);
			I_SUBD => asm.subd_r_r_u12_u1(rd, rn, imm, lsl12);
			I_SUBQ => asm.subq_r_r_u12_u1(rd, rn, imm, lsl12);
			_ => invalidOpcode(opcode);
		}
	}
	def assemble_r_r_r_sh_u6(rd: Arm64Gpr, rn: Arm64Gpr, rm: Arm64Gpr, sh: RegShift, imm: u6, opcode: int) {
		match (getCode(opcode)) {
			I_ADDD => asm.addd_r_r_r_sh_u5(rd, rn, rm, sh, u5.view(imm));
			I_ADDQ => asm.addq_r_r_r_sh_u6(rd, rn, rm, sh, imm);
			I_SUBD => asm.subd_r_r_r_sh_u5(rd, rn, rm, sh, u5.view(imm));
			I_SUBQ => asm.subq_r_r_r_sh_u6(rd, rn, rm, sh, imm);
			_ => invalidOpcode(opcode);
		}
	}
	def selectWidth(i: SsaApplyOp, op: byte) -> byte {
		return if(PrimType.!(i.op.typeArgs[0]).width > 32, op + I_QD_DIFF, op);
	}
	def emitIntBinop(code: byte, i: SsaApplyOp) {
		emitSimpleBinop(selectWidth(i, code), i);
	}
	def emitSimpleBinop(code: byte, i: SsaApplyOp) {
		m.intbinop(i);
		dfnReg(i);
		use(m.x);

		var opcode: int;
		if (tryUseImm32(m.y)) { // TODO: imm32 is incorrect
			opcode = makeOpcode(code, AM_R_R_I_I, 0, ARG_NONE);
		} else {
			opcode = makeOpcode(code, AM_R_R_R_SH_I, ARG_SH_LSL, 0);
			use(m.y);
		}
		emitN(opcode);
	}
	def tryUseImm32(i: SsaInstr) -> bool {
		if (i == null) { useInt(0); return true; }
		if (SsaConst.?(i)) {
			var val = SsaConst.!(i).val;
			match (val) {
				null => { useImm(val); return true; }
				x: Box<int> => { useImm(val); return true; }
				x: Box<long> => if(x.val == int.view(x.val)) { useInt(int.view(x.val)); return true; }
				x: Addr => { useImm(val); return true; }
				x: Box<bool> => { useInt(if(x.val, 1, 0)); return true; }
				x: ArrayRangeStart => { useImm(val); return true; }
			}
		}
		return false;
	}

	def toLoc(o: Operand) -> int {
		match (o) {
			Overwrite(dst, src, assignment) => return assignment;
			Def(vreg, assignment) => return assignment;
			Use(vreg, assignment) => return assignment;
			_ => return V3.fail("expected operand with assignment");
		}
	}

	def toGpr(o: Operand) -> Arm64Gpr {
		return loc_r(toLoc(o));
	}

	def loc_r(loc: int) -> Arm64Gpr {
		var gpr = MRegs.toGpr(loc);
		if (gpr == null) return V3.fail1("expected GPR, got %s", regSet.identify(loc));
		return gpr;
	}

	def toB32(val: Val) -> int {
		var addr: Addr, b: int;
		match (val) {
			x: Box<int> 		=> b = x.val;
			x: Box<long> 		=> b = int.view(x.val);
			x: Box<bool> 		=> b = if(x.val, 1);
			x: Float32Val 		=> b = int.view(x.bits);
			x: Float64Val 		=> b = int.view(x.bits);
			x: ArrayRangeStart 	=> b = x.start;
			null 			=> b = 0;
			_ => ;
		}
		return b;
	}

	def toRegShift(sh: byte) -> RegShift {
		match (sh) {
			ARG_SH_NONE => return RegShift.NONE;
			ARG_SH_LSL => return RegShift.LSL;
			ARG_SH_LSR => return RegShift.LSR;
			ARG_SH_ASR => return RegShift.ASR;
			_ => {
				context.fail1("unknown reg shift %d", sh);
				return RegShift.NONE;
			}
		}
	}

	def frameAdjust() -> int {
		// assumes return address already pushed
		return frame.size() - mach.code.addressSize;
	}

	def invalidOpcode(opcode: int) {
		context.fail(Strings.format2("invalid opcode am=%x code=%x", getAM(opcode), getCode(opcode)));
	}

	def unimplemented() { context.fail("unimplemented"); }

	def getOutput() -> ArchInstrBuffer {
		if (out != null) return out;
		return out = SsaArm64Buffer.new(this, context.prog, regSet);
	}
}

class SsaArm64Buffer extends ArchInstrBuffer {
	def arm64codegen: SsaArm64Gen;
	new(arm64codegen, prog: Program, regSet: MachRegSet) super(arm64codegen, prog, regSet) { }
	def putArchInstr(indent: int, i: ArchInstr) -> int {
		var opcode = int.view(i.opcode()), a = i.operands;
		var name: string, cond: X86_64Cond, rounding = false;

		match (opcode & 0xFF) {
			I_ADDD,
			I_ADDQ => name = "add";
			I_SUBD,
			I_SUBQ => name = "sub";
			I_MOVZ,
			I_MOVK => name = "mov";
			I_LDR => name = "ldr";
			I_STR => name = "str";
		}
		putIndent(indent);
		puts(name);
		sp();
		putOperands(i.operands);
		return indent;
	}
}