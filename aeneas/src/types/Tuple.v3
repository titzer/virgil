// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility methods for working with the representation of tuples in the compiler
component Tuple {
	def TYPECON = Tuple_TypeCon.new();

	def toTypeList(paramType: Type) -> List<Type> {
		if (paramType == null || paramType == Void.TYPE) return null;
		if (paramType.typeCon == TYPECON) return paramType.nested;
		return List.new(paramType, null);
	}
	def toTypeArray(paramType: Type) -> Array<Type> {
		if (paramType == null || paramType == Void.TYPE) return [];
		if (paramType.typeCon == TYPECON) return Lists.toArray(paramType.nested);
		return [paramType];
	}
	def newType(types: List<Type>) -> Type {
		return TYPECON.create(types);
	}
	def length(paramType: Type) -> int {
		if (paramType == Void.TYPE) return 0;
		if (paramType.typeCon == TYPECON) return Lists.length(paramType.nested);
		return 1;
	}
	def fromTypeArray(a: Array<Type>) -> Type {
		if (a.length == 0) return Void.TYPE;
		if (a.length == 1) return a[0];
		return newType(Lists.fromArray(a));
	}
	def elementType(t: Type, index: int) -> Type {
		if (t.typeCon == TYPECON) return Lists.get(t.nested, index);
		return null;
	}
}

class Tuple_TypeCon extends TypeCon {
	new() super("tuple", V3Kind.TUPLE, -1, TypeUtil.globalCache) {
		singleton = Void.TYPE; // zero-arg tuple == void
	}

	def create1(t: Type) -> Type {
		return t;
	}
	def renderType(buffer: StringBuilder, list: List<Type>) -> StringBuilder {
		if (list == null) {
			buffer.puts("void");
		} else if (list.tail == null) {
			list.head.render(buffer);
		} else {
			buffer.putc('(');
			Lists.render(buffer, Type.render, list);
			buffer.putc(')');
		}
		return buffer;
	}
	def renderTypeRef(buffer: StringBuilder, list: List<TypeRef>) -> StringBuilder {
		if (list == null) {
			buffer.puts("void");
		} else if (list.tail == null) {
			list.head.render(buffer);
		} else {
			buffer.putc('(');
			Lists.render(buffer, TypeRef.render, list);
			buffer.putc(')');
		}
		return buffer;
	}
}

class TupleVal(values: Array<Val>) extends Val {
	def equals(other: Val) -> bool {
		if (other == this) return true;
		if (other == null) return Values.deepEqualBottom(values);
		if (!TupleVal.?(other)) return false;
		return Values.deepEqual(this.values, TupleVal.!(other).values);
	}
	def hash() -> int {
		return Arrays.hash(22, values, Values.hash);
	}
}
