// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def checkNoTypeVarsInOp(op: Operator) {
	for (t in op.typeArgs) checkNoTypeVars(t);
	for (t in op.sig.paramTypes) checkNoTypeVars(t);
	for (t in op.sig.returnTypes) checkNoTypeVars(t);
}
def checkNoTypeVars(t: Type) {
	if (t.hasTypeVars()) return V3.fail("no type vars allowed");
}
def checkInputs(inputs: Array<SsaInstr>) {
	for (i in inputs) if (i == null) return V3.fail("null input");
}

def NEW_OPTIMIZER = false;
def N = Facts.NONE;
// Support class for constructing SSA instruction-by-instruction.
class SsaBuilder extends SsaBlockState {
	def context: SsaContext;
	def graph: SsaGraph;
	var block: SsaBlock;
	var pt: SsaLink;
	var opt: SsaOptimizer;
	var source: Source;

	new(context, graph, block) { }

	// General methods.
	def addApply(source: Source, op: Operator, args: Array<SsaInstr>) -> SsaInstr {
		if (Debug.PARANOID) { checkNoTypeVarsInOp(op); checkInputs(args); }
		if (end) return unreachable(op.sig.returnType());
		if (op.opcode == Opcode.VstSugar) {
			return at(source).addApplyVst(OperatorOf<VstOperator>.!(op), args);
		}
		var i = SsaApplyOp.new(source, op, args);
		i.setFact(Operators.facts(op.opcode));
		append(i);
		if (NEW_OPTIMIZER && opt != null) return opt.reduceApply(this, i);
		return i;
	}
	def addApplyVst(op: OperatorOf<VstOperator>, args: Array<SsaInstr>) -> SsaInstr {
		var pt = op.sig.paramTypes;
		if (args.length != pt.length) args = normalizeArgs(pt, args);
		match (op.attribute) {
			TypeCast => return opTypeCast(op.typeArgs[0], op.typeArgs[1], args[0]);
			TypeQuery => return opTypeQuery(op.typeArgs[0], op.typeArgs[1], args[0]);
			NotEqual => return opBoolNot(opEqual(pt[0], args[0], args[1]));
			IntMixedEqual => return opIntCompare(V3Infix.Equal, pt[0], pt[1], args[0], args[1]);
			IntMixedNotEqual => return opBoolNot(opIntCompare(V3Infix.Equal, pt[0], pt[1], args[0], args[1]));
			IntMixedLt => return opIntCompare(V3Infix.Lt, pt[0], pt[1], args[0], args[1]);
			IntMixedLteq => return opIntCompare(V3Infix.Lteq, pt[0], pt[1], args[0], args[1]);
			IntGt => return opIntCompare(V3Infix.Lt, pt[1], pt[0], args[1], args[0]);
			IntGteq => return opIntCompare(V3Infix.Lteq, pt[1], pt[0], args[1], args[0]);
			IntBitwiseNeg => return opBitwiseNeg(IntType.!(op.typeArgs[0]), args[0]);
			IntView => return opIntView(op.typeArgs[0], IntType.!(op.typeArgs[1]), args[0]);
			IntQueryI => {
				var ft = op.typeArgs[0], tt = op.typeArgs[1];
				if (!IntType.?(ft)) {
					// polymorphic int type query requires specialization
					var op = V3Op.newIntQueryI(ft, tt);
					return pure(op, args);
				}
				return opIntQueryI(IntType.!(ft), IntType.!(tt), args[0]);
			}
			IntCastI => {
				var ft = op.typeArgs[0], tt = op.typeArgs[1], x = args[0];
				if (!IntType.?(ft)) {
					// TODO: this is an ugly hack for the polymorphic cast of future int type casts
					return pure(V3Op.newIntCastI(ft, tt), args);
				}
				return opIntCastI(IntType.!(ft), IntType.!(tt), args[0]);
			}
			FloatGt => {
				var fop = Float_TypeCon.!(op.typeArgs[0].typeCon).lookupInfix(V3Infix.Lt, null);
				return pure(fop, [args[1], args[0]]);
			}
			FloatGteq => {
				var fop = Float_TypeCon.!(op.typeArgs[0].typeCon).lookupInfix(V3Infix.Lteq, null);
				return pure(fop, [args[1], args[0]]);
			}
			FloatSign => {
				var ftc = Float_TypeCon.!(op.typeArgs[0].typeCon);
				return opFloatBitField(op, ftc, ftc.total_width - byte.!(1), 0, args[0]);
			}
			FloatExponent => {
				var ftc = Float_TypeCon.!(op.typeArgs[0].typeCon);
				return opFloatBitField(op, ftc, ftc.fraction_width, 0 - ftc.exponentBias(), args[0]);
			}
			FloatFraction => {
				var ftc = Float_TypeCon.!(op.typeArgs[0].typeCon);
				return opFloatBitField(op, ftc, 0, 0, args[0]);
			}
			VariantGetName => {
				var variant = V3Class_TypeCon.!(op.typeArgs[0].typeCon);
				var array = context.graph.recordConst(variant.getNames(context.prog));
				var tag = add(V3Op.newVariantGetTag(op.typeArgs[0]), args, N);
				var index = opIntConvert0(variant.classDecl.tagType, Int.TYPE, tag);
				return add(V3Op.newArrayGetElem(V3.stringArrayType), [array, index], Facts.O_SAFE_BOUNDS | Fact.O_PURE);
			}
			EnumGetParam(field) => {
				var constructor = field.receiver.constructor;
				if (constructor.initIndex >= 0) {
					// initialize the enum fields if necessary
					opInit(context.prog.ir.makeIrClass(field.receiver.declType).methods[0]);
				}
				var arrayType = field.enumVals.rtype;
				var array = context.graph.recordConst(field.enumVals);
				return add(V3Op.newArrayGetElem(arrayType), [array, args[0]], Facts.O_SAFE_BOUNDS | Fact.O_PURE);
			}
			EnumSetHas(vcase) => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				var val: Val = if(it.width <= 32, Int.box(1 << u5.!(vcase.tag)), Long.box(1L << u6.!(vcase.tag)));
				var mask = context.graph.valConst(it, val);
				var and = add(etc.andOp, [args[0], mask], N);
				return add(etc.eqOp, [and, mask], N);
			}
			EnumSetUnion => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				return add(etc.orOp, args, N);
			}
			EnumSetIntersect => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				return add(etc.andOp, args, N);
			}
			EnumSetSubtract => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				var minus_one = context.graph.valConst(it, it.box(-1));
				var inv = add(etc.xorOp, [args[1], minus_one], N);
				return add(etc.andOp, [args[0], inv], N);
			}
			EnumSetStrictSubset => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				var intersect = add(etc.andOp, args, N);
				var lt = add(etc.eqOp, [args[0], intersect], N);
				var neq = opBoolNot(add(etc.eqOp, [args[0], args[1]], N));
				return opBoolAnd(V3Op.opBoolAnd, lt, neq);
			}
			EnumSetStrictSuperset => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				var intersect = add(etc.andOp, args, N);
				var gt = add(etc.eqOp, [args[1], intersect], N);
				var neq = opBoolNot(add(etc.eqOp, [args[0], args[1]], N));
				return opBoolAnd(V3Op.opBoolAnd, gt, neq);
			}
			EnumSetSubset => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				var intersect = add(etc.andOp, args, N);
				return add(etc.eqOp, [args[0], intersect], N);
			}
			EnumSetSuperset => {
				var etc = V3EnumSet_TypeCon.!(pt[0].typeCon), it = etc.intType;
				var intersect = add(etc.andOp, args, N);
				return add(etc.eqOp, [args[1], intersect], N);
			}
		}
	}
	def opFloatBitField(op: Operator, ftc: Float_TypeCon, shift: byte, plus: int, x: SsaInstr) -> SsaInstr {
		var viewOp = if(ftc.is64, V3Op.opIntViewF64, V3Op.opIntViewF32);
		x = pure(viewOp, [x]);
		var ivt = IntType.!(viewOp.sig.returnType());
		var itt = IntType.!(op.sig.returnType());
		if (shift > 0) {
			x = add(ivt.lookupInfix0(V3Infix.Shr), [x, graph.intConst(shift)], Fact.O_NO_SHIFT_CHECK);
		}
		if (plus != 0) {
			// XXX: can be a little more efficient to do the subtraction after truncation
			var val = if(ivt.width > 32, graph.longConst(plus), graph.intConst(plus));
			x = pure(ivt.lookupInfix0(V3Infix.Add), [x, val]);
		}
		return opIntConvert0(ivt, itt, x);
	}
	def opFloatBitEqual(is64: bool, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		var viewOp = if(is64, V3Op.opIntViewF64, V3Op.opIntViewF32);
		var it = Int.getType(false, if(is64, 64, 32));
		var cmpOp = it.lookupInfix(V3Infix.Equal, it);
		var xb = add(viewOp, [x], Fact.O_PURE);
		var yb = add(viewOp, [y], Fact.O_PURE);
		return add(cmpOp, [xb, yb], Fact.O_PURE);
	}
	def opBitwiseNeg(it: IntType, e: SsaInstr) -> SsaInstr {
		var const = if(it.width <= 32, graph.intConst(it.trunc_32(-1)), graph.longConst(it.trunc_64(-1)));
		return pure(it.lookupInfix0(V3Infix.Xor), [e, const]);
	}
	def opIntView(ft: Type, itt: IntType, e: SsaInstr) -> SsaInstr {
		if (ft.typeCon.kind == V3Kind.FLOAT) {
			var op = if(Float_TypeCon.!(ft.typeCon).is64, V3Op.opIntViewF64, V3Op.opIntViewF32);
			return pure(op, [e]);
		} else {
			return opIntConvert0(ft, itt, e);
		}
	}
	def opEnumSetHas(etc: V3EnumSet_TypeCon, set: SsaInstr, e: SsaInstr) -> SsaInstr {
		var it = etc.intType;
		var one = context.graph.valConst(it, if(it.width <= 32, Int.box(1), Long.box(1L)));
		var mask = add(it.lookupInfix0(V3Infix.Shl), [one, e], Fact.O_NO_SHIFT_CHECK);
		var and = add(etc.andOp, [set, mask], N);
		return add(etc.eqOp, [and, mask], N);
	}
	def opIntCompare(infix: byte, xt: Type, yt: Type, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		var lt = IntType.!(xt), rt = IntType.!(yt);
		if (lt == rt) return pure(lt.lookupInfix0(infix), [x, y]);
		// Handle mixed-sign comparisons.
		if (lt.signed) {
			var neg = add(lt.lookupInfix0(V3Infix.Lt), [x, context.graph.nullConst(lt)], N);
			x = opIntSignedPromote(lt, rt, x);
			var cmp = add(rt.lookupInfix0(infix), [x, y], N);
			if (infix == V3Infix.Equal) return opBoolAnd0(opBoolNot(neg), cmp);
			return opBoolOr(V3Op.opBoolOr, neg, cmp);
		} else {
			var neg = add(rt.lookupInfix0(V3Infix.Lt), [y, context.graph.nullConst(rt)], N);
			y = opIntSignedPromote(rt, lt, y);
			var cmp = add(lt.lookupInfix0(infix), [x, y], N);
			if (infix == V3Infix.Equal) return opBoolAnd0(opBoolNot(neg), cmp);
			return opBoolAnd0(opBoolNot(neg), cmp);
		}
	}
	def opIntQueryI(ift: IntType, itt: IntType, x: SsaInstr) -> SsaInstr {
		var diff = int.!(ift.width) - int.!(itt.width);
		if (diff > 0) {
			if (ift.signed) {
				if (itt.signed) {
					var shl = ift.lookupInfix0(V3Infix.Shl);
					var sar = ift.lookupInfix0(V3Infix.Sar);
					var cmp = ift.lookupInfix0(V3Infix.Equal);
					var d = graph.valConst(Byte.TYPE, Int.box(diff));
					var s = pure(shl, [x, d]);
					s = pure(sar, [s, d]);
					return pure(cmp, [x, s]); // ((x << diff) >> diff) == x
				} else {
					var shr = ift.lookupInfix0(V3Infix.Shr);
					var s = pure(shr, [x, graph.valConst(Byte.TYPE, Int.box(itt.width))]);
					var cmp = ift.lookupInfix0(V3Infix.Equal);
					return pure(cmp, [s, graph.nullConst(ift)]); // (x >> T.width) == 0
				}
			} else {
				var lteq = ift.lookupInfix0(V3Infix.Lteq);
				return pure(lteq, [x, graph.valConst(ift, itt.max)]); // x <= T.max
			}
		} else if (diff == 0) {
			if (ift.signed == itt.signed) return graph.trueConst();
			var lteq = ift.lookupInfix0(V3Infix.Lteq);
			if (ift.signed) return pure(lteq, [graph.nullConst(ift), x]); // x >= 0
			return pure(lteq, [x, graph.valConst(ift, itt.max)]); // x <= T.max
		} else {
			if (ift.signed && !itt.signed) {
				var lteq = ift.lookupInfix0(V3Infix.Lteq);
				return pure(lteq, [graph.nullConst(ift), x]); // x >= 0
			}
			return graph.trueConst();
		}
	}
	def opIntCastI(ift: IntType, itt: IntType, x: SsaInstr) -> SsaInstr {
		var ok = opIntQueryI(ift, itt, x);
		var inv = opBoolNot(ok);
		opConditionalThrow(V3Exception.TypeCheck, inv);
		return opIntConvert0(ift, itt, x);
	}
	def opIntSignedPromote(st: IntType, ut: IntType, x: SsaInstr) -> SsaInstr {
		return if(st.width != ut.width, opIntConvert0(Int.getType(false, st.width), ut, x), x);
	}
	def normalizeArgs(paramTypes: Array<Type>, args: Array<SsaInstr>) -> Array<SsaInstr> {
		if (paramTypes.length == 0) return [];
		if (paramTypes.length == 1) {
			if (args.length == 0) return [context.graph.nullConst(paramTypes[0])];
			var tt = paramTypes[0];
			return [opTupleCreate(tt, args)];
		}
		if (args.length != 1) return V3.fail("expected to expand exactly one argument");
		var tt = Tuple.newType(Lists.fromArray(paramTypes));
		var arg0 = args[0];
		args = Array<SsaInstr>.new(paramTypes.length);
		for (i < paramTypes.length) args[i] = opTupleGetElem(tt, i, arg0);
		return args;
	}
	def addThrow(source: Source, ex: string) -> SsaInstr {
		if (end) return block.end();
		end = true;
		var t = SsaThrow.new(source, ex);
		block.append(t);
		return t;
	}
	def addGoto(target: SsaBlock) {
		if (end) return;
		end = true;
		block.append(SsaGoto.new(target));
	}
	def addReturn(vals: Array<SsaInstr>) {
		if (Debug.PARANOID) { checkInputs(vals); }
		if (end) return;
		end = true;
		block.append(SsaReturn.new(vals));
	}
	def addPhi(t: Type, vals: Array<SsaInstr>) -> SsaInstr {
		if (Debug.PARANOID) { checkInputs(vals); }
		var phi = SsaPhi.new(t, block, vals);
		block.prepend(phi);
		return phi;
	}
	def append(i: SsaInstr) {
		if (pt != null) i.insertBefore(pt);
		else block.append(i);
	}

	// sets the source position that will apply to subsequent instructions
	def at(source: Source) -> this {
		this.source = source;
	}
	def addIf(cond: SsaInstr, tblock: SsaBlock, fblock: SsaBlock) {
		if (Debug.PARANOID) { checkInputs([cond]); }
		if (end) return;
		if (SsaConst.?(cond)) {
			// fold if(#k)
			return addGoto(if(cond.unbox<bool>(), tblock, fblock));
		}
		if (SsaApplyOp.?(cond)) {
			// if(!op) t else f => if(op) f else t
			if (SsaApplyOp.!(cond).op.opcode == Opcode.BoolNot) return addIf(cond.input0(), fblock, tblock);
			// XXX: optimize if(x == true|false) and if(x != true|false)
		}
		end = true;
		var i = SsaIf.new(cond, tblock, fblock);
		append(i);
		if (NEW_OPTIMIZER && opt != null) opt.reduceIf(this, i);
	}
	def addIfNull(x: SsaInstr, tblock: SsaBlock, fblock: SsaBlock) {
		if (end) return;
		var t = x.getType();
		return addIf(pure(V3Op.newRefEqual(t), [x, graph.nullConst(t)]), tblock, fblock);
	}
	def opEqualOf(op: Operator, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return if (x == y, graph.trueConst(), add(op, [x, y], N));
	}
	// x == y
	def opEqual(t: Type, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		match (t.typeCon.kind) {
			V3Kind.VOID,
			V3Kind.COMPONENT => return context.graph.trueConst();
			_ => return opEqualOf(V3Op.newEqual(t), x, y);
		}
	}
	// x != y
	def opNotEqual(t: Type, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return opBoolNot(opEqual(t, x, y));
	}
	// x && y
	def opBoolAnd0(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return opBoolAnd(V3Op.opBoolAnd, x, y);
	}
	def opBoolAnd(op: Operator, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return if(x == y, x, add(op, [x, y], N));
	}
	// x || y
	def opBoolOr(op: Operator, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return if(x == y, x, add(op, [x, y], N));
	}
	// !x
	def opBoolNot(x: SsaInstr) -> SsaInstr {
		return pure(V3Op.opBoolNot, [x]);
	}
	// TypeCast<F, T>(x)
	def opTypeCast(ft: Type, tt: Type, x: SsaInstr) -> SsaInstr {
		if (ft == tt) return x; // fold TypeCast<T, T>
		if (tt == x.getType()) return x; // fold TypeCast<F, T>(x: T)
		var cast = TypeSystem.newTypeCast(ft, tt);
		if (cast.approx != CastOp.UNKNOWN_CAST && SsaConst.?(x)) {
			cast = cast.eval(ft, tt, SsaConst.!(x).val);
		}

		match (cast.approx) {
			TRUE => return x; // fold TypeCast
			FALSE => {
				return addThrow(source, V3Exception.TypeCheck); // fold TypeCast
			}
			TRUE_X_NULL => {
				// TypeCast -> ConditionalThrow(x == null)
				var cmp = opEqual(ft, x, graph.nullConst(ft));
				opConditionalThrow(V3Exception.TypeCheck, cmp);
				return x;
			}
			FALSE_X_NULL => {
				// TypeCast -> ConditionalThrow(x != null)
				var cmp = opNotEqual(ft, x, graph.nullConst(ft));
				opConditionalThrow(V3Exception.TypeCheck, cmp);
				return graph.nullConst(tt);
			}
			VALUE => {
				return graph.valConst(tt, cast.result);
			}
			INT_CONVERSION => {
				return opIntConvert0(ft, tt, x);
			}
			FLOAT_PROMOTE_I => {
				return pure(V3Op.opFloatPromoteI(ft, tt), [x]);
			}
			FLOAT_PROMOTE_F => {
				return pure(V3Op.opFloatPromoteF, [x]);
			}
			FLOAT_CAST_I => {
				return add(V3Op.opFloatCastI(ft, tt), [x], Facts.NONE);
			}
			FLOAT_CAST_D => {
				return add(V3Op.opFloatCastD, [x], Facts.NONE);
			}
			INT_CAST_F => {
				return add(V3Op.opIntCastF(ft, tt), [x], Facts.NONE);
			}
			ENUM_TO_SET => {
				return V3EnumSet_TypeCon.!(tt.typeCon).genEnumToSet(x, this);
			}
			_ => {
				var facts = if(x.facts.V_NON_ZERO, Fact.O_NO_NULL_CHECK, Facts.NONE);
				return add(V3Op.newTypeCast(ft, tt), [x], facts);
			}
		}
	}
	def opIntConvert0(from: Type, to: Type, x: SsaInstr) -> SsaInstr {
		return opIntConvert(V3Op.newIntConvert(from, to), x);
	}
	def opIntConvert(op: Operator, x: SsaInstr) -> SsaInstr {
		return add(op, [x], N);
	}
	// TypeQuery<F, T>(x)
	def opTypeQuery(ft: Type, tt: Type, x: SsaInstr) -> SsaInstr {
		var cast = TypeSystem.newTypeQuery(ft, tt);
		if (cast.approx != CastOp.UNKNOWN_QUERY && SsaConst.?(x)) {
			cast = cast.eval(ft, tt, SsaConst.!(x).val);
		}
		match (cast.approx) {
			TRUE => return graph.trueConst();
			FALSE => return graph.falseConst();
			TRUE_X_NULL => return opNotEqual(ft, x, graph.nullConst(ft));
			FLOAT_QUERY_I => return pure(V3Op.opFloatQueryI(ft, tt), [x]);
			FLOAT_QUERY_D => return pure(V3Op.opFloatQueryD, [x]);
			INT_QUERY_F => {
				return add(V3Op.opIntQueryF(ft, tt), [x], Facts.NONE);
			}
			_ => {
				var facts = if(x.facts.V_NON_ZERO, Fact.O_NO_NULL_CHECK, Facts.NONE);
				return add(V3Op.newTypeQuery(ft, tt), [x], facts);
			}
		}
	}
	// TypeSubsume<F, T>(x)
	def opTypeSubsume(ft: Type, tt: Type, x: SsaInstr) -> SsaInstr {
 		if (ft == tt) return x; // fold TypeSubsume<T, T>(x)
 		if (tt == x.getType()) return x; // fold TypeSubsume<F, T>(x: T)
		match (tt.typeCon.kind) {
			V3Kind.CLASS => {
				if (ft == Null.TYPE) return graph.nullConst(tt);
				if (TypeSystem.isSubtype(ft, tt)) return x; // remove redundant subsume of class
			}
			V3Kind.INT => {
				if (ft.typeCon.kind != V3Kind.VARIANT && ft.typeCon.kind != V3Kind.ENUM) return opTypeCast(ft, tt, x);
			}
			V3Kind.TUPLE => {
				return opTypeCast(ft, tt, x);
			}
			V3Kind.ENUM_SET => {
				return V3EnumSet_TypeCon.!(tt.typeCon).genEnumToSet(x, this);
			}
			V3Kind.FLOAT => {
				return opTypeCast(ft, tt, x);
			}
		}
		if (SsaConst.?(x)) return graph.valConst(tt, SsaConst.!(x).val); // fold TypeSubsume(K)
 		return add(V3Op.newTypeSubsume(ft, tt), [x], x.facts);
	}
	// ConditionalThrow(exception, cond)
	def opConditionalThrow(ex: string, x: SsaInstr) -> SsaInstr {
		if (SsaConst.?(x)) {
			if (Bool.unbox(SsaConst.!(x).val)) return addThrow(source, ex);
			else return graph.nop();
		}
		return add(V3Op.newConditionalThrow(ex), [x], N);
	}
	def opArrayAlloc(arrayType: Type, x: SsaInstr) -> SsaInstr {
		return add(V3Op.newArrayAlloc(arrayType), [x], N);
	}
	def opArrayInit(arrayType: Type, x: Array<SsaInstr>) -> SsaInstr {
		return add(V3Op.newArrayInit(arrayType, x.length), x, Fact.V_NON_ZERO);
	}
	def opArrayGetElem(arrayType: Type, indexType: Type, facts: Fact.set, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		var it = IntType.!(indexType);
		if (it.width > 32) {
			y = opWideBoundsCheck(arrayType, it, x, y);
			facts |= Fact.O_NO_BOUNDS_CHECK;
		}
		return add(V3Op.newArrayGetElem(arrayType), [x, y], facts);
	}
	def opArraySetElem(arrayType: Type, indexType: Type, facts: Fact.set, x: SsaInstr, y: SsaInstr, z: SsaInstr) -> SsaInstr {
		var it = IntType.!(indexType);
		if (it.width > 32) {
			y = opWideBoundsCheck(arrayType, it, x, y);
			facts |= Fact.O_NO_BOUNDS_CHECK;
		}
		return add(V3Op.newArraySetElem(arrayType), [x, y, z], facts);
	}
	def opWideBoundsCheck(arrayType: Type, it: IntType, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		var l = opArrayGetLength(arrayType, x);
		l = opIntConvert0(Int.TYPE, it, l);
		var ut = Int.getType(false, it.width);
		var c = pure(ut.lookupInfix(V3Infix.Lteq, null), [l, y]);
		opConditionalThrow(V3Exception.BoundsCheck, c);
		y = opIntConvert0(it, Int.TYPE, y);
		return y;
	}
	// ArrayGetLength<T>(x)
	def opArrayGetLength(arrayType: Type, x: SsaInstr) -> SsaInstr {
		return add(V3Op.newArrayGetLength(arrayType), [x], Fact.O_FOLDABLE);
	}
	// ClassAlloc[new](args)
	def opClassAlloc(m: IrSpec, x: Array<SsaInstr>) -> SsaInstr {
		if (m.member == null) return add(V3Op.newEmptyClassAlloc(m.receiver), Ssa.NO_INSTRS, Fact.V_NON_ZERO);
		return add(V3Op.newClassAlloc(m), x, Fact.V_NON_ZERO);
	}
	// (Component|Class|Variant)GetField[f](x)
	def opGetField(f: IrSpec, x: SsaInstr) -> SsaInstr {
		var op: Operator;
		if (V3.isComponent(f.receiver)) op = V3Op.newComponentGetField(f);
		else if (V3.isVariant(f.receiver)) op = V3Op.newVariantGetField(f);
		else op = V3Op.newClassGetField(f);
		return add(op, [x], f.member.facts);
	}
	// ClassSetField[f](x, v)
	def opClassSetField(f: IrSpec, x: SsaInstr, v: SsaInstr, init: bool) -> SsaInstr {
		if (init) return add(V3Op.newClassInitField(f), [x, v], Fact.O_NO_NULL_CHECK);
		else return add(V3Op.newClassSetField(f), [x, v], Facts.NONE);
	}
	// ClassGetVirtual[m](x)
	def opClassGetVirtual(m: IrSpec, x: SsaInstr) -> SsaInstr {
		var facts = if(m.receiver.typeCon.kind != V3Kind.CLASS, Fact.O_NO_NULL_CHECK, Facts.NONE);
		return add(V3Op.newClassGetVirtual(m), [x], facts);
	}
	// ClassGetVirtFunc[m](x)
	def opClassGetVirtFunc(m: IrSpec, x: SsaInstr) -> SsaInstr {
		var facts = if(m.receiver.typeCon.kind != V3Kind.CLASS, Fact.O_NO_NULL_CHECK, Facts.NONE);
		return add(V3Op.newClassGetVirtFunc(m), [x], facts);
	}
	// Init[new]
	def opInit(meth: IrMethod) -> SsaInstr {
		return add(V3Op.newInit(meth), Ssa.NO_INSTRS, N);
	}
	// ComponentSetField[f](x, v)
	def opComponentSetField(f: IrSpec, x: SsaInstr, v: SsaInstr) -> SsaInstr {
		return add(V3Op.newComponentSetField(f), [x, v], N);
	}
	// (x, y)
	def opTupleCreate(tupleType: Type, x: Array<SsaInstr>) -> SsaInstr {
		return add(V3Op.newTupleCreate(tupleType), x, N);
	}
	// e.N
	def opTupleGetElem(tupleType: Type, i: int, x: SsaInstr) -> SsaInstr {
		return add(V3Op.newTupleGetElem(tupleType, i), [x], N);
	}
	// NullCheck(x)
	def opNullCheck(rtype: Type, x: SsaInstr) -> SsaInstr {
		if (V3.isVariant(rtype)) return x;  // XXX: ugly
		return add(V3Op.newNullCheck(x.getType()), [x], N);
	}
	// BoundsCheck(array, index)
	def opBoundsCheck(rtype: Type, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return add(V3Op.newBoundsCheck(rtype), [x, y], N);
	}
	// CallMethod[m](args)
	def opCallMethod(m: IrSpec, x: Array<SsaInstr>) -> SsaInstr {
		recordDirectCall(m);
		var facts = m.member.facts & Fact.O_PURE;
		var op = V3Op.newCallMethod(m);
		if (m.receiver.typeCon.kind == V3Kind.CLASS) {
			return add(op, x, facts);
		} else {
			return add(op, x, facts | Fact.O_NO_NULL_CHECK | Operators.facts(op.opcode));
		}
	}
	// CallClassVirtual[m](args)
	def opCallClassVirtual(m: IrSpec, x: Array<SsaInstr>) -> SsaInstr {
		var facts = m.member.facts & Fact.O_PURE;
		var op = V3Op.newCallClassVirtual(m);
		if (m.receiver.typeCon.kind == V3Kind.CLASS) {
			return add(op, x, facts);
		} else {
			return add(op, x, facts | Operators.facts(op.opcode));
		}
	}
	// CallClosure(args)
	def opCallClosure(ftype: Type, x: Array<SsaInstr>) -> SsaInstr {
		return add(V3Op.newCallClosure(ftype), x, N);
	}
	// CreateClosure[m](x)
	def opCreateClosure(m: IrSpec, x: SsaInstr) -> SsaInstr {
		return pure(V3Op.newCreateClosure(m, x.getType()), [x]);
	}
	def pure(op: Operator, args: Array<SsaInstr>) -> SsaApplyOp {
		return add(op, args, Fact.O_PURE);
	}
	def add(op: Operator, args: Array<SsaInstr>, facts: Fact.set) -> SsaApplyOp {
		if (op.opcode == Opcode.VstSugar) return V3.fail("unexpected sugar");
		var i = SsaApplyOp.new(source, op, args).setFact(facts | Operators.facts(op.opcode));
		append(i);
		return i;
	}
	def opIntAdd(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return opIntInfix(V3Infix.Add, x, y);
	}
	def opIntAnd(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return opIntInfix(V3Infix.And, x, y);
	}
	def opIntMul(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return opIntInfix(V3Infix.Mul, x, y);
	}
	def opIntLt(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return opIntInfix(V3Infix.Lt, x, y);
	}
	def opIntGteq(x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return opIntInfix(V3Infix.Lteq, y, x);
	}
	def opIntInfix(infix: byte, x: SsaInstr, y: SsaInstr) -> SsaInstr {
		return add(Int.TYPE.lookupInfix0(infix), [x, y], N);
	}
	def unreachable(t: Type) -> SsaInstr {
		return graph.nullConst(t);
	}
	def recordDirectCall(spec: IrSpec) {
		// overridden in VstSsaEnv
	}
}
