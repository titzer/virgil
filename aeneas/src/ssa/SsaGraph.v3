// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def CACHE_SIZE = 7;
def VOID_ENTRY = SsaConstCacheEntry.new(0, Void.TYPE, null, Fact.V_ZERO);
def TRUE_ENTRY = SsaConstCacheEntry.new(1, Bool.TYPE, Bool.TRUE, Fact.V_NON_ZERO);
def FALSE_ENTRY = SsaConstCacheEntry.new(2, Bool.TYPE, Bool.FALSE, Fact.V_ZERO);
def ZERO_ENTRY = SsaConstCacheEntry.new(3, Int.TYPE, null, Facts.V_DEFAULT);
def ONE_ENTRY = SsaConstCacheEntry.new(4, Int.TYPE, Int.ONE, Facts.intFacts(1));
def TWO_ENTRY = SsaConstCacheEntry.new(5, Int.TYPE, Int.TWO, Facts.intFacts(2));
def FOUR_ENTRY = SsaConstCacheEntry.new(6, Int.TYPE, Int.FOUR, Facts.intFacts(4));

class SsaConstCacheEntry(index: byte, vtype: Type, val: Val, facts: Fact.set) {
	def get(graph: SsaGraph) -> SsaConst {
		var v: SsaConst;
		if (graph.values != null) {
			v = graph.values[index];
			if (v != null) return v;
		}
		return graph.newSsaConst(index, vtype, val, facts);
	}
}
// Represents a graph of SSA code.
// Canonicalizes constants, contains parameters, etc.
class SsaGraph {
	var params: Array<SsaParam>;
	var returnType: Type;
	def startBlock = SsaBlock.new();
	var values: Array<SsaConst>;
	var valuesPos = CACHE_SIZE;
	var valueMap: HashMap<(Type, Val), int>; // XXX: make a custom hashmap for Ssa constants
	var markGen: int;		// marking generation
	var numValues: int;		// used in SSA interpreter

	new(params, returnType) { }

	def isMultiBlock() -> bool {
		return startBlock.succs().length > 0;
	}
	def intConst(v: int) -> SsaConst {
		match (v) {
			0 => return ZERO_ENTRY.get(this);
			1 => return ONE_ENTRY.get(this);
			2 => return TWO_ENTRY.get(this);
			4 => return FOUR_ENTRY.get(this);
		}
		return getConst(Int.TYPE, Int.box(v), Facts.intFacts(v));
	}
	def longConst(v: long) -> SsaConst {
		match (v) {
			0 => return ZERO_ENTRY.get(this);
			1 => return ONE_ENTRY.get(this);
			2 => return TWO_ENTRY.get(this);
			4 => return FOUR_ENTRY.get(this);
		}
		return getConst(Long.TYPE, Long.box(v), Facts.longFacts(v));
	}
	def boolConst(v: bool) -> SsaConst {
		return if(v, trueConst(), falseConst());
	}
	def trueConst() -> SsaConst {
		return TRUE_ENTRY.get(this);
	}
	def falseConst() -> SsaConst {
		return FALSE_ENTRY.get(this);
	}
	def zeroConst() -> SsaConst {
		return ZERO_ENTRY.get(this);
	}
	def oneConst() -> SsaConst {
		return ONE_ENTRY.get(this);
	}
	def twoConst() -> SsaConst {
		return TWO_ENTRY.get(this);
	}
	def fourConst() -> SsaConst {
		return FOUR_ENTRY.get(this);
	}
	def nullConst(t: Type) -> SsaConst {
		match (t.typeCon.kind) {
			V3Kind.BOOL => return falseConst();
			V3Kind.INT => {
				if (t == Int.TYPE) return zeroConst();
				if (IntType.!(t).width <= 32) return zeroConst();
				// XXX: long value ugliness
			}
			V3Kind.VOID => return nop();
		}
		return getConst(t, null, Facts.V_DEFAULT);
	}
	def valConst(t: Type, v: Val) -> SsaConst {
		if (v == null) return nullConst(t);
		if (Box<bool>.?(v)) return boolConst(Box<bool>.!(v).val);
		if (IntType.?(t)) {
			var it = IntType.!(t);
			if (it.width <= 32) {
				match (v) {
					x: Box<int> => return intConst(x.val);
				}
			} else {
				match (v) {
					x: Box<int> => return longConst(if(it.signed, x.val, u32.view(x.val)));
				}
			}
		}
		if (Record.?(v)) return getConst(Record.!(v).rtype, v, Fact.V_NON_ZERO);
		return getConst(t, v, Facts.NONE);
	}
	def recordConst(r: Record) -> SsaConst {
		return getConst(r.rtype, r, Fact.V_NON_ZERO);
	}
	def nullReceiver() -> SsaConst {
		return nullConst(AnyObject.TYPE);
	}
	def nop() -> SsaConst {
		return VOID_ENTRY.get(this);
	}
	def bfBlocks() -> Array<SsaBlock> {
		var queue = Vector<SsaBlock>.new().grow(10); // TUNABLE: queue size
		var mark = ++markGen;
		queue.put(startBlock);
		startBlock.mark = mark;
		// do breadth-first walk over all reachable blocks
		for (i < queue.length) {
			addSuccessors(queue[i], queue, mark);
		}
		for (i < queue.length) {
			queue[i].mark = 0;
		}
		return queue.extract();
	}
	def addSuccessors(block: SsaBlock, queue: Vector<SsaBlock>, mark: int) {
		for (e in block.succs()) {
			var b = e.dest;
			if (b != null && b.mark != mark) {
				queue.put(b);
				b.mark = mark;
			}
		}
	}
	private def getConst(t: Type, v: Val, facts: Fact.set) -> SsaConst {
		if (valueMap != null) {
			// search using the hashmap if there is one.
			var index = valueMap[t, v];
			if (index > 0) {
				var vi = values[index-1];
				if (vi != null) return vi;
			}
		} else if (values != null) {
			// linear search of existing values.
			for (i = CACHE_SIZE; i < values.length; i++) {
				var vi = values[i];
				if (vi != null && vi.vtype == t && Values.equal(vi.val, v)) return vi;
			}
		}
		return newSsaConst(valuesPos++, t, v, facts);
	}
	private def hash(t: Type, v: Val) -> int {
		return t.hash + Values.hash(v);
	}
	private def equal(a: (Type, Val), b: (Type, Val)) -> bool {
		return a.0 == b.0 && Values.equal(a.1, b.1);
	}
	def newSsaConst(index: int, t: Type, v: Val, facts: Fact.set) -> SsaConst {
		if (values == null) {
			// Allocate new values array.
			values = Array.new(CACHE_SIZE + 4);
		} else if (valuesPos == values.length) {
			// Overflow of values array.
			if (valueMap == null) {
				// Allocate hashmap on first grow.
				valueMap = HashMap.new(hash, equal);
				for (i < values.length) {
					var v = values[i];
					if (v != null) valueMap[(v.vtype, v.val)] = i + 1;
				}
			}
			values = Arrays.grow(values, values.length * 2);
		}
		if (valueMap != null) valueMap[(t, v)] = index + 1;
		var instr = SsaConst.new(t, v);
		instr.facts = facts;
		values[index] = instr;
		return instr;
	}
	def resetMarks(order: SsaBlockOrder) {
		if (values != null) {
			for (i in values) if (i != null) i.mark = -1;
		}
		if (order != null) {
			markGen += order.order.length;
			for (i < order.order.length) {
				var b = order.order[i].block;
				resetMarksInBlock(b);
				b.mark = i;
			}
		} else {
			for (b in bfBlocks()) resetMarksInBlock(b);
		}
	}
	def resetMarksInBlock(b: SsaBlock) {
		b.mark = -1;
		for (i = b.next; i != b; i = i.next) i.mark = -1;
	}
}
