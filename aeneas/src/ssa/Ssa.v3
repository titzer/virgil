// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Ssa items that can be placed into a block's linked list, including the
// basic block itself, instructions in the block, and the end.
class SsaLink {
	def uid: int = UID.next++;	// unique identification number
	var prev: SsaLink;
	var next: SsaLink;
	var mark: int = -1;		// fast mapping of instr->int
	var facts: Fact.set;		// computed dataflow facts

	def clearMark() { this.mark = -1; }
	def checkFact(set: Fact.set) -> bool { return facts >= set; }
	def setFact(set: Fact.set) -> this { facts |= set; }
	def setFactIf(cond: Fact.set, set: Fact.set) -> this {
		if (facts >= cond) facts |= set;
	}
	def insertBefore(t: SsaLink) {
		var p = t.prev;
		if (p != null) {
			p.next = this;
			this.prev = p;
		}
		t.prev = this;
		next = t;
	}
	def remove() {
		if (prev != null) prev.next = next;
		if (next != null) next.prev = prev;
		prev = next = null;
	}
}
// An instruction that produces a value, including dataflow facts and usages
class SsaInstr extends SsaLink {
	var inputs: Array<SsaDfEdge>;	// inputs to this instruction, if any
	var useList: SsaDfEdge;		// list of uses of this instruction
	var instrVal: SsaInstr;		// fast mapping of instr->instr
	var valueNum: int = -1;		// used by SsaInterpreter

	// constructor allocates and initializes dataflow edges
	new(a: Array<SsaInstr>) {
		setInputs(a);
	}
	// transform an array of instructions into dataflow edges
	def setInputs(a: Array<SsaInstr>) {
		if (inputs != null) kill(); // kill previous edges
		if (a != null && a.length > 0) {
			// initialize dataflow edges
			var d = Array<SsaDfEdge>.new(a.length);
			for (i < d.length) {
				d[i] = SsaDfEdge.new(this, a[i]);
			}
			inputs = d;
		} else {
			inputs = Ssa.NO_DF_EDGES;
		}
	}
	// replace this instruction with the specified instruction in all uses
	def replace(replacement: SsaInstr) {
		if (replacement == this) return; // nothing to do
		while (useList != null) {
			// update() has the side-effect of removing the use from this useList
			useList.update(replacement);
		}
	}
	// kill this instruction and remove it from the use lists of each input
	def kill() {
		for (j < inputs.length) inputs[j].update(null);
	}
	def input0() -> SsaInstr { return inputs[0].dest; }
	def input1() -> SsaInstr { return inputs[1].dest; }
	def optag() -> int { return -1; }
	def getType() -> Type;
	def unbox<T>() -> T {
		var d: T, val = SsaConst.!(this).val;
		return if(val == null, d, Box<T>.!(val).val);
	}
}
// Representation of a dataflow edge
class SsaDfEdge extends Edge<SsaInstr> {
	new(src: SsaInstr, dst: SsaInstr) super(src, dst) { }
	def getListHead(dest: SsaInstr) -> SsaDfEdge { return dest.useList; }
	def setListHead(dest: SsaInstr, e: Edge<SsaInstr>) { dest.useList = SsaDfEdge.!(e); }
	def isOnlyEdge() -> bool { return next == null && dest.useList == this; }
	def getDest() -> SsaInstr { return dest; }
}
// Representation of a control flow edge
class SsaCfEdge {
	def src: SsaEnd;
	var dest: SsaBlock;
	var desti: int;
	new(src, dest) {
		if (dest != null) desti = dest.addPred(this);
	}
	// Disconnect this edge from the destination.
	def disconnect() {
		if (dest != null && dest.preds[desti] == this) dest.preds[desti] = null;
		dest = null;
		desti = 0;
	}
	// Connect this edge to a new destination.
	def connect(nd: SsaBlock) {
		disconnect();
		dest = nd;
		desti = nd.addPred(this);
	}
	def connectI(nd: SsaBlock, index: int) {
		disconnect();
		dest = nd;
		desti = index;
		nd.preds[index] = this;
	}
	// Replace the {old} edge {A -> D} with {this -> D}.
	def replace(old: SsaCfEdge) {
		var odest = old.dest, odesti = old.desti;
		old.disconnect();
		disconnect();
		dest = odest;
		desti = odesti;
		dest.preds[desti] = this;
	}
}
// A parameter to this function
class SsaParam(index: int, vtype: Type) extends SsaInstr(null) {
	def getType() -> Type { return vtype; }
}
// An application of an operator
class SsaApplyOp extends SsaInstr {
	def source: Source;
	def op: Operator;
	new(source, op, a: Array<SsaInstr>) super(a) { }
	def optag() -> int { return op.opcode.tag; }
	def getType() -> Type { return op.sig.returnType(); }
}
class SsaCheckpoint extends SsaInstr {
	def source: Source;
	var breakpoint: bool;
	new(source, a: Array<SsaInstr>) super(a) { }
	def getType() -> Type { return Void.TYPE; }
}
// Support for instrumentation
class SsaProbe extends SsaInstr(Ssa.NO_INSTRS) {
	def getType() -> Type { return Void.TYPE; }
	def fire(i: SsaInterpreter) { }
	def dup() -> SsaProbe { return SsaProbe.new(); }
	def render(buf: StringBuilder) -> StringBuilder {
		return buf.puts("probe");
	}
}
// A new local variable
class SsaNewVar extends SsaInstr {
	def name: string;
	def vtype: Type;
	def index: int;
	new(name, vtype, index, a: Array<SsaInstr>) super(a) { }
	def getType() -> Type { return Void.TYPE; }
}
// Delete {n} local variables
class SsaDeleteVar(n: int) extends SsaInstr(null) {
	def getType() -> Type { return Void.TYPE; }
}
// Update a non-ssa vaiable's SsaInstr
class SsaUpdateVar extends SsaInstr {
	def index: int;
	new(index, a: Array<SsaInstr>) super(a) { }
	def getType() -> Type { return Void.TYPE; }
}
// A constant value, such as int #0
class SsaConst(vtype: Type, val: Val) extends SsaInstr(null) {
	def getType() -> Type { return vtype; }
}
// A phi, representing a merge in the dataflow
class SsaPhi extends SsaInstr {
	def vtype: Type;
	def block: SsaBlock;
	new(vtype, block, a: Array<SsaInstr>) super(a) { }
	def getType() -> Type { return vtype; }
}
// A basic block
class SsaBlock extends SsaLink {
	var preds: Array<SsaCfEdge> = Ssa.NO_CF_EDGES;
	new() {
		next = this;
		prev = this;
	}
	def addPred(p: SsaCfEdge) -> int {
		preds = Arrays.append(p, preds);
		return preds.length - 1;
	}
	def hasPred() -> bool {
		for (p in preds) if (p != null) return true;
		return false;
	}
	def prepend(i: SsaInstr) {
		i.insertBefore(next);
	}
	def append(i: SsaLink) {
		i.insertBefore(if(SsaEnd.?(prev), prev, this));
	}
	def appendN(i: SsaLink) {
		if (i == null) return;
		var last = this.prev;
		last.next = i;
		i.prev = last;
		last = i;
		while (last.next != null) last = last.next;
		last.next = this;
		this.prev = last;
	}
	def succ(index: int) -> SsaCfEdge {
		return succs()[index];
	}
	def succs() -> Array<SsaCfEdge> {
		return if(SsaEnd.?(prev), SsaEnd.!(prev).succs, Ssa.NO_CF_EDGES);
	}
	def end() -> SsaEnd {
		return if(SsaEnd.?(prev), SsaEnd.!(prev));
	}
	def empty() -> bool {
		for (i = next; i != this; i = i.next) {
			if (SsaPhi.?(i)) continue;
			if (SsaEnd.?(i)) return true;
			return false;
		}
		return false;
	}
	def hasPhis() -> bool {
		return SsaPhi.?(next);
	}
	def countDown(limit: int) -> int {
		var i = next;
		while (limit-- >= 0) {
			if (i == this) return limit;
			i = i.next;
		}
		return -1;
	}
	def count() -> int {
		var c = 0;
		for (i = next; i != this; i = i.next) c++;
		return c;
	}
}
// The endpoint of a block
class SsaEnd extends SsaInstr {
	var succs: Array<SsaCfEdge>;
	new(inputs: Array<SsaInstr>, dests: Array<SsaBlock>) super(inputs) {
		succs = Array<SsaCfEdge>.new(dests.length);
		for (i < succs.length) {
			succs[i] = SsaCfEdge.new(this, dests[i]);
		}
	}
	def getType() -> Type { return Void.TYPE; }
	def block() -> SsaBlock { return SsaBlock.!(next); }
}
// A throw of an exception
class SsaThrow extends SsaEnd {
	def source: Source;
	def exception: string;
	new(source, exception) super(Ssa.NO_INSTRS, Ssa.NO_BLOCKS) { }
}
// A return of values from a method
class SsaReturn extends SsaEnd {
	new(a: Array<SsaInstr>) super(a, Ssa.NO_BLOCKS) { }
}
// A two-way branch on a boolean.
class SsaIf extends SsaEnd {
	new(cond: SsaInstr, tblock: SsaBlock, fblock: SsaBlock) super([cond], [tblock, fblock]) { }
	def trueBlock() -> SsaBlock { return block().succ(0).dest; }
	def falseBlock() -> SsaBlock { return block().succ(1).dest; }
	def findSucc(v: Val) -> SsaCfEdge { return block().succ(if(Bool.unbox(v), 0, 1)); }
}
// A table-based switch statement.
class SsaSwitch extends SsaEnd {
	def keyType: Type;
	def maxValue: int;
	new(keyType, maxValue, cond: SsaInstr) super([cond], Array<SsaBlock>.new(2 + maxValue)) { }
	def findSucc(v: Val) -> SsaCfEdge {
		var iv = Int.unbox(v);
		if (iv < 0 || iv > maxValue) return succs[succs.length - 1];
		return block().succ(iv);
	}
	def default() -> SsaCfEdge { return succs[succs.length - 1]; }
	def size() -> int { return succs.length; }
}
// A goto
class SsaGoto extends SsaEnd {
	new(target: SsaBlock) super(Ssa.NO_INSTRS, [target]) { }
	def target() -> SsaBlock { return block().succ(0).dest; }
}
// Utility methods for dealing with the SSA representation
component Ssa {
	def NO_INSTRS: Array<SsaInstr> = [];
	def NO_DF_EDGES: Array<SsaDfEdge> = [];
	def NO_BLOCKS: Array<SsaBlock> = [];
	def NO_CF_EDGES: Array<SsaCfEdge> = [];

	def newMap<T>() -> PartialMap<SsaInstr, T> {
		return HashMap<SsaInstr, T>.new(SsaInstr.uid, SsaInstr.==);
	}
	def newBlockMap<T>() -> PartialMap<SsaBlock, T> {
		return HashMap<SsaBlock, T>.new(SsaBlock.uid, SsaBlock.==);
	}
	def isTrueConst(i: SsaInstr) -> bool {
		return SsaConst.?(i) && Bool.unbox(SsaConst.!(i).val);
	}
	def isFalseConst(i: SsaInstr) -> bool {
		return SsaConst.?(i) && !Bool.unbox(SsaConst.!(i).val);
	}
	def inputs(x: SsaInstr) -> Array<SsaInstr> {
		var r = Array<SsaInstr>.new(x.inputs.length);
		for (i < r.length) r[i] = x.inputs[i].dest;
		return r;
	}
	// Inserts an intermediate block between any control-flow edges from a
	// predecessor with more than one successor to a successor with more than
	// one predecessor.
	// i.e. {A -> B} becomes {A -> temp -> B} if {|A.succs| > 1 && |B.preds| > 1}
	def splitCriticalEdges(graph: SsaGraph) {
		split(graph.startBlock, ++graph.markGen);
	}
	private def split(block: SsaBlock, done: int) {
		if (block.mark >= done) return;
		block.mark = done;
		var succs = block.succs();
		for (edge in succs) split(edge.dest, done);
		if (succs.length <= 1) return;
		for (edge in succs) {
			var d = edge.dest;
			if (d.preds.length <= 1) continue;
			// create {temp}
			var temp = SsaBlock.new();
			// create new edge {temp} -> {d}
			var g = SsaGoto.new(null);
			var nedge = SsaCfEdge.new(g, null);
			nedge.dest = d;
			nedge.desti = edge.desti;
			d.preds[nedge.desti] = nedge;
			g.succs = [nedge];
			temp.append(g);
			// update edge {block} -> {temp}
			edge.dest = temp;
			edge.desti = temp.addPred(edge);
		}
	}
}
