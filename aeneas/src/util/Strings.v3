// Copyright 2010 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A component that contains a number of utility methods for manipulating
// strings, including hashing, comparing, etc.
component Strings {
	def hexMap = "0123456789ABCDEF";

	def hash(str: string) -> int {
		var hashval = str.length;
		for (c in str) hashval = hashval * 31 + c;
		return hashval;
	}
	def equal(arr1: string, arr2: string) -> bool {
		if (arr1 == arr2) return true;
		if (arr1.length != arr2.length) return false;
		for (i < arr1.length) {
			if (arr1[i] != arr2[i]) return false;
		}
		return true;
	}
	def newMap<V>() -> HashMap<string, V> {
		return HashMap.new(hash, equal);
	}
	// render an integer value as decimal into an array at the given position.
	// (there must be sufficient space at the end of the array)
	def renderDecimal(buf: Array<byte>, pos: int, val: int) -> int {
		if (val < 0) {
			// handle negative integers
			if (val == -2147483648) {
				// special case
				Arrays.copyInto("-2147483648", buf, pos);
				return pos + 11;
			}
			buf[pos++] = '-';
			val = 0 - val;
		}
		if (val < 10) {
			// most common case: small one-byte integer
			buf[pos] = hexMap[val];
			return pos + 1;
		} else if (val < 100) {
			// second most common case: two-byte integer
			buf[pos + 1] = hexMap[val % 10];
			buf[pos] = hexMap[val / 10];
			return pos + 2;
		} else {
			// convert decimal integer
			var nonZero = false;
			for (radix = 1000000000; radix > 0; radix = radix / 10) {
				var digit = val / radix;
				val = val % radix;
				if (digit != 0) nonZero = true;
				if (nonZero) buf[pos++] = hexMap[digit];
			}
			return pos;
		}
	}
	// render an integer as hexadecimal into the specified array
	def renderHex8(buf: Array<byte>, pos: int, val: int) -> int {
		var end = pos + 7;
		for (i < 8) {
			buf[end - i] = hexMap[val & 0xf];
			val = val >> 4;
		}
		return end + 1;
	}
	def render(render: StringBuffer -> StringBuffer) -> string {
		return render(StringBuffer.new()).toString();
	}
	def nop(buf: StringBuffer) -> StringBuffer {
		return buf;
	}
	def toBuffer(str: string) -> StringBuffer {
		return StringBuffer.new().puts(str);
	}
	def writeBuffer(fd: int, buf: StringBuffer) {
		System.fileWriteK(fd, buf.buffer, 0, buf.length);
	}
	def format1<A>(fmt: string, a: A) -> string {
		return StringBuffer.new().format1(fmt, a).toString();
	}
	def format2<A, B>(fmt: string, a: A, b: B) -> string {
		return StringBuffer.new().format2(fmt, a, b).toString();
	}
	def format3<A, B, C>(fmt: string, a: A, b: B, c: C) -> string {
		return StringBuffer.new().format3(fmt, a, b, c).toString();
	}
	def makeFileName(dir: string, fileName: string, ext: string) -> string {
		var buf = StringBuffer.new();
		if (dir != null) {
			buf.puts(dir);
			if (dir[dir.length - 1] != '/') buf.putc('/');
		}
		buf.puts(fileName);
		if (ext != null) buf.putc('.').puts(ext);
		return buf.toString();
	}
	def concat(a: string, b: string) -> string {
		var amax = a.length;
		var r = Array<byte>.new(amax + b.length);
		for (i < amax) r[i] = a[i];
		var bmax = b.length;
		for (i < bmax) r[i + amax] = b[i];
		return r;
	}
	def baseName(fileName: string) -> string {
		var start = 0, end = fileName.length;
		// skip any path separators
		for (i < end) {
			if (fileName[i] == '/') start = i + 1; // TODO: UNIX specific path parsing
		}
		// skip file extension
		for (j = end - 1; j >= start; j--) {
			if (fileName[j] == '.') { end = j; break; }
		}
		return Arrays.range(fileName, start, end);
	}
}
// A StringBuffer is a utility for building strings. It contains a number
// of methods for appending characters, integers, strings, or renderable
// functions (i.e. functions of type StringBuffer -> StringBuffer). It uses
// parameterized methods to allow calling formatX() methods with any type
// of arguments.
class StringBuffer {
	var buffer = Array<byte>.new(20); // TUNABLE: initial size of string buffer
	var length: int;

	// append a hexadecimal byte
	def appendHexByte(ch: byte) -> this {
		acquire(2);
		var l = length;
		buffer[l] = Strings.hexMap[ch >> 4];
		buffer[l + 1] = Strings.hexMap[ch & 0xF];
		length = l + 2;
	}
	// append a boolean
	def appendBool(val: bool) -> this {
		var x = if(val, puts("true"),  puts("false"));
	}
	// append a char, escaping special characters
	def appendEscapedChar(ch: byte) -> this {
		match (ch) {
			'\'' => puts("\\\'");
			'\"' => puts("\\\"");
			'\t' => puts("\\t");
			'\r' => puts("\\r");
			'\n' => puts("\\n");
			'\\' => puts("\\\\");
		} else if (ch <= '~' && ch >= ' ') {
			putc(ch);
		} else {
			// append a hex representation
			putc('\\');
			putc('x');
			putc(Strings.hexMap[ch >>> 4]);
			putc(Strings.hexMap[ch & 0xf]);
		}
	}
	// append a byte
	def putc(ch: byte) -> this {
		acquire(1);
		buffer[length++] = ch;
	}
	// append a string
	def puts(str: string) -> this {
		var max = str.length;
		acquire(max);
		var pos = length;
		for (i < max) buffer[pos + i] = str[i];
		length = pos + max;
	}
	// append an integer in decimal
	def puti(val: int) -> this {
		// check for common case of small, one-byte integer
		if (val >= 0 && val <= 9) {
			putc(Strings.hexMap[val]);
		} else {
			acquire(11);
			length = Strings.renderDecimal(buffer, length, val);
		}
	}
	// append an integer in 0x00000000 format
	def putx(val: int) -> this {
		acquire(10);
		buffer[length] = '0';
		buffer[length + 1] = 'x';
		Strings.renderHex8(buffer, length + 2, val);
		length = length + 10;
	}
	// append an integer in 00000000 format
	def putxn(val: int) -> this {
		acquire(8);
		Strings.renderHex8(buffer, length, val);
		length = length + 8;
	}
	// append a long in 0x0000000000000000 format
	def putxl(val: (int, int)) -> this {
		acquire(18);
		buffer[length] = '0';
		buffer[length + 1] = 'x';
		Strings.renderHex8(buffer, length + 2, val.0);
		Strings.renderHex8(buffer, length + 10, val.1);
		length = length + 18;
	}
	// append a long in 0x0000000000000000 format
	// TODO: print longs in decimal format
	def putl(val: long) -> this {
		acquire(18);
		buffer[length] = '0';
		buffer[length + 1] = 'x';
		Strings.renderHex8(buffer, length + 2, int.!(val >> 32));
		Strings.renderHex8(buffer, length + 10, int.!(val));
		length = length + 18;
	}
	// append a comma-separated list
	def appendCommaList<T>(list: List<T>, append: (T, StringBuffer) -> StringBuffer) -> this {
		if (list == null) return;
		append(list.head, this);
		for (l = list.tail; l != null; l = l.tail) {
			append(l.head, csp());
		}
	}
	// append a comma-separated list of array elements
	def appendArray<T>(array: Array<T>) -> this {
		for (i < array.length) {
			if (i > 0) csp();
			put1(array[i]);
		}
	}
	// append a single item of type string|int|byte|bool|(StringBuffer -> StringBuffer)
	def put1<A>(a: A) -> this {
		if (string.?(a)) puts(string.!(a));
		else if (int.?(a)) puti(int.!(a));
		else if (byte.?(a)) putc(byte.!(a));
		else if (bool.?(a)) appendBool(bool.!(a));
		else if (Poly.is<A, StringBuffer -> StringBuffer>(a)) {
			Poly.as<A, StringBuffer -> StringBuffer>(a)(this);
		} else if (u32.?(a)) putx(int.!(a));
		else if (long.?(a)) putl(long.!(a));
	}
	def format1<A>(fmt: string, a: A) -> this {
		format3(fmt, a, (), ());
	}
	def format2<A, B>(fmt: string, a: A, b: B) -> this {
		format3(fmt, a, b, ());
	}
	def format3<A, B, C>(fmt: string, a: A, b: B, c: C) -> this {
		var i = 0, max = fmt.length - 1, start = 0;
		while (i < max) {
			if (fmt[i] == '%') {
				appendRange(fmt, start, i);
				start = i + 2;
				var nc = fmt[i + 1];
				if (nc == '1') { put1(a); i = start; continue; }
				if (nc == '2') { put1(b); i = start; continue; }
				if (nc == '3') { put1(c); i = start; continue; }
				if (nc == '%') { start--; continue; }
			}
			i++;
		}
		appendRange(fmt, start, fmt.length);
	}
	private def appendRange(str: string, start: int, end: int) {
		acquire(end - start);
		var pos = length;
		for (i = start; i < end; i++) buffer[pos++] = str[i];
		length = pos;
	}
	def end() -> this {
	}
	// append a line terminator
	def ln() -> this {
		putc('\n');
	}
	// append a space
	def sp() -> this {
		putc(' ');
	}
	// append ", "
	def csp() -> this {
		puts(", ");
	}
	// append a tab
	def tab() -> this {
		putc('\t');
	}
	// reset the size of this buffer, reusing internal storage
	def reset() -> this {
		length = 0;
	}
	// pad the buffer to the specified size with the pad character
	def pad(ch: byte, nlen: int) -> this {
		var diff = nlen - length;
		if (diff <= 0) return;
		acquire(diff);
		for (i = length; i < nlen; i++) buffer[i] = ch;
		length = nlen;
	}
	// convert this buffer to a string
	def toString() -> string {
		return Arrays.range(buffer, 0, length);
	}
	// acquire at least n bytes of internal storage at the end of the array
	private def acquire(n: int) {
		var nlen = length + n, olen = buffer.length;
		if (nlen > olen) buffer = Arrays.copy(buffer, Array<byte>.new(nlen + olen));
	}
}
