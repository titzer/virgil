// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A utility for aligning integers to powers of 2.
class Alignment(size: u31) {
	def add: u31 = size - 1u31;
	def mask_u32: u32 = ~(u32.!(add));
	def mask_u64: u64 = ~(u64.!(add));
	def shift_u5 = u5.view(Ints.log(size));
	def shift_u6 = u6.view(shift_u5);

	def alignDown_i32(i: int) -> int {
		return i & int.view(mask_u32);
	}
	def alignDown_i64(i: long) -> long {
		return i & long.view(mask_u64);
	}
	def alignUp_i32(i: int) -> int {
		return (i + add) & int.view(mask_u32);
	}
	def alignUp_u32(i: u32) -> u32 {
		return (i + add) & mask_u32;
	}
	def alignUp_i64(i: long) -> long {
		return (i + add) & long.view(mask_u64);
	}
	def alignUp_u64(i: u64) -> u64 {
		return (i + add) & mask_u64;
	}
}
