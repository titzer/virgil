// Copyright 2017 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def XXX: Terminal;
def ENABLE_SHADOW_STACK = false;
class WasmCodeGen extends ArchCodeGen {
	def wasm: WasmProgram;
	def rt: MachRuntime;
	def stackgen = WasmStackInstrGen.new();
	var stackifier: MachStackifier;
	def matcher = SsaInstrMatcher.new();
	def spiller = ShadowStackSpiller.new();
	def cfgr = CfgRestructurer.new();
	def stackBlocks = Vector<(int, ArchInstr, ArchInstr)>.new();

	new(context: SsaContext, wasm: WasmProgram, rt, buffer: MachDataBuffer) super(context, wasm.mach, ArmMachRegs.regs, buffer) {
		anyReg = ArmMachRegs.GPR;
		stackgen.context = context;
		stackifier = MachStackifier.new(this);
	}
	def emitWasm(m: IrMethod, funcNum: int) {
		context.enterMethod(m);
		stackBlocks.length = 0;
		var order = SsaBlockOrder.new(m.ssa);
		reset(m.ssa, order, null);
		cfgr.context = context;
		var code = cfgr.gen(order);

		var out = if(context.shouldPrintMach(), this.getOutput());
		stackifier.out = if(Aeneas.PRINT_STACKIFY.get(), out);
		if (out != null) cfgr.print(out, context, regSet, code);
		if (Debug.PARANOID) cfgr.verify();

		// Reset locals and assign parameter spill slots.
		stackgen.reset(m, m.ssa.params, getVReg);
		// Select instructions for CFG code.
		blockEnd = cursor = ArchInstr.new(ArchInstrs.ARCH_END, []);
		for (i = code.length - 1; i >= 0; i--) {
			selectInstructionsForCfgInstr(code[i]);
			advanceCursor();
		}
		spiller.reset(this, order);
		if (out != null) {
			out.format1("wasm func #%1:", funcNum);
			out.outln();
			print();
			spiller.print();
		}
		// Spill livepoints to shadow stack
		if (ENABLE_SHADOW_STACK) {
			spiller.run();
			// Stackify the code after spills are inserted
			for (i < stackBlocks.length) {
				var t = stackBlocks[i];
				stackifier.stackify(t.0, t.1, t.2, false);
			}
		}
		// Assemble WASM bytecode into the buffer.
		var sizepos = buffer.skip_u4leb();
		var start = buffer.pos;
		stackgen.emitVarDecls(buffer);
		assembleInstrs();
		var size = buffer.pos - start;
		buffer.overwrite_u4leb(sizepos, size);
		cursor = null;
	}
	def selectInstructionsForCfgInstr(c: CfgInstr) {
		match (c) {
			Unreachable => {
				emit0(WasmOp.UNREACHABLE.opcode);
			}
			Fallthrough(bi, vals) => {
				context.block = bi.block;
				for (v in vals) use(v);
				emitN(ArchInstrs.ARCH_NOP);
			}
			Ret(r) => {
				context.block = r.block();
				for (e in r.inputs) use(e.dest);
				emitN(ArchInstrs.ARCH_RET);
			}
			Block(t, bi) => {
				blockEnd = cursor;
				emit1(WasmOp.BLOCK.opcode, useInt(t));
			}
			Loop(bi) => {
				emit1(WasmOp.LOOP.opcode, useInt(WasmType.Void));
				blockEnd = cursor;
			}
			Body(bi) => {
				var block = bi.block, prevEnd = blockEnd;
				context.block = block;
				gatherLivenessForBlock(block);
				selectInstructionsForBlock(block);
				if (ENABLE_SHADOW_STACK) {
					stackBlocks.put((block.uid, cursor, prevEnd));
				} else {
					stackifier.stackify(block.uid, cursor, prevEnd, false);
				}
				blockEnd = cursor;
			}
			If(split, val, t, join) => {
				context.block = split;
				blockEnd = cursor;
				emit2(WasmOp.IF.opcode, useInt(t), use(val));
			}
			Else => {
				emit0(WasmOp.ELSE.opcode);
				blockEnd = cursor;
			}
			End => {
				blockEnd = cursor;
				emit0(WasmOp.END.opcode);
			}
			Br(bi, reldepth) => {
				blockEnd = cursor;
				emit1(WasmOp.BR.opcode, useInt(reldepth));
			}
			BrIf(split, val, bi, reldepth) => {
				context.block = split;
				blockEnd = cursor;
				emit2(WasmOp.BR_IF.opcode, use(val), useInt(reldepth));
			}
			BrTable(split, val, min, reldepths) => {
				context.block = split;
				blockEnd = cursor;
				if (min != 0) {
					var t = newTmpVReg(WasmType.I32);
					emit3(WasmOp.I32_SUB.opcode, dfnv(t), use(val), use(context.graph.intConst(min)));
					usev(t);
				} else {
					use(val);
				}
				for (i < reldepths.length) useImm(Int.box(reldepths[i]));
				emitN(WasmOp.BR_TABLE.opcode);
			}
		}
	}
	def visitApply(block: SsaBlock, i: SsaApplyOp) {
		match (i.op.opcode) {
			IntAdd => emitIntBinop(i, WasmOp.I32_ADD, WasmOp.I64_ADD);
			IntSub => emitIntBinop(i, WasmOp.I32_SUB, WasmOp.I64_SUB);
			IntMul => emitIntBinop(i, WasmOp.I32_MUL, WasmOp.I64_MUL);
			IntDiv => emitIntBinopSU(i, WasmOp.I32_DIV_S, WasmOp.I32_DIV_U, WasmOp.I64_DIV_S, WasmOp.I64_DIV_U);
			IntMod => emitIntBinopSU(i, WasmOp.I32_REM_S, WasmOp.I32_REM_U, WasmOp.I64_REM_S, WasmOp.I64_REM_U);
			IntAnd => emitIntBinop(i, WasmOp.I32_AND, WasmOp.I64_AND);
			IntOr  => emitIntBinop(i, WasmOp.I32_OR, WasmOp.I64_OR);
			IntXor => emitIntBinop(i, WasmOp.I32_XOR, WasmOp.I64_XOR);
			IntShl => emitShift(i, WasmOp.I32_SHL, WasmOp.I64_SHL);
			IntSar => emitShift(i, WasmOp.I32_SHR_S, WasmOp.I64_SHR_S);
			IntShr => emitShift(i, WasmOp.I32_SHR_U, WasmOp.I64_SHR_U);
			IntConvert => emitIntConvert(i);
			IntCastF => emitIntCastF(i);
			IntViewF => emitFloatUnop(i, WasmOp.I32_REINTERPRET_F32, WasmOp.I64_REINTERPRET_F64);
			BoolAnd => emit3(WasmOp.I32_AND.opcode, dfn(i), use(i.input0()), use(i.input1()));
			BoolOr => emit3(WasmOp.I32_OR.opcode, dfn(i), use(i.input0()), use(i.input1()));
			BoolNot => {
				// XXX: match and invert comparisons
				emit3(WasmOp.I32_XOR.opcode, dfn(i), use(i.input0()), useIntConst(1));
			}
			IntTruncF =>            emitIntTruncF(i);
			FloatPromoteI =>	emitFloatPromoteI(i);
			FloatPromoteF => {
				emit2(WasmOp.F64_PROMOTE_F32.opcode, dfn(i), use(i.input0()));
			}
			FloatViewI => emitFloatUnop(i, WasmOp.F32_REINTERPRET_I32, WasmOp.F64_REINTERPRET_I64);
			FloatRoundI => emitFloatRoundI(i);
			FloatRound => emitFloatUnop(i, WasmOp.F32_NEAREST, WasmOp.F64_NEAREST);
			FloatRoundD => {
				emit2(WasmOp.F32_DEMOTE_F64.opcode, dfn(i), use(i.input0()));
			}
			FloatAdd => emitFloatBinop(i, WasmOp.F32_ADD, WasmOp.F64_ADD);
			FloatSub => emitFloatBinop(i, WasmOp.F32_SUB, WasmOp.F64_SUB);
			FloatMul => emitFloatBinop(i, WasmOp.F32_MUL, WasmOp.F64_MUL);
			FloatDiv => emitFloatBinop(i, WasmOp.F32_DIV, WasmOp.F64_DIV);
			FloatEq => emitFloatBinop(i, WasmOp.F32_EQ, WasmOp.F64_EQ);
			FloatNe => emitFloatBinop(i, WasmOp.F32_NE, WasmOp.F64_NE);
			FloatLt => emitFloatBinop(i, WasmOp.F32_LT, WasmOp.F64_LT);
			FloatLteq => emitFloatBinop(i, WasmOp.F32_LE, WasmOp.F64_LE);
			FloatAbs => emitFloatUnop(i, WasmOp.F32_ABS, WasmOp.F64_ABS);
			FloatCeil => emitFloatUnop(i, WasmOp.F32_CEIL, WasmOp.F64_CEIL);
			FloatFloor => emitFloatUnop(i, WasmOp.F32_FLOOR, WasmOp.F64_FLOOR);
			FloatSqrt => emitFloatUnop(i, WasmOp.F32_SQRT, WasmOp.F64_SQRT);
			BoolEq,
			RefEq,
			IntEq	=> emitEq(i);
			IntLt	=> emitIntBinopSU(i, WasmOp.I32_LT_S, WasmOp.I32_LT_U, WasmOp.I64_LT_S, WasmOp.I64_LT_U);
			IntLteq => emitIntBinopSU(i, WasmOp.I32_LE_S, WasmOp.I32_LE_U, WasmOp.I64_LE_S, WasmOp.I64_LE_U);
			UnsignedLt => emitBinop(i, WasmOp.I32_LT_U);
			UnsignedLteq => emitBinop(i, WasmOp.I32_LE_U);
			UnsignedGt => emitBinop(i, WasmOp.I32_GT_U);
			UnsignedGteq => emitBinop(i, WasmOp.I32_GE_U);
			PtrLoad => {
				var ty = i.op.typeArgs[1];
				var wop: WasmOp, size = mach.sizeOf(ty);
				if (ty.typeCon.kind == V3Kind.FLOAT) {
					var ftc = Float_TypeCon.!(ty.typeCon);
					wop = if(ftc.is64, WasmOp.F64_LOAD, WasmOp.F32_LOAD);
				} else {
					match (size) {
						1 => wop = if(V3.isSigned(ty), WasmOp.I32_LOAD8_S, WasmOp.I32_LOAD8_U);
						2 => wop = if(V3.isSigned(ty), WasmOp.I32_LOAD16_S, WasmOp.I32_LOAD16_U);
						4 => wop = WasmOp.I32_LOAD;
						8 => wop = WasmOp.I64_LOAD;
						_ => context.fail("invalid load size");
					}
				}
				var t = matchAddress(i.input0()), opcode = wop.opcode | ArchInstrs.FLAG_LOAD;
				if (t.1 == null) {
					emit3(opcode, dfn(i), useImm(t.0), use(context.graph.zeroConst()));
				} else {
					emit3(opcode, dfn(i), useImm(t.0), use(t.1));
				}
			}
			PtrStore => {
				var ty = i.op.typeArgs[1];
				var wop: WasmOp, size = mach.sizeOf(ty);
				if (ty.typeCon.kind == V3Kind.FLOAT) {
					var ftc = Float_TypeCon.!(ty.typeCon);
					wop = if(ftc.is64, WasmOp.F64_STORE, WasmOp.F32_STORE);
				} else {
					var l = isLong(i.input1().getType());  // handle store narrowing
					match (size) {
						1 => wop = if(l, WasmOp.I64_STORE8, WasmOp.I32_STORE8);
						2 => wop = if(l, WasmOp.I64_STORE16, WasmOp.I32_STORE16);
						4 => wop = if(l, WasmOp.I64_STORE32, WasmOp.I32_STORE);
						8 => wop = WasmOp.I64_STORE;
						_ => context.fail("invalid store size");
					}
				}
				var t = matchAddress(i.input0()), opcode = wop.opcode | ArchInstrs.FLAG_STORE;
				if (t.1 == null) {
					emit3(opcode, useImm(t.0), use(context.graph.zeroConst()), use(i.input1()));
				} else {
					emit3(opcode, useImm(t.0), use(t.1), use(i.input1()));
				}
			}
			PtrAdd => emitBinop(i, WasmOp.I32_ADD);
			PtrSub => emitBinop(i, WasmOp.I32_SUB);
			ConditionalThrow => {
				emit2(WasmOp.IF.opcode, useInt(WasmType.Void), use(i.inputs[0].dest));
				emitN(WasmOp.UNREACHABLE.opcode);
				emitN(WasmOp.END.opcode);
			}
			CallAddress => {
				dfnAll(i);
				kill(newLivepoint(), 0);
				var target = i.inputs[0].dest;
				if (SsaConst.?(target)) {
					var val = SsaConst.!(target).val, m: IrMethod;
					if (val == null) {  // call of a null address => always throws
						emitN(WasmOp.UNREACHABLE.opcode);
						return;
					}
					if (FuncVal.?(val)) m = FuncVal.!(val).memberRef.asMethod();
					else if (Address<IrMethod>.?(val)) m = Address<IrMethod>.!(val).val;
					else context.fail("constant target is not an address or function value");
					var start = if(V3.isComponent(m.receiver), 2, 1);
					useAll(i.inputs, start); // skip target and maybe receiver
					useInt(m.machIndex);
					emitN(WasmOp.CALL.opcode);
				} else {
					var sig = FuncType.!(i.op.typeArgs[0].nested.head).sig();
					var sigIndex = wasm.addSig(Void.TYPE, sig);
					useAll(i.inputs, 1);   // use arguments, except target
					use(i.inputs[0].dest); // target of the call comes last
					useInt(int.!(sigIndex));
					emitN(WasmOp.CALL_INDIRECT.opcode);
					wasm.containsCallIndirect = true;
				}
				// XXX: match Call(PtrLoad(PtrAdd(#meta, x))
			}
			Alloc => {
				emit3(WasmOp.CALL.opcode, dfn(i), use(i.inputs[0].dest), useInt(wasm.allocateStubFuncIndex));
			}
			MachSystemOp => context.fail("unimplemented MachSystemOp");
			_ => return context.fail1("unexpected opcode %1 in WASM codegen", i.op.opcode.name);
		}
	}
	def emitFloatUnop(i: SsaApplyOp, op32: WasmOp, op64: WasmOp) {
		var ftc = Float_TypeCon.!(i.op.typeArgs[0].typeCon);
		var op = if(ftc.is64, op64, op32);
		emit2(op.opcode, dfn(i), use(i.input0()));
	}
	def emitFloatBinop(i: SsaApplyOp, op32: WasmOp, op64: WasmOp) {
		var ftc = Float_TypeCon.!(i.op.typeArgs[0].typeCon);
		var op = if(ftc.is64, op64, op32);
		emit3(op.opcode, dfn(i), use(i.input0()), use(i.input1()));
	}
	def emitIntTruncF(i: SsaApplyOp) {
		var itt = IntType.!(i.op.typeArgs[1]);
		var ftc = Float_TypeCon.!(i.op.typeArgs[0].typeCon);
		var is64 = ftc.is64;
		var op: WasmExtOp;
		match (itt.rank) {
			I32 => op = if(is64, WasmExtOp.I32_TRUNC_SAT_F64_S, WasmExtOp.I32_TRUNC_SAT_F32_S);
			U32 => op = if(is64, WasmExtOp.I32_TRUNC_SAT_F64_U, WasmExtOp.I32_TRUNC_SAT_F32_U);
			I64 => op = if(is64, WasmExtOp.I64_TRUNC_SAT_F64_S, WasmExtOp.I64_TRUNC_SAT_F32_S);
			U64 => op = if(is64, WasmExtOp.I64_TRUNC_SAT_F64_U, WasmExtOp.I64_TRUNC_SAT_F32_U);
			_ => { // sub{i,u}{32,64} cases
				context.fail1("unexpected rank for IntTruncF", i.op.opcode.name);
			}
		}
		emit2(op.extopcode, dfn(i), use(i.input0()));
	}
	def emitFloatPromoteI(i: SsaApplyOp) {
		emitFloatRoundI(i);
	}
	def emitFloatRoundI(i: SsaApplyOp) {
		var ftc = Float_TypeCon.!(i.op.sig.returnType().typeCon);
		var ift = IntType.!(i.op.typeArgs[0]);
		match (ift.rank) {
			SUBI32, I32 => {
				var op = if(ftc.is64, WasmOp.F64_CONVERT_S_I32, WasmOp.F32_CONVERT_S_I32);
				emit2(op.opcode, dfn(i), use(i.input0()));
			}
			SUBU32, U32 => {
				var op = if(ftc.is64, WasmOp.F64_CONVERT_U_I32, WasmOp.F32_CONVERT_U_I32);
				emit2(op.opcode, dfn(i), use(i.input0()));
			}
			SUBI64, I64 => {
				var op = if(ftc.is64, WasmOp.F64_CONVERT_S_I64, WasmOp.F32_CONVERT_S_I64);
				emit2(op.opcode, dfn(i), use(i.input0()));
			}
			SUBU64, U64 => {
				var op = if(ftc.is64, WasmOp.F64_CONVERT_U_I64, WasmOp.F32_CONVERT_U_I64);
				emit2(op.opcode, dfn(i), use(i.input0()));
			}
		}
	}
	def emitEq(i: SsaApplyOp) {
		var l = i.op.typeArgs.length > 0 && isLong(i.op.typeArgs[0]);
		matcher.binop(i);
		if (matcher.yzero) {
			var op = if (l, WasmOp.I64_EQZ, WasmOp.I32_EQZ);
			emit2(op.opcode, dfn(i), use(matcher.x));
		} else {
			var op = if(l, WasmOp.I64_EQ, WasmOp.I32_EQ);
			emit3(op.opcode, dfn(i), use(matcher.x), use(matcher.y));
		}
	}
	def visitThrow(block: SsaBlock, i: SsaThrow) {
		emitN(WasmOp.UNREACHABLE.opcode); // TODO: record exception location
	}
	def visitIf(block: SsaBlock, i: SsaIf) {
		// do nothing; handled by CfgRestructurer
	}
	def visitSwitch(block: SsaBlock, i: SsaSwitch) {
		// do nothing; handled by CfgRestructurer
	}
	def visitGoto(block: SsaBlock, target: SsaGoto) {
		// do nothing; handled by CfgRestructurer
	}
	def visitReturn(block: SsaBlock, i: SsaReturn) {
		// do nothing; handled by CfgRestructurer
	}
	def emitIntConvert(i: SsaApplyOp) {
		var ft = IntType.!(i.op.sig.paramTypes[0]), tt = IntType.!(i.op.sig.returnType());
		// TODO: use IntRank
		if (ft.width > 32) {
			// Converting from 64-bit
			if (tt.width > 32) {
				// long-long conversion.
				if (tt.signed) {
					var t = newTmpVReg(WasmType.I64);
					emit3(WasmOp.I64_SHL.opcode, dfnv(t), use(i.input0()), useLongConst(64 - tt.width));
					emit3(WasmOp.I64_SHR_S.opcode, dfn(i), usev(t), useLongConst(64 - tt.width));
				} else {
					emit3(WasmOp.I64_AND.opcode, dfn(i), use(i.input0()), useConstant(context.graph.valConst(Long.TYPE, tt.max), 0));
				}
			} else if (tt.width == 32) {
				// long-int conversion.
				emit2(WasmOp.I32_WRAP_I64.opcode, dfn(i), use(i.input0()));
			} else {
				// long-sub-int conversion.
				var t1 = newTmpVReg(WasmType.I32);
				emit2(WasmOp.I32_WRAP_I64.opcode, dfnv(t1), use(i.input0()));
				if (tt.signed) {
					var t2 = newTmpVReg(WasmType.I32);
					emit3(WasmOp.I32_SHL.opcode, dfnv(t2), usev(t1), useIntConst(32 - tt.width));
					emit3(WasmOp.I32_SHR_S.opcode, dfn(i), usev(t2), useIntConst(32 - tt.width));
				} else {
					emit3(WasmOp.I32_AND.opcode, dfn(i), usev(t1), useConstant(context.graph.valConst(Int.TYPE, tt.max), 0));
				}
			}
		} else {
			// Converting from 32-bit
			if (tt.width <= 32) {
				// int-int conversion.
				if (tt.signed) {
					var t = newTmpVReg(WasmType.I32);
					emit3(WasmOp.I32_SHL.opcode, dfnv(t), use(i.input0()), useIntConst(32 - tt.width));
					emit3(WasmOp.I32_SHR_S.opcode, dfn(i), usev(t), useIntConst(32 - tt.width));
				} else {
					emit3(WasmOp.I32_AND.opcode, dfn(i), use(i.input0()), useConstant(context.graph.valConst(Int.TYPE, tt.max), 0));
				}
			} else {
				// int-long conversion.
				var op = if(ft.signed, WasmOp.I64_EXTEND_S_I32, WasmOp.I64_EXTEND_U_I32);
				emit2(op.opcode, dfn(i), use(i.input0()));
			}
		}
	}
	def emitIntCastF(i: SsaApplyOp) {
		var ftc = Float_TypeCon.!(i.op.typeArgs[0].typeCon);
		var itt = IntType.!(i.op.typeArgs[1]);
		var op: WasmOp;
		match (itt.rank) {
			SUBI32, I32 => op = if(ftc.is64, WasmOp.I32_TRUNC_S_F64, WasmOp.I32_TRUNC_S_F32);
			SUBU32, U32 => op = if(ftc.is64, WasmOp.I32_TRUNC_U_F64, WasmOp.I32_TRUNC_U_F32);
			SUBI64, I64 => op = if(ftc.is64, WasmOp.I64_TRUNC_S_F64, WasmOp.I64_TRUNC_S_F32);
			SUBU64, U64 => op = if(ftc.is64, WasmOp.I64_TRUNC_U_F64, WasmOp.I64_TRUNC_U_F32);
		}
		emit2(op.opcode, dfn(i), use(i.input0()));
	}
	def matchAddress(a: SsaInstr) -> (Val, SsaInstr) {
		if (SsaConst.?(a)) return (SsaConst.!(a).val, null);
		var add = cover(Opcode.PtrAdd, a);
		if (add != null) {
			var r = add.input1();
			if (SsaConst.?(r)) return (SsaConst.!(r).val, add.input0());
		}
		return (null, a);
	}
	def emitShift(i: SsaApplyOp, op32: WasmOp, op64: WasmOp) {
		if (IntType.!(i.op.typeArgs[0]).width <= 32) {
			emit3(op32.opcode, dfn(i), use(i.input0()), use(i.input1()));
		} else {
			// wasm requires a 64-bit shiftend
			var t = newTmpVReg(WasmType.I64);
			emit2(WasmOp.I64_EXTEND_U_I32.opcode, dfnv(t), use(i.input1()));
			emit3(op64.opcode, dfn(i), use(i.input0()), usev(t));
		}
	}
	def emitIntBinop(i: SsaApplyOp, op32: WasmOp, op64: WasmOp) {
		var op = if(isLong(i.op.typeArgs[0]), op64, op32);
		emit3(op.opcode, dfn(i), use(i.input0()), use(i.input1()));
	}
	def emitIntBinopSU(i: SsaApplyOp, op32s: WasmOp, op32u: WasmOp, op64s: WasmOp, op64u: WasmOp) {
		if (isSigned(i.op)) emitIntBinop(i, op32s, op64s);
		else emitIntBinop(i, op32u, op64u);
	}
	def emitBinop(i: SsaApplyOp, op: WasmOp) {
		emit3(op.opcode, dfn(i), use(i.input0()), use(i.input1()));
	}
	def newTmpVReg(wasmType: byte) -> VReg {
		return stackgen.allocSlot(newVReg(null), wasmType);
	}
	// Assembling support
	def assemble(opcode: int, a: Array<Operand>) {
		match (opcode) {
			ArchInstrs.ARCH_NOP => return;
			ArchInstrs.ARCH_RET => return buffer.i1(WasmOp.RETURN.opcode);
			ArchInstrs.ARCH_BLOCK => {
				context.block = toBlock(a[0]);
				return;
			}
			ArchInstrs.ARCH_END => return buffer.i1(WasmOp.END.opcode);
			ArchInstrs.ARCH_PARMOVE => return context.fail("parallel move instructions should be removed by stackifier");
		}
		if (opcode > 255) buffer.i2be(opcode);
		else buffer.i1(opcode);
		// emit immediates for specific opcodes
		match (opcode) {
			WasmOp.GET_LOCAL.opcode,
			WasmOp.SET_LOCAL.opcode,
			WasmOp.TEE_LOCAL.opcode => {
				var v = toVar(a[0]), slot = stackgen.slotOf(v);
				if (slot < 0) context.fail1("variable @%1 not allocated a spill slot", v.ssa.uid);
				buffer.i4leb(slot);
			}

			WasmOp.LOOP.opcode,
			WasmOp.BLOCK.opcode,
			WasmOp.IF.opcode => buffer.i1(toInt(a[0]));

			WasmOp.BR.opcode => buffer.u4leb(u32.!(toInt(a[0])));
			WasmOp.BR_IF.opcode => buffer.u4leb(u32.!(toInt(a[1])));
			WasmOp.BR_TABLE.opcode => {
				var count = a.length - 2;
				buffer.u4leb(u32.!(count));
				for (i = 1; i < a.length; i++) {
					buffer.u4leb(u32.!(toInt(a[i])));
				}
			}

			WasmOp.I32_LOAD8_S.opcode,
			WasmOp.I32_LOAD8_U.opcode,
			WasmOp.I64_LOAD8_S.opcode,
			WasmOp.I64_LOAD8_U.opcode => asmls(0, a, 1);

			WasmOp.I32_STORE8.opcode,
			WasmOp.I64_STORE8.opcode => asmls(0, a, 0);

			WasmOp.I32_LOAD16_S.opcode,
			WasmOp.I32_LOAD16_U.opcode,
			WasmOp.I64_LOAD16_S.opcode,
			WasmOp.I64_LOAD16_U.opcode => asmls(1, a, 1);

			WasmOp.I32_STORE16.opcode,
			WasmOp.I64_STORE16.opcode => asmls(1, a, 0);

			WasmOp.I32_LOAD.opcode,
			WasmOp.F32_LOAD.opcode => asmls(2, a, 1);

			WasmOp.I64_LOAD32_S.opcode,
			WasmOp.I64_LOAD32_U.opcode => asmls(3, a, 1);

			WasmOp.I32_STORE.opcode,
			WasmOp.F32_STORE.opcode => asmls(2, a, 0);

			WasmOp.I64_STORE32.opcode => asmls(2, a, 0);

			WasmOp.I64_LOAD.opcode,
			WasmOp.F64_LOAD.opcode => asmls(3, a, 1);

			WasmOp.I64_STORE.opcode,
			WasmOp.F64_STORE.opcode => asmls(3, a, 0);

			WasmOp.I64_CONST.opcode,
			WasmOp.I32_CONST.opcode => emitInt(a[0]);
			WasmOp.CALL.opcode => {
				emitInt(a[a.length - 1]); // function_index
			}
			WasmOp.F32_CONST.opcode => {
				var val = Operand.Immediate.!(a[0]).val;
				if (val == null) buffer.i4(0);
				else buffer.i4(i32.view(Float32Val.!(val).bits));
			}
			WasmOp.F64_CONST.opcode => {
				var val = Operand.Immediate.!(a[0]).val;
				if (val == null) buffer.i8(0);
				else buffer.i8(i64.view(Float64Val.!(val).bits));
			}
			WasmOp.CALL_INDIRECT.opcode => {
				emitInt(a[a.length - 1]); // type_index
				buffer.i1(0);	 // reserved
			}
		}
	}
	def emitAddr(addr: Addr) {
		buffer.recordPatch(addr, buffer.pos);
		buffer.skipN(5);
	}
	def emitInt(o: Operand) {
		var val = Operand.Immediate.!(o).val;
		if (val == null) {
			return buffer.i1(0);
		} else if (Addr.?(val)) {
			emitAddr(Addr.!(val));
		} else if (Box<int>.?(val)) {
			buffer.i4leb(Box<int>.!(val).val);
		} else if (Box<long>.?(val)) {
			buffer.i8leb(Box<long>.!(val).val);
		} else {
			context.fail1("cannot convert immediate to int: %1", V3.renderVal(val));
		}
	}
	def asmls(logAlign: int, a: Array<Operand>, offsetOperand: int) {
		buffer.i1(logAlign);	// flags = alignment
		emitInt(a[offsetOperand]);  // offset
	}
	def emitEntryStub() {
		def b = buffer.i1;
		var sizepos = buffer.pos;

		b(/*body_size=*/0);
		var start = buffer.pos;
		b(/*locals_count=*/0);

		// ============ entrypoint stub code =========================
		var m = rt.getRiInit();
		b(WasmOp.GET_LOCAL.opcode); b(0);
		b(WasmOp.CALL.opcode); buffer.i4leb(m.machIndex);
		var main = mach.prog.getMain().asMethod();
		if (main.sig.paramTypes.length == 0) {
			// main with no parameters => drop args
			b(WasmOp.DROP.opcode);
		}
		b(WasmOp.CALL.opcode); buffer.i4leb(main.machIndex);
		if (main.sig.returnTypes.length == 0) {
			// main with no return => emit i32.const #0
			b(WasmOp.I32_CONST.opcode);
			b(0);
		}
		b(WasmOp.END.opcode);
		// ============================================================
		var bodysize = buffer.pos - start;
		buffer.at(sizepos).i1(bodysize);
		buffer.atEnd();
	}
	def emitAllocationStub() {
		var gcmeth = rt.getRiGc();
		def b = buffer.i1;

		var sizepos = buffer.pos;
		b(/*body_size=*/0);
		var start = buffer.pos;

		def tSize = 0;
		def tNewHeapCur = 1;
		// A temporary local is needed to use the new heap cur twice
		b(/*locals_count=*/1);
		buffer.i4leb(1); buffer.i1(WasmType.I32);

		// ============ allocation stub code =========================
		genLoadRtAddr_i32(CiRuntimeModule.HEAP_CUR_LOC);
		b(WasmOp.I32_CONST.opcode); emitAddr(CiRuntimeModule.HEAP_CUR_LOC);
		genLoadRtAddr_i32(CiRuntimeModule.HEAP_CUR_LOC);
		b(WasmOp.GET_LOCAL.opcode); b(0);
		b(WasmOp.I32_ADD.opcode);
		b(WasmOp.TEE_LOCAL.opcode); b(tNewHeapCur);
		// if (newHeapCur > heapEnd)
		genLoadRtAddr_i32(CiRuntimeModule.HEAP_END_LOC);
		b(WasmOp.I32_GT_S.opcode);
		b(WasmOp.IF.opcode); b(WasmType.Void);
		if (gcmeth != null) {
			// return gcmeth(size);
			b(WasmOp.GET_LOCAL.opcode); b(tSize);
			b(WasmOp.CALL.opcode); buffer.i4leb(gcmeth.machIndex);
			b(WasmOp.RETURN.opcode);
		} else {
			// if no RiRuntime.gc() method, just crash.
			b(WasmOp.UNREACHABLE.opcode);
		}
		// end if
		b(WasmOp.END.opcode);
                b(WasmOp.GET_LOCAL.opcode); b(tNewHeapCur);
		b(WasmOp.I32_STORE.opcode); b(/*align=*/2); b(/*offset=*/0);
		b(WasmOp.END.opcode);
		// ============================================================
		var bodysize = buffer.pos - start;
		buffer.at(sizepos).i1(bodysize);
		buffer.atEnd();
	}
	def emitIndirectAdapter(sig: Signature, index: u32, m: IrMethod) {
		def b = buffer.i1;

		var sizepos = buffer.skip_u4leb(); /*body_size*/
		var start = buffer.pos;
		b(/*locals_count=*/0);

		// indirect adapters just drop the first parameter, so just load
		// all arguments except the first one and call the target
		// ============ indirect adapter code ========================
		for (i = 1; i < m.ssa.params.length; i++) {
			b(WasmOp.GET_LOCAL.opcode); buffer.i4leb(i);
		}
		b(WasmOp.CALL.opcode);
		buffer.i4leb(m.machIndex);
		b(WasmOp.END.opcode);
		// ===========================================================

		buffer.overwrite_u4leb(sizepos, buffer.pos - start);
		buffer.atEnd();
	}
	def genLoadRtAddr_i32(addr: CiRuntime_Address) {
		def b = buffer.i1;
		b(WasmOp.I32_CONST.opcode); emitAddr(addr);
		b(WasmOp.I32_LOAD.opcode); b(/*align=*/2); b(/*offset=*/0);
	}
	// Used by MachStackifier
	def genLoadLocal(v: VReg) {
		emit1(WasmOp.GET_LOCAL.opcode, usev(stackgen.mapVar(v)));
	}
	def genStoreLocal(v: VReg, pop: bool) {
		var opcode = if(pop, WasmOp.SET_LOCAL.opcode, WasmOp.TEE_LOCAL.opcode);
		emit1(opcode, dfnv(stackgen.mapVar(v)));
	}
	def genPop(v: VReg) {
		emit0(WasmOp.DROP.opcode);
	}
	def genLoadConst(t: Type, val: Val) {
		match (t.typeCon.kind) {
			V3Kind.ENUM,
			V3Kind.ENUM_SET,
			V3Kind.INT => {
				if (isLong(t)) {
					var signed = IntType.?(t) && IntType.!(t).signed;
					val = Long.box(Long.unboxSU(val, signed));
					emit1(WasmOp.I64_CONST.opcode, useImm(val));
				} else {
					emit1(WasmOp.I32_CONST.opcode, useImm(val));
				}
			}
			V3Kind.BOOL => {
				var truth = if(Values.equal(Bool.TRUE, val), Int.ONE);
				emit1(WasmOp.I32_CONST.opcode, useImm(truth));
			}
			V3Kind.FLOAT => {
				var ftc = Float_TypeCon.!(t.typeCon);
				var opcode = if(ftc.is64, WasmOp.F64_CONST.opcode, WasmOp.F32_CONST.opcode);
				emit1(opcode, useImm(val));
			}
			MachType.MACH_POINTER,
			V3Kind.FUNCREF,
			V3Kind.ANYFUNC,
			V3Kind.CLASS,
			V3Kind.ARRAY,
			V3Kind.VARIANT => {
				emit1(WasmOp.I32_CONST.opcode, useImm(val));
			}
			_ => context.fail1("unimplemented insertLoadConst() of type %1", t.render);
		}
	}
	// Used by ShadowStackSpiller
	def newShadowSpTmp() -> VReg {
		return newTmpVReg(WasmType.I32);
	}
	def genShadowStackAlloc(shadow_sp: VReg, slots: int) {
		if (!ENABLE_SHADOW_STACK) return;
		var tmp = newTmpVReg(WasmType.I32);
		emit3(WasmOp.I32_LOAD.opcode, dfnv(shadow_sp), useInt(0), useAddrConst(CiRuntimeModule.SHADOW_STACK_CUR_PTR));
		emit3(WasmOp.I32_ADD.opcode, dfnv(tmp), usev(shadow_sp), useIntConst(slots * mach.data.addressSize));
		emit3(WasmOp.I32_STORE.opcode, useInt(0), useAddrConst(CiRuntimeModule.SHADOW_STACK_CUR_PTR), usev(tmp));
	}
	def genShadowStackFree(shadow_sp: VReg, slots: int) {
		if (!ENABLE_SHADOW_STACK) return;
		emit3(WasmOp.I32_STORE.opcode, useInt(0), useAddrConst(CiRuntimeModule.SHADOW_STACK_CUR_PTR), usev(shadow_sp));
	}
	def genShadowStackSave(shadow_sp: VReg, v: VReg, slot: int) {
		if (!ENABLE_SHADOW_STACK) return;
		var offset = slot * mach.data.addressSize;
		emit3(WasmOp.I32_STORE.opcode, useInt(offset), usev(shadow_sp), usev(v));
	}
	def genShadowStackRestore(shadow_sp: VReg, v: VReg, slot: int) {
		if (!ENABLE_SHADOW_STACK) return;
		var offset = slot * mach.data.addressSize;
		emit3(WasmOp.I32_LOAD.opcode, dfnv(v), useInt(offset), usev(shadow_sp));
	}
	def getOutput() -> ArchInstrBuffer {
		if (out != null) return out;
		return out = WasmInstrBuffer.new(this, context.prog, regSet);
	}
}
class WasmInstrBuffer extends ArchInstrBuffer {
	new(codegen: ArchCodeGen, prog: Program, regSet: MachRegSet) super(codegen, prog, regSet) { }
	def putArchInstr(indent: int, i: ArchInstr) -> int {
		var opcode = int.!(i.opcode()), a = i.operands;
		var name = if(opcode > 255, WasmOpNames.extarray[opcode & 0xFF], WasmOpNames.array[opcode]);
		match (opcode) {
			WasmOp.IF.opcode,
			WasmOp.BLOCK.opcode,
			WasmOp.LOOP.opcode => {
				putIndent(indent);
				puts(name);
				puts(": ");
				var typ = WasmType.name(byte.!(codegen.toInt(a[0])));
				yellow().puts(typ).end();
				if (a.length > 1) sp().putOperand(a[1]);
				return indent+1;
			}
			WasmOp.BR.opcode => {
				putIndent(indent);
				puts(name);
				puts(" depth=");
				green().puti(codegen.toInt(a[0])).end();
				return indent;
			}
			WasmOp.BR_IF.opcode => {
				putIndent(indent);
				puts(name);
				sp();
				putOperand(a[0]);
				puts(" depth=");
				green().puti(codegen.toInt(a[1])).end();
				return indent;
			}
			WasmOp.I32_LOAD.opcode,
			WasmOp.I64_LOAD.opcode,
			WasmOp.F32_LOAD.opcode,
			WasmOp.F64_LOAD.opcode,
			WasmOp.I32_LOAD8_S.opcode,
			WasmOp.I32_LOAD8_U.opcode,
			WasmOp.I32_LOAD16_S.opcode,
			WasmOp.I32_LOAD16_U.opcode,
			WasmOp.I64_LOAD8_S.opcode,
			WasmOp.I64_LOAD8_U.opcode,
			WasmOp.I64_LOAD16_S.opcode,
			WasmOp.I64_LOAD16_U.opcode,
			WasmOp.I64_LOAD32_S.opcode,
			WasmOp.I64_LOAD32_U.opcode => {
				putIndent(indent);
				puts(name);
				puts(" offset=").green();
				V3.renderResult(codegen.toImm(a[1]), null, this);
				end().sp();
				if (Operand.Def.?(a[0])) putOperand(a[0]).sp();
				if (a.length > 2) putOperand(a[2]);
				return indent;
			}
			WasmOp.I32_STORE.opcode,
			WasmOp.I64_STORE.opcode,
			WasmOp.F32_STORE.opcode,
			WasmOp.F64_STORE.opcode,
			WasmOp.I32_STORE8.opcode,
			WasmOp.I32_STORE16.opcode,
			WasmOp.I64_STORE8.opcode,
			WasmOp.I64_STORE16.opcode,
			WasmOp.I64_STORE32.opcode => {
				putIndent(indent);
				puts(name);
				puts(" offset=").green();
				V3.renderResult(codegen.toImm(a[0]), null, this);
				end().sp();
				for (j = 1; j < a.length; j++) {
					if (j > 1) csp();
					putOperand(a[j]);
				}
				return indent;
			}
			WasmOp.ELSE.opcode => {
				var i = if(indent == 1, 1, indent - 1);
				putIndent(i);
				puts(name);
				return indent;
			}
			WasmOp.END.opcode => {
				var i = if(indent == 1, 1, indent - 1);
				putIndent(i);
				puts(name);
				return i;
			}
			_ => {
				if (name == null) return putSimpleInstr(indent, i);
				return putNamedInstr(indent, name, a);
			}
		}
	}
}
def isLong(t: Type) -> bool {
	if (IntType.?(t)) return IntType.!(t).width > 32;
	if (V3EnumSet_TypeCon.?(t.typeCon)) return V3.getEnumSetType(t).byteSize() > 4;
	return false;
}
// A stack instruction generator specifically for WASM.
// Since it may dynamically assign locals for machine variables, it manages this mapping
// as well.
class WasmStackInstrGen {
	var context: SsaContext;
	var params = 0;
	var i32_count = 0;
	var i64_count = 0;
	var f32_count = 0;
	var f64_count = 0;
	def reset(m: IrMethod, params: Array<SsaParam>, getVReg: SsaInstr -> VReg) {
		i32_count = 0;
		i64_count = 0;
		f32_count = 0;
		f64_count = 0;

		var slot = if(V3.isComponent(m.receiver), 0, 1);
		for (p in params) {
			var v = getVReg(p);
			v.spill = slot++;
			v.hint = WasmType.of(p.vtype);
		}
		this.params = slot - 1;
	}
	def mapVar(v: VReg) -> VReg {
		if (v.hint != 0) return v;
		return allocSlot(v, WasmType.of(v.ssa.getType()));
	}
	def allocSlot(v: VReg, wasmType: byte) -> VReg{
		var index = 0;
		match (v.hint = wasmType) {
			WasmType.I32 => index = ++i32_count;
			WasmType.I64 => index = ++i64_count;
			WasmType.F32 => index = ++f32_count;
			WasmType.F64 => index = ++f64_count;
			_ => return V3.fail1("invalid WasmType for variable @%1", v.ssa.uid);
		}
		v.spill = params + index;
		return v;
	}
	def slotOf(v: VReg) -> int {
		var slot = v.spill - 1;
		if (v.spill < 0) return -1;
		if (slot < params) return slot;
		if (v.hint == WasmType.I32) return slot;
		slot += i32_count;
		if (v.hint == WasmType.I64) return slot;
		slot += i64_count;
		if (v.hint == WasmType.F32) return slot;
		slot += f32_count;
		if (v.hint != WasmType.F64) return V3.fail1("invalid WasmType for variable @%1", v.ssa.uid);
		return slot;
	}
	def emitVarDecls(buffer: MachDataBuffer) {
		var count = 0;
		if (i32_count > 0) count++;
		if (i64_count > 0) count++;
		if (f32_count > 0) count++;
		if (f64_count > 0) count++;
		buffer.i1(count);
		if (i32_count > 0) { buffer.i4leb(i32_count); buffer.i1(WasmType.I32); }
		if (i64_count > 0) { buffer.i4leb(i64_count); buffer.i1(WasmType.I64); }
		if (f32_count > 0) { buffer.i4leb(f32_count); buffer.i1(WasmType.F32); }
		if (f64_count > 0) { buffer.i4leb(f64_count); buffer.i1(WasmType.F64); }
	}
}
