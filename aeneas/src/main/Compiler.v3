// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def makeMatcher(pattern: string) -> VstMatcher {
	if (pattern == null) return null;
	if (pattern.length > 0) return VstMatcher.new(false, pattern);
	return VstMatcher.new(true, "");
}
// Parser and collection of optimization settings, using a customized flag format
// -opt=none|all|unsafe|Opt|+Opt|-Opt
class OptimizationFlags {
	var map: PartialMap<string, bool>;
	var none: bool;
	var all: bool;
	var unsafe: bool;
	new(input: string, ERROR: ErrorGen) {
		if (input != null && input.length > 0) {
			map = Strings.newMap();
			var opt = Strings.concat("-opt=", input);
			var f = ParsedFile.new("flags", opt);
			var p = ParserState.new(f, ERROR, skip, null);
			parseFlags(p);
		}
	}
	def parseFlags(p: ParserState) {
		p.advance(5); // advance past "-opt="
		while (true) {
			var lastPos = p.curPos;
			parseFlag(p);
			if (p.curPos == lastPos) return; // didn't parse anything
			if (p.curByte == '\x00') return; // end of input
			if (p.opt1(',') < 0) return p.error(", expected");
		}
	}
	def parseFlag(p: ParserState) {
		var val = p.opt1('+') >= 0 || p.opt1('-') < 0;
		if (!Char.isIdentStart(p.curByte)) return p.error("optimization flag name expected");
		var d = p.input, q = p.curPos + 1;
		while (q < d.length) {
			if (!Char.isIdentMiddle(d(q))) break;
			q++;
		}
		return setFlag(p.token(q - p.curPos), val);
	}
	def setFlag(t: Token, val: bool) {
		if (Strings.equal("none", t.image)) none = true;
		else if (Strings.equal("all", t.image)) all = true;
		else if (Strings.equal("unsafe", t.image)) all = unsafe = true;
		else map.set(t.image, val);
	}
	def get(name: string, val: bool) -> bool {
		if (all) return true;
		if (none) return false; // all optimizations turned off
		if (map != null && map.has(name)) val = map.get(name); // check for flag
		return val; // return default
	}
	def getUnsafe(name: string) -> bool {
		return unsafe || (map != null && map.has(name));
	}
	def skip(p: ParserState) {
		// don't skip any characters
	}
}
// A stateless representation of a compiler that is configured once at startup
// and then can be reused to compile various programs.
class Compiler(target: Target) {
	def optError = ErrorGen.new(1);
	def flags = OptimizationFlags.new(Aeneas.OPT.get(), optError);
	def printMatcher = makeMatcher(Aeneas.PRINT_SSA.val);
	def statsMatcher = makeMatcher(Aeneas.PRINT_SSA_STATS.val);
	var icMon: (IrSpec, IcMethod) -> void;	 // monitor for IC generation
	// major phases of compilation
	var Trace			= Aeneas.TRACE.get();
	var TraceCalls			= Aeneas.TRACE_CALLS.get();
	var VstVerify			= true;
	var VstInit			= true;
	var GenVariantEqual		= false;
	var Reachability		= Aeneas.RA.get();
	// optimization settings
	var VstBranchOpt		= flags.get("VstBranchOpt", true);
	var VstControlOpt		= flags.get("VstControlOpt", true);
	var VstLoopOpt			= flags.get("VstLoopOpt", true);
	var PartialBoundsChecks 	= flags.get("PartialBoundsChecks", true);
	var PartialSpecialization	= flags.get("PartialSpecialization", false);
	var ChaDevirtualize		= flags.get("ChaDevirtualize", true);
	var RaDevirtualize		= flags.get("RaDevirtualize", true);
	var InlineEarly			= flags.get("InlineEarly", false);
	var LoadOptimize		= flags.get("LoadOptimize", true);
	var NewOptimizer		= flags.get("NewOptimizer", false);
	var EmitSwitch			= flags.get("EmitSwitch", true);
	// XXX: flags for folding, reduction, typechecks, dead code, phi simplification
	// XXX: implement CSE and code motion
	// unsafe optimization settings; for performance testing only
	var DisableBoundsChecks		= flags.getUnsafe("DisableBoundsChecks");
	var DisableNullChecks		= flags.getUnsafe("DisableNullChecks");
	var DisableTypeChecks		= flags.getUnsafe("DisableTypeChecks");
	var DisableLengthChecks		= flags.getUnsafe("DisableLengthChecks");

	def setArgs(prog: Program, args: Array<string>) {
		for (i = 0; true; i++) {
			if (i == args.length) {
				prog.files = args;
				prog.args = [];
				break;
			}
			var arg = args(i);
			var len = arg.length;
			if (len <= 3 || arg(len - 1) != '3' || arg(len - 2) != 'v' || arg(len - 3) != '.') {
				prog.files = Arrays.copy(args, Array<string>.new(i));
				prog.args = Arrays.range(args, i, args.length);
				break;
			}
		}
		prog.inputs = Array.new(prog.files.length);
	}
	// perform all phases of compilation
	def compile(prog: Program) -> bool {
		if (optError.numErrors > 0) {
			prog.ERROR.copy(optError);
			return false;
		}
		if (target != null) target.configure(this, prog);
		var c = Compilation.new(this, prog, target);
		return c.parse()
			&& (!VstVerify || c.verify())
			&& (!VstInit || c.init())
			&& (!Reachability || c.reachability())
			&& c.emit()
			&& prog.ERROR.noErrors;
	}
	def runMain(prog: Program) -> Result {
		var main = prog.getMain();
		if (main == null) return null;
		var invoker = getInterpreter(prog);
		return invoker(Closure.new(Values.BOTTOM, main), evalMainArgs(prog));
	}
	private def evalMainArgs(prog: Program) -> Array<Val> {
		var atypes = prog.getMain().getParamTypes();
		if (atypes.length == 1 && atypes(0) == V3.stringArrayType) {
			// box the argument as an array of strings
			var args = prog.args;
			var array = prog.newRecord(V3.stringArrayType, args.length);
			for (i < args.length) {
				array.values(i) = Byte.boxString(args(i), prog);
			}
			return [array];
		}
		return Array.new(atypes.length);	
	}
	def getInterpreter(prog: Program) -> (Closure, Array<Val>) -> Result {
		var interp = IcInterpreter.new(prog, genIc(prog, _));
		if (Trace) IcTracer.new(interp);
		if (TraceCalls != null) {
			var tracer = IcCallTracer.new(makeMatcher(TraceCalls));
			icMon = tracer.onGen;
		}
		return interp.invoke;
	}
	def genIc(prog: Program, memberRef: IrSpec) -> IcMethod {
		var icm: IcMethod;
		genSsa(prog, memberRef, 0);
		// XXX: cache the SsaContext for creating IC?
		icm = SsaIcGen.new(SsaContext.new(this, prog).enterSpec(memberRef)).generate();
		if (icMon != null) icMon(memberRef, icm);
		if (Aeneas.PRINT_IC.get()) IrPrinter.new(prog).doIc(icm);
		return icm;
	}
	def genSsa(prog: Program, memberRef: IrSpec, depth: int) -> SsaGraph {
		var meth = memberRef.asMethod();
		if (meth.ssa == null) {
			var context = SsaContext.new(this, prog).enterSpec(memberRef);
			var gen = VstSsaGen.new(context, prog.opBuilder);
			var graph = gen.generate();
			meth.ssa = graph;
			if (gen.directCallBlocks != null) {
				// Perform inlining.
				SsaEarlyInliner.new(context, gen).inline(depth);
			}
			if (VstControlOpt && graph.isMultiBlock()) {
				// Perform control flow optimizations.
				SsaCfOptimizer.new(context).optimize();
				context.printSsa("Optimized");
			}
			if (LoadOptimize) {
				// Perform load elimination.
				if (SsaLoadOptimizer.new(context).optimize()) context.printSsa("LoadOptimized");
			}
			if (NewOptimizer) {
				// Run the new post-pass optimizer.
				var opt = SsaOptimizer.new(context);
				opt.optGraph();
				context.printSsa("New Optimized");
			}
		}
		return meth.ssa;
	}
	def getOutputFileName(fileName: string, ext: string) -> string {
		return Strings.makeFileName(Aeneas.OUTPUT.get(), fileName, ext);
	}
	def makeExecutable(fileName: string) {
		if (Aeneas.SET_EXEC.get()) System.chmod(fileName, 493); // 0755 = rwxr-xr-x
	}
	def baseName(fileName: string) -> string {
		var start = 0, end = fileName.length;
		// skip any path separators
		for (i < end) {
			if (fileName(i) == '/') start = i + 1; // TODO: UNIX specific path parsing
		}
		// skip file extension
		for (j = end - 1; j >= start; j--) {
			if (fileName(j) == '.') { end = j; break; }
		}
		return Arrays.range(fileName, start, end);
	}
}
// phases of compilation, including parsing, typechecking, initialization, etc
class Compilation(compiler: Compiler, prog: Program, target: Target) {
	def parse() -> bool {
		prog.vst = VstPortion.new();
		var files = prog.files;
		var vec = Vector<VstFile>.new().grow(files.length);
		for (i = 0; i < files.length && prog.ERROR.notTooMany; i++) {
			var fileName = files(i), input = prog.inputs(i);
			if (input == null) {
				input = System.fileLoad(fileName);
				if (input == null) {
					prog.ERROR.FileNotFound(fileName);
					continue;
				}
				prog.inputs(i) = input;
			}
			vec.add(Parser.parseFile(fileName, input, prog.ERROR, prog.typeCache));
		}
		prog.vst.files = vec.extract();
		return prog.ERROR.noErrors;
	}
	def verify() -> bool {
		if (prog.ERROR.notTooMany) {
			Verifier.new(compiler, prog).verify();
			if (Aeneas.PRINT_VST.get()) VstPrinter.new().printProgram(prog);
		}
		return prog.ERROR.noErrors;
	}
	def init() -> bool {
		prog.initState = Array.new(prog.ir.init.length);
		prog.compRecords = Array.new(prog.vst.numComponents);
		prog.strRecords = Array.new(prog.vst.numStrings);
		for (list = prog.vst.declList; list != null; list = list.tail) {
			if (!VstComponent.?(list.head)) continue;
			var decl = VstComponent.!(list.head);
			var record = prog.newRecord(decl.getDeclaredType(), decl.numFields);
			prog.compRecords(decl.recordIndex) = record;
		}
		var invoker = compiler.getInterpreter(prog);
		for (i < prog.initState.length) {
			if (prog.initState(i) == InitState.Uninit) {
				prog.initState(i) = InitState.Running;
				if (!runInitMethod(prog.ir.init.get(i), invoker)) break;
				prog.initState(i) = InitState.Done;
			}
		}
		return prog.ERROR.noErrors;
	}
	def runInitMethod(meth: IrMethod, invoker: (Closure, Array<Val>) -> Result) -> bool {
		var ctype = meth.container;
		var newRef = IrSpec.new(ctype, [ctype], meth);
		var result = invoker(Closure.new(Values.BOTTOM, newRef), null);
		if (Exception.?(result)) {
			prog.ERROR.ExceptionInInitialization(meth.source, Exception.!(result));
			return false;
		}
		return true;
	}
	def reachability() -> bool {
		var main = prog.getMain();
		if (main == null) return false;
		var ra = ReachabilityAnalyzer.new(compiler, prog);
		var root = ra.addRootMethod(main);
		if (target != null) target.addRoots(compiler, prog, ra.addRootMethod);
		ra.analyze();
		if (Aeneas.PRINT_RA.get()) ra.dump();
		ra.transform();
		prog.ir.mainMethod = root.spec;
		return prog.ERROR.noErrors;
	}
	def emit() -> bool {
		if (target != null) {
			if (prog.getMain() == null) return false;
			target.emit(compiler, prog);
		}
		return true;
	}
}
// Configures compilation based on the properties of a target platform, including
// instruction set, operating system, etc.
class Target(name: string) {
	var typedMain: bool;

	def configure(compiler: Compiler, prog: Program) { }
	def emit(compiler: Compiler, prog: Program) { }
	def addRoots(compiler: Compiler, prog: Program, add: IrSpec -> RaRoot) { }
	def computeFieldOffsets(prog: Program, b: Vector<IrField>, start: int) {
		// XXX: is this the best place for computing field offsets?
		for (i = start; i < b.length; i++) {
			b.get(i).machOffset = i;
		}
	}
	def verifyMain(main: VstMethod, error: (FileRange, string) -> void) {
		if (!typedMain) return;
		var ftype = main.getType();
		var paramType = Function.getParamType(ftype);
		if (paramType != Void.TYPE && paramType != V3.stringArrayType) {
			error(main.token.range(), "Main method parameter type must be void|Array<string>");
		}
		var returnType = main.rettype.getType();
		match (returnType.typeCon.kind) {
			V3Kind.INT, V3Kind.BOOL, V3Kind.VOID: {}
		} else {
			error(main.rettype.range(), "Main method return type must be void|bool|byte|int");
		}
	}
}
