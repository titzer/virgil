// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Utility methods for dealing with range types in Virgil
component V3Range {
	def TYPECON = Simple_TypeCon.new("Range", Kind.RANGE, 1, TypeUtil.globalCache, RangeType.new(_, true, _, _));
	def RO_TYPECON = Simple_TypeCon.new("range", Kind.RANGE, 1, TypeUtil.globalCache, RangeType.new(_, false, _, _));
	def START_TYPE = TypeUtil.singleType("#RangeStart", Kind.RANGE_START, RangeStartType.new);

	def newType(elementType: Type) -> Type {
		return TYPECON.create1(elementType);
	}
}
// The representation of Range<T> and range<T> types.
class RangeType extends Type {
	def writeable: bool;
	new(hash: int, writeable, typeCon: TypeCon, nested: List<Type>) super(hash, typeCon, nested) { }
	def elementType() -> Type {
		return nested.head;
	}
}
// A value representing a (sub)range of an array; the record, the {start}, and the {length}; used before normalization.
class ArrayRangeVal(array: Record, start: int, length: int) extends Val {
	def equals(other: Val) -> bool {
		if (other == this) return true;
		if (other == null) return array == null && start == 0 && length == 0;
		if (!ArrayRangeVal.?(other)) return false;
		var that = ArrayRangeVal.!(other);
		return this.array == that.array && this.start == that.start && this.length == that.length;
	}
	def hash() -> int {
		var h = if(array != null, array.id);
		return h + start * 33 + length * 97;
	}
}
// A value representing the start of a range of an array; used after normalization.
class ArrayRangeStart(start: int, arrayType: Type) extends Val {
	def equals(other: Val) -> bool {
		if (other == this) return true;
		if (other == null) return false;
		if (!ArrayRangeStart.?(other)) return false;
		var that = ArrayRangeStart.!(other);
		return this.start == that.start && this.arrayType == that.arrayType;
	}
	def hash() -> int {
		return start * 65 + arrayType.hash * 37;
	}
}
// A value representing the start of a range of a pointer; used after normalization.
class PointerRangeStart(start: Addr) extends Val {
	def equals(other: Val) -> bool {
		if (other == this) return true;
		if (other == null) return start == null || start.equals(null);
		if (!PointerRangeStart.?(other)) return false;
		var that = PointerRangeStart.!(other);
		return this.start.equals(that.start);
	}
	def hash() -> int {
		return start.hash() * 65;
	}
}
// The type of the start of a range (either the range of an array of a pointer); used after normalization.
class RangeStartType extends Type {
	new(typeCon: TypeCon) super(typeCon.uid, typeCon, null) { }
}