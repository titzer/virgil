// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

component V3Kind {
	def VOID	= 0;
	def BOOL	= 1;
	def INT		= 2;
	def FLOAT	= 3;
	def ARRAY	= 4;
	def COMPONENT	= 5;
	def CLASS	= 6;
	def CLOSURE	= 7;
	def TUPLE	= 8;
	def TYPE_PARAM	= 9;
	def TYPE_VAR	= 10;
	def FUNCREF	= 11;
	def ANYFUNC	= 12;
	def SET		= 13;
	def VARIANT	= 14;
	def ENUM	= 15;
	def ENUM_SET	= 16;

	new() {
		// prevent inconsistencies with Type infrastructure
		if (TYPE_VAR != TypeUtil.TYPE_VAR_KIND) return V3.fail("kind number does not match");
		if (TYPE_PARAM != TypeUtil.TYPE_PARAM_KIND) return V3.fail("kind number does not match");
	}
}
// Utilities related to the Virgil III source language, including common types,
// type queries, name mangling, etc.
component V3 {
	def stringType      = V3Array.newType(Byte.TYPE);
	def stringTypeCon   = TypeUtil.aliasTypeCon("string", stringType);
	def stringArrayType = V3Array.newType(stringType);
	def voidArrayType   = V3Array.newType(Void.TYPE);

	// For performance and to avoid pre-allocating all the fixed-size integer types,
	// we use a custom lookup routine for the top level types.
	def lookupToplevelType(name: string) -> TypeCon {
		match(name[0]) {
			'A' => if (Strings.equal("Array", name)) return V3Array.TYPECON;
			'b' => {
				if (Strings.equal("bool", name)) return Bool.TYPE.typeCon;
				if (Strings.equal("byte", name)) return Byte.TYPE.typeCon;
			}
			'd' => {
				if (Aeneas.FLOAT.val && Strings.equal("double", name)) return Float.FLOAT64.typeCon;
			}
			'i' => {
				if (Strings.equal("int", name)) return Int.TYPE.typeCon;
				return Int.lookupFsi(name, true);
			}
			'f' => {
				if (Aeneas.FLOAT.val && Strings.equal("float", name)) return Float.FLOAT32.typeCon;
			}
			'l' => {
				if (Strings.equal("long", name)) return Long.TYPE.typeCon;
			}
			's' => {
				if (Strings.equal("string", name)) return stringTypeCon;
				if (Strings.equal("short", name)) return Int.getTypeCon(true, 16);
			}
			'u' => return Int.lookupFsi(name, false);
			'v' => if (Strings.equal("void", name)) return Void.TYPE.typeCon;
		}
		return null;
	}
	def isClass(t: Type) -> bool { return t != null && V3Class_TypeCon.?(t.typeCon); }
	def isComponent(t: Type) -> bool { return t != null && V3Component_TypeCon.?(t.typeCon); }
	def isFunction(t: Type) -> bool { return t != null && Function_TypeCon.?(t.typeCon); }
	def isArray(t: Type) -> bool { return t != null && t.typeCon == V3Array.TYPECON; }
	def isVariant(t: Type) -> bool { return t != null && t.typeCon.kind == V3Kind.VARIANT; }
	def isTuple(t: Type) -> bool { return t != null && t.typeCon == Tuple.TYPECON; }
	def isEnum(t: Type) -> bool { return t != null && t.typeCon.kind == V3Kind.ENUM; }
	def isEnumSet(t: Type) -> bool { return t != null && t.typeCon.kind == V3Kind.ENUM_SET; }
	def isString(t: Type) -> bool { return t == stringType; }
	def classDecl(t: Type) -> VstClass {
		return ClassType.!(t).classDecl;
	}
	def componentDecl(t: Type) -> VstComponent {
		return V3Component_TypeCon.!(t.typeCon).componentDecl;
	}
	def isSigned(t: Type) -> bool {
		return if(IntType.?(t), IntType.!(t).signed);
	}
	def isDouble(t: Type) -> bool {
		return Float_TypeCon.!(t.typeCon).is64;
	}
	def getSyntheticVariantType(t: Type, vcase: VstCaseMember) -> Type {
		return vcase.decl.typeCon.create(t.nested);
	}
	def makeDefaultVariantRecord(prog: Program, t: Type) -> Record {
		var cases = ClassType.!(getRootType(t)).classDecl.cases;
		if (cases.length > 0) t = cases[0].decl.typeCon.create(t.nested);
		var len = prog.ir.makeIrClass(t).fields.length;
		return prog.newRecord(t, len);
	}
	def getVariantTag(t: Type) -> int {
		var tag = ClassType.!(t).classDecl.variantTag;
		return if(tag >= 0, tag);
	}
	def getVariantTagType(t: Type) -> IntType {
		return ClassType.!(getRootType(t)).classDecl.tagType;
	}
	def getEnumSetType(t: Type) -> IntType {
		return V3EnumSet_TypeCon.!(t.typeCon).intType;
	}
	def getRootType(t: Type) -> Type {
		while (true) {
			var s = getSuperType(t);
			if (s == null) return t;
			t = s;
		}
		return t;
	}
	def getSuperType(t: Type) -> Type {
		// inlined and optimized super type lookup
		return if(ClassType.?(t), ClassType.!(t).getSuperType());
	}
	def getTypeArgs(t: Type) -> TypeArgs {
		// inlined and optimized type arguments lookup
		if (t == null) return null;
		if (t.nested == null) return null;
		return if(ClassType.?(t), ClassType.!(t).getTypeArgs());
	}
	def mangleIrMember(m: IrMember) -> string {
		var buffer = m.render(StringBuilder.new());
		return Mangle.mangleB(buffer, StringBuilder.new()).toString();
	}
	def newRecordMap<T>() -> HashMap<Record, T> {
		return HashMap.new(Record.id, Record.==);
	}
	def renderResult(result: Result, vtype: Type, buf: StringBuilder) -> StringBuilder {
		if (buf == null) buf = StringBuilder.new();
		if (Exception.?(result)) {
			var except = Exception.!(result);
			if (except.msg != null) buf.put2("!%s: %s\n", except.error, except.msg);
			else buf.put1("!%s\n", except.error);

			for (list = except.trace; list != null; list = list.tail) {
				for (s = list.head; s != null; s = s.tail) {
					buf.put1("\t%q\n", s.render);
				}
			}
			return buf;
		}
		var val = Val.!(result);
		if (val == Values.BOTTOM) {
			renderBottom(vtype, buf);
			return buf;
		} else if (vtype == Byte.TYPE) {
			renderByte(Byte.unbox(val), buf);
			return buf;
		}
		match (val) {
			x: Box<int> => buf.putd(x.val);
			x: Box<long> => buf.putd(x.val);
			x: Box<bool> => buf.putz(x.val);
			x: Record => buf.put2("#%d:%q", x.id, x.rtype.render);
			x: TupleVal => {
				var vals = x.values;
				var list: List<Type> = null;
				if (vtype != null) list = vtype.nested;
				buf.putc('(');
				for (i < vals.length) {
					if (i > 0) buf.csp();
					if (list != null) {
						renderResult(vals[i], list.head, buf);
						list = list.tail;
					} else {
						renderResult(vals[i], null, buf);
					}
				}
				buf.putc(')');
			}
			x: Closure => {
				renderResult(x.val, null, buf);
				buf.putc(':');
				x.memberRef.render(buf);
			}
			x: FuncVal => x.memberRef.render(buf.putc('&'));
			x: Address<IrMethod> => addOffset(buf.put1("&%q", x.val.renderLong), x);
			x: Address<IrMtable> => addOffset(buf.put1("&mt(%q)", x.val.render), x);
			x: Address<Record> => addOffset(buf.put1("&#%d", x.val.id), x);
			x: Address<IrField> => addOffset(buf.put1("&%q", x.val.renderLong), x);
			x: Address<string> => addOffset(buf.put1("&%s", x.val), x);
			x: Address<long> => addOffset(buf.put1("&long=0x%x", x.val), x);
			x: CiRuntime_Address => addOffset(buf.put1("&CiRuntime.%s", x.name), x);
			x: Addr => addOffset(buf.putc('&'), x);
			x: Float32Val => buf.puts("f32:").putx(x.bits);
			x: Float64Val => buf.puts("f64:").putx(x.bits);
			_ => buf.puts("<unknown value>");
		}
		return buf;
	}
	def renderVal(val: Val) -> string {
		return renderResult(val, null, null).toString();
	}
	def unboxIntegral(v: Val) -> int {
		if (v == null) return 0;
		if (Box<int>.?(v)) return Box<int>.!(v).val;
		if (Box<bool>.?(v)) return if(Bool.unbox(v), 1, 0);
		return fail("expected integral value");
	}
	def fail<T>(msg: string) -> T {
		var v: T;
		System.error(V3Exception.InternalError, msg);
		return v;
	}
	def fail1<T, A>(msg: string, p: A) -> T {
		return fail(Strings.format1(msg, p));
	}
	def unimplemented<T>() -> T {
		return fail("unimplemented");
	}
}
def addOffset(buf: StringBuilder, addr: Addr) {
	var name = if(addr.region == null, "data", addr.region.name);
	buf.put1("[%s", name);
	if (addr.offset < 0) buf.puts(" - ").putd(0 - addr.offset);
	if (addr.offset > 0) buf.puts(" + ").putd(addr.offset);
	buf.puts("]");
}
def renderByte(ch: byte, buf: StringBuilder) -> bool {
	Char.renderEscaped(ch, buf.putc('\'')).putc('\'');
	return true;
}
def renderBottom(vtype: Type, buf: StringBuilder) {
	if (vtype == null) {
		buf.puts("<null>");
		return;
	}
	var str: string;
	match (vtype.typeCon.kind) {
		V3Kind.VOID => str = "void";
		V3Kind.BOOL => str = "false";
		V3Kind.INT => str = "0";
		V3Kind.FLOAT => str = "float:0";
		V3Kind.CLASS, V3Kind.ARRAY, V3Kind.CLOSURE => str = "null";
		V3Kind.COMPONENT => str = V3.componentDecl(vtype).name();
		V3Kind.TUPLE => {
			buf.putc('(');
			for (list = vtype.nested; list != null; list = list.tail) {
				if (list != vtype.nested) buf.csp();
				V3.renderResult(Values.BOTTOM, list.head, buf);
			}
			buf.putc(')');
		}
		V3Kind.VARIANT => {
			vtype.render(buf);
			str = ".null";
		}
	}
	buf.puts(str);
}

// Constants for exception names
component V3Exception {
	def NullCheck     = "NullCheckException";
	def TypeCheck     = "TypeCheckException";
	def BoundsCheck   = "BoundsCheckException";
	def LengthCheck   = "LengthCheckException";
	def Unimplemented = "UnimplementedException";
	def DivideByZero  = "DivideByZeroException";
	def InternalError = "InternalError";
	def HeapOverflow  = "HeapOverflow";
	def FatalCall     = "FatalCall";
}
