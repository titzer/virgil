// Copyright 2011 Google Inc. All rights reserved.
// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A specialized DataWriter with machine-level properties, such as a start address
// and the ability to encode an address that is tracked for later patching.
class MachDataWriter extends DataWriter {
	def pageAlign: Alignment;
	var startPos: int; // start position from which offset() is calculated
	var refs: List<(Addr, /*posAddr:*/u64, /*pos:*/int)>;
	var arm64Refs: List<(Addr, Arm64PatchKind, /*posAddr:*/u64, /*pos:*/int)>;

	new(pageAlign, startAddr: u64, initialSize: int) {
		grow(initialSize);
		this.startAddr = startAddr;
	}

	def recordArm64Patch(addr: Addr, kind: Arm64PatchKind, pos: int) {
		def posAddr = this.addr();
		arm64Refs = List.new((addr, kind, posAddr, pos), arm64Refs);
	}
	def recordPatch(addr: Addr, pos: int) {
		var posAddr = this.addr();
		refs = List.new((addr, posAddr, pos), refs);
	}
	def patch(f: (MachDataWriter, Addr, u64) -> void) {
		// patch address locations with user function
		for (l = refs; l != null; l = l.tail) {
			var addr = l.head.0, posAddr = l.head.1, pos = l.head.2;
			f(at(pos), addr, posAddr);
		}
	}
	def patchArm64(f: (MachDataWriter, Addr, Arm64PatchKind, u64) -> void) {
		for (l = arm64Refs; l != null; l = l.tail) {
			var addr = l.head.0, kind = l.head.1, posAddr = l.head.2, pos = l.head.3;
			f(at(pos), addr, kind, posAddr);
		}	
	}
	def abs4(addr: Addr, encoding: int) {
		recordPatch(addr, pos);
		put_b32(encoding);
	}
	def skipPage() {
		startAddr = startAddr + u64.!(pageAlign.size);
	}
	def endPageAddr() -> u64 {
		return pageAlign.alignDown64(endAddr());
	}
	def putmd(w: MachDataWriter) {
		var p = this.pos;
		putr(w.alias());
		for (l = w.refs; l != null; l = l.tail) {
			var t = l.head;
			refs = List.new((t.0, t.1 + u64.!(p), t.2 + p), refs);
		}
		w.refs = null;
	}
	def offset() -> int {
		return pos - startPos;
	}
	def endOffset() -> int {
		return end() - startPos;
	}
	def bind(addr: Addr) {
		addr.absolute = int.!(this.addr()); // TODO(64)
	}
	def bindSize(addr: Addr) {
		addr.size = int.!(startAddr - u64.!(addr.absolute)); // TODO(64)
	}
}
