// Copyright 2011 Google Inc. All rights reserved.
// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A specialized DataWriter with machine-level properties, such as a start address
// and the ability to encode an address that is tracked for later patching.
class MachDataWriter extends DataWriter {
	def pageAlign: Alignment;
	var startPos: int; // start position from which offset() is calculated
	var refs: List<(Addr, /*posAddr:*/int, /*pos:*/int)>;
	var arm64Refs: List<(Addr, Arm64PatchKind, /*posAddr:*/int, /*pos:*/int)>;

	new(pageAlign, startAddr: u64, initialSize: int) {
		grow(initialSize);
	}

	def recordArm64Patch(addr: Addr, kind: Arm64PatchKind, pos: int) {
		arm64Refs = List.new((addr, kind, int.!(addr_of(pos)), pos), arm64Refs); // TODO(addr64)
	}
	def recordPatch(addr: Addr, pos: int) {
		refs = List.new((addr, int.!(addr_of(pos)), pos), refs); // TODO(addr64)
	}
	def patch(f: (MachDataWriter, Addr, int) -> void) {
		// patch address locations with user function
		for (l = refs; l != null; l = l.tail) {
			var addr = l.head.0, posAddr = l.head.1, pos = l.head.2;
			f(at(pos), addr, posAddr);
		}
	}
	def patchArm64(f: (MachDataWriter, Addr, Arm64PatchKind, int) -> void) {
		for (l = arm64Refs; l != null; l = l.tail) {
			var addr = l.head.0, kind = l.head.1, posAddr = l.head.2, pos = l.head.3;
			f(at(pos), addr, kind, posAddr);
		}	
	}
	def abs4(addr: Addr, encoding: int) {
		recordPatch(addr, pos);
		put_b32(encoding);
	}
	def skipPage() {
		skipAddr(pageAlign.size);
	}
	def endPageAddr() -> int {
		return pageAlign.alignDown(int.!(addr_end())); // TODO(addr64)
	}
	def putmd(w: MachDataWriter) {
		var p = this.pos;
		putr(w.alias());
		for (l = w.refs; l != null; l = l.tail) {
			var t = l.head;
			refs = List.new((t.0, t.1 + p, t.2 + p), refs);
		}
		w.refs = null;
	}
	def offset() -> int {
		return pos - startPos;
	}
	def endOffset() -> int {
		return end() - startPos;
	}
	def bind(addr: Addr) {
		addr.absolute = int.!(this.addr()); // TODO(addr64)
	}
	def bindSize(addr: Addr) {
		addr.size = int.!(this.addr()) - addr.absolute; // TODO(addr64)
	}
}
