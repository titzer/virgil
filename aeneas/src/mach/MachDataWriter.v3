// Copyright 2011 Google Inc. All rights reserved.
// Copyright 2021 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A specialized DataWriter with machine-level properties, such as a start address
// and the ability to encode an address that is tracked for later patching.
class MachDataWriter extends DataWriter {
	def pageAlign: Alignment;
	var startAddr: int;
	var refs: List<(Addr, /*posAddr:*/int, /*pos:*/int)>;
	new(pageAlign, startAddr, initialSize: int) {
		grow(initialSize);
	}

	def recordPatch(addr: Addr, pos: int) {
		var posAddr = startAddr + pos;
		refs = List.new((addr, posAddr, pos), refs);
	}
	def patch(f: (MachDataWriter, Addr, int) -> void) {
		// patch address locations with user function
		for (l = refs; l != null; l = l.tail) {
			var addr = l.head.0, posAddr = l.head.1, pos = l.head.2;
			f(at(pos), addr, posAddr);
		}
	}
	def abs4(addr: Addr, encoding: int) {
		recordPatch(addr, pos);
		put_b32(encoding);
	}
	def alignAddr(size: int) {
		var rem = (pos + startAddr) & (size - 1);
		if (rem > 0) {
			acquire(size - rem);
			at(pos + size - rem);
		}
	}
	def posAddr() -> int {
		return startAddr + pos;
	}
	def endAddr() -> int {
		return startAddr + end();
	}
	def skipPage() {
		startAddr = startAddr + pageAlign.size;
	}
	def endPageAddr() -> int {
		return pageAlign.alignDown(startAddr + end());
	}
	def putmd(w: MachDataWriter) {
		var p = this.pos;
		putk(w.data, 0, w.end());
		for (l = w.refs; l != null; l = l.tail) {
			var t = l.head;
			refs = List.new((t.0, t.1 + p, t.2 + p), refs);
		}
		w.refs = null;
	}
}
