// Copyright 2012 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// and encodes CiRuntime.GC tables into a compact format.
// XXX: make CiRuntime addresses into global constants that store the actual
// addresses in an array in the MachProgram or MachRuntime
class MachRtGcTables(mach: MachProgram, rt: MachRuntime) {
	var refMapBuilder = MachRefMapBuilder.new();
	var stackRefMaps = MachRtPageTable.new(CiRuntimeModule.GC_STACKMAP_PAGES, CiRuntimeModule.GC_STACKMAP_TABLE, null, false).grow(mach.numMethods * 2);
	var typeRefMaps = Vector<int>.new();
	var mutableMap = TypeUtil.newTypeMap<List<int>>();
	var rootMap: BitMatrix;
	var debug = CLOptions.PRINT_STACKMAP.get();

	def recordStackRefMap(off: int, source: Source, stackMap: int) {
		var map = StringBuilder.new().putx(stackMap).toString();
		if (debug && rt.src != null) rt.src.debugPoint("stackmap", map, off - 1, source, rt.src.frameSlots());
		stackRefMaps.add(off - 1, stackMap, 0);
	}
	def recordTypeRefMap(ic: IrClass) {
		// compute the reference map for a given IrClass
		var slots = (ic.machSize + mach.refSize - 1) / mach.refSize;
		refMapBuilder.beginRefMap(slots, 32); // XXX: 32-bit centric; use 16-bit maps on 16-bit targets
		var mutable: List<int>;
		for (f in ic.fields) {
			if (mach.isRefType(f.fieldType)) {
				refMapBuilder.setRefMap(f.machOffset / mach.refSize);
				if (!f.facts.F_VALUE) mutable = List.new(f.machOffset, mutable);
			}
		}
		mutableMap[ic.ctype] = mutable;
		var refmap = refMapBuilder.finishRefMap();
		// grow the typeRefMaps table if necessary
		var typeId = ic.minClassId;
		if (debug) Terminal.put3("%q typeid=%d = 0x%x\n", ic.ctype.render, typeId, refmap);
		if (typeId >= typeRefMaps.length) {
			typeRefMaps.grow(typeRefMaps.length + typeId + 10);
			typeRefMaps.length = typeId + 1; // XXX: dirty, direct modification of sequence length
		}
		typeRefMaps[typeId] = refmap;
	}
	def recordRootObject(off: int, r: Record) {
		// record the (mutable) references inside a root object at the given offset
		if (V3.isArray(r.rtype)) {
			var arrayRep = mach.arrayRep(r.rtype), tid = Int.unbox(arrayRep.typeTag);
			match (tid & 0x3) {
				MachObjectTags.REF_ARRAY_TAG => {
					var start = off + mach.getArrayElemOffset(r.rtype, 0);
					recordRootRefRange(start, start + r.values.length * mach.refSize);
				}
				MachObjectTags.MIXED_ARRAY_TAG => {
					var start = off + mach.getArrayElemOffset(r.rtype, 0);
					for (j < arrayRep.elemTypes.length) {
						if (!mach.isRefType(arrayRep.elemTypes[j])) continue;
						for (i < r.values.length) { // XXX: use bitmap from array's type tag
							recordRootRef(start + i * arrayRep.elemScale + arrayRep.offsets[j]);
						}
					}
				}
			}
		} else if (V3.isClass(r.rtype)) {
			// only record mutable field refs of this object
			for (l = mutableMap[r.rtype]; l != null; l = l.tail) {
				recordRootRef(off + l.head);
			}
		}
	}
	def beginRefMap(slots: int, max: int) -> MachRefMapBuilder {
		refMapBuilder.beginRefMap(slots, max);
		return refMapBuilder;
	}
	def emptyRefMap(max: int) -> int {
		refMapBuilder.beginRefMap(0, max);
		return refMapBuilder.finishRefMap();
	}
	def recordDataSize(size: int) {
		rootMap = BitMatrix.new(1, mach.data.addrAlign.alignUp(size) / mach.refSize);
	}
	def recordRootRef(off: int) {
		if (debug) Terminal.put1("@rootref data + %d\n", off);
		rootMap[0, off / mach.refSize] = true;
	}
	def recordRootRefRange(startOff: int, endOff: int) {
		// XXX: optimize setting of root ranges
		if (debug) Terminal.put2("@rootref data + %d ... %d\n", startOff, endOff);
		var end = endOff / mach.refSize;
		for (off = startOff / mach.refSize; off < end; off++) rootMap[0, off] = true;
	}
	def encode(w: MachDataWriter) {
		// encode stack-refmap table
		if (debug) Terminal.put1("starting GC stack ref maps @ 0x%x\n", w.addr());
		stackRefMaps.encode(rt, w);
		// encode root-refmap as a series of (offset, refmap) pairs
		if (debug) Terminal.put1("starting GC_ROOTS_START @ 0x%x\n", w.addr());
		CiRuntimeModule.GC_ROOTS_START.setAbs(rt, w.endAddr());
		var b = rootMap.rowInts(0);
		for (i < b.length) {
			if (b[i] != 0) { // XXX: trimming trailing zeroes would save a little scan time.
				w.put_b32(i * 32 * mach.refSize); // offset
				w.put_b32(b[i]); // refmap bits
			}
		}
		CiRuntimeModule.GC_ROOTS_END.setAbs(rt, w.endAddr());
		// encode type-refmap table
		if (debug) Terminal.put1("starting GC_TYPE_TABLE @ 0x%x\n", w.addr());
		CiRuntimeModule.GC_TYPE_TABLE.setAbs(rt, w.endAddr());
		for (i < typeRefMaps.length) {
			if (debug) Terminal.put2("typeMap %d = 0x%x\n", i, typeRefMaps[i]);
			w.put_b32(typeRefMaps[i]);
		}
		// encode ext-refmap area
		if (debug) Terminal.put1("starting GC_EXTMAPS @ 0x%x\n", w.addr());
		CiRuntimeModule.GC_EXTMAPS.setAbs(rt, w.endAddr());
		var ex = refMapBuilder.extended;
		for (i < ex.length) {
			if (debug) Terminal.put2("exMap %d = 0x%x\n", i, ex[i]);
			w.put_b32(ex[i]);
		}
		if (debug) Terminal.put1("finished GC maps at 0x%x\n", w.addr());
	}
}
// Helper for building a reference map, including extended reference maps.
class MachRefMapBuilder {
	def extended = Vector<int>.new();
	var curSlots: int;
	var curBits: int;
	var curExtended: int;

	// begin a new stackmap entry with the given length in bits
	def beginRefMap(slots: int, max: int) {
		curSlots = slots;
		if (slots >= (max - 1)) {
			// allocate space in the extended map
			curExtended = 1 << u5.!(max - 1);
			curBits = extended.length;
			var words = (slots + 31) / 31;
			// bit 31 set indicates more words to follow
			for (i = 1; i < words; i++) extended.put(0x80000000);
			// bit 31 not set indicates this is the last word
			extended.put(1 << u5.!(slots % 31));
		} else {
			// number of slots is small enough to fit in the compact map
			curBits = 1 << u5.!(slots);
			curExtended = 0;
		}
	}
	// set the given bit in the current stackmap entry
	def setRefMap(index: int) {
		if (u32.view(index) >= u32.view(curSlots)) return V3.fail("out of bounds in stack map construction");
		if (curExtended != 0) {
			// set the appropriate bit in the extended map
			var i = curBits + index / 31, bit = u5.!(index % 31);
			extended[i] = extended[i] | (1 << bit);
		} else {
			// set the bit in the current bits
			curBits = curBits | (1 << u5.!(index));
		}
	}
	// return the encoded form of the current entry
	def finishRefMap() -> int {
		return curExtended | curBits;
	}
}
