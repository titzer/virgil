// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Represents all runtime data structures exposed to the Virgil runtime code.
class MachRuntime(mach: MachProgram) {
	def codeRegion = mach.codeRegion;
	def addrs = Array<int>.new(CiRuntimeModule.max); // CiRuntime absolute addresses
	var typeCon: CiRuntime_TypeCon;

	// Indexes in the IrModule's roots vector
	var ri_init = -1;
	var ri_gc = -1;
	var ri_signal = -1;
	var ri_exit = -1;

	var src: MachRtSrcTables;
	var gc: MachRtGcTables;

	var heapSize = CLOptions.HEAP_SIZE.get();
	var shadowStackSize: u32;

	new() {
		if (CLOptions.RT_STTABLES.get()) src = MachRtSrcTables.new(mach, this);
		if (CLOptions.RT_GCTABLES.get()) gc = MachRtGcTables.new(mach, this);
		var ptrType: Type;
		match (mach.data.addressSize) {
			2 => ptrType = MachModule.PTR_16.singleton;
			4 => ptrType = MachModule.PTR_32.singleton;
			8 => ptrType = MachModule.PTR_64.singleton;
			_ => ptrType = V3.fail("invalid address size");
		}
		typeCon = CiRuntime_TypeCon.new(ptrType, mach.prog.typeCache);
		shadowStackSize = CLOptions.SHADOW_STACK_SIZE.get();
		if (shadowStackSize == 0) {
			var percent = CLOptions.SHADOW_STACK_PERCENT.get();
			if (percent > 100) percent = 100;
			if (percent > 0) shadowStackSize = u32.view((100uL * heapSize) / percent);
		}
	}

	def recordStackStart(addr: int) {
		recordAddr(CiRuntimeModule.STACK_START, addr);
	}
	def recordStackEnd(addr: int) {
		recordAddr(CiRuntimeModule.STACK_END, addr);
	}
	def recordCodeStart(addr: int) {
		recordAddr(CiRuntimeModule.CODE_START, addr);
	}
	def recordReservedCode(startAddr: int, size: int, fileOffset: int) {
		recordAddr(CiRuntimeModule.RESERVED_CODE_START, startAddr);
		recordAddr(CiRuntimeModule.RESERVED_CODE_END, startAddr + size);
		recordAddr(CiRuntimeModule.RESERVED_CODE_FILE_OFFSET, fileOffset);
	}
	def recordRuntimeCode(startAddr: int, size: int) {
		recordAddr(CiRuntimeModule.RUNTIME_CODE_START, startAddr);
		recordAddr(CiRuntimeModule.RUNTIME_CODE_END, startAddr + size);
	}
	def recordCodeEnd(addr: int) {
		recordAddr(CiRuntimeModule.CODE_END, addr);
	}
	def recordDataStart(addr: int) {
		recordAddr(CiRuntimeModule.DATA_START, addr);
	}
	def recordDataEnd(addr: int) {
		recordAddr(CiRuntimeModule.DATA_END, addr);
	}
	def recordAddr(c: CiRuntime_Address, addr: int) {
		if (CLOptions.PRINT_BIN.get()) Terminal.put2("CiRuntime.%s=%x\n", c.name, addr);
		c.setAbs(this, addr);
	}
	def getExceptionDest(off: int, ex: string, source: Source) -> Addr {
		if (src != null) return src.newExceptionDest(ex, source);
		return getFatalAddress(ex);
	}
	def addHeapPointers(w: MachDataWriter) {
		def C: CiRuntimeModule;
		w.atEnd().alignAddr(mach.data.addressSize);

		addPtr(w, C.HEAP_CUR_LOC);
		addPtr(w, C.HEAP_END_LOC);
		var addr = w.endAddr();
		if (shadowStackSize > 0) {
			addPtr(w, C.SHADOW_STACK_START_PTR);
			addPtr(w, C.SHADOW_STACK_CUR_PTR);
			addPtr(w, C.SHADOW_STACK_END_PTR);
			addr = w.endAddr();
			setAddr(C.SHADOW_STACK_START, addr);
			setPtr(w, C.SHADOW_STACK_START_PTR, addr);
			setPtr(w, C.SHADOW_STACK_CUR_PTR, addr);
			setPtr(w, C.SHADOW_STACK_END_PTR, addr += int.!(shadowStackSize));
			setAddr(C.SHADOW_STACK_END, addr);
		}
		setAddr(C.HEAP_START, addr);
		setPtr(w, C.HEAP_CUR_LOC, addr);
		setPtr(w, C.HEAP_END_LOC, addr += int.!(heapSize));
		setAddr(C.HEAP_END, addr);
		w.atEnd();
	}
	def addPtr(w: MachDataWriter, ptr: CiRuntime_Address) {
		var addr = w.endAddr();
		if (CLOptions.PRINT_BIN.get()) {
			Terminal.put2("CiRuntime.%s @ %x\n", ptr.name, addr);
		}
		ptr.setAbs(this, addr);
		w.skipN(mach.data.addressSize);
	}
	def setAddr(ptr: CiRuntime_Address, addr: int) {
		if (CLOptions.PRINT_BIN.get()) {
			Terminal.put2("CiRuntime.%s = %x\n", ptr.name, addr);
		}
		ptr.setAbs(this, addr);
	}
	def setPtr(w: MachDataWriter, ptr: CiRuntime_Address, addr: int) {
		var abs = addrs[ptr.index];
		if (CLOptions.PRINT_BIN.get()) {
			Terminal.put3("CiRuntime.%s @ %x = %x\n", ptr.name, abs, addr);
		}
		w.at(abs - w.startAddr);
		match (mach.data.addressSize) {
			1 => w.putb(addr);
			2 => w.put_b16(addr);
			4 => w.put_b32(addr);
			8 => w.put_b64(addr);
			_ => { return V3.fail("invalid data address size"); }
		}
	}
	def codePages() -> int {
		var end = CiRuntimeModule.CODE_END.getAbs(this);
		if (end == 0) mach.prog.ERROR.fail("code end not set yet");
		var size = end - CiRuntimeModule.CODE_START.getAbs(this);
		var align = codeRegion.space.pageAlign;
		return align.alignUp(size) >> align.shift;
	}
	def encode(w: MachDataWriter) {
		if (gc != null) gc.encode(w);
		if (src != null) src.encode(w);
	}
	def getFatalAddress(ex: string) -> Addr {
		var addr = mach.stubMap[ex].0;
		if (addr == null) {
			addr = Address.new(codeRegion, Arrays.concat("fatal_", ex));
			mach.stubMap[ex] = (addr, null);
		}
		return addr;
	}
	def parseTestInputs(prog: Program, error: ErrorGen, expected: int) -> Array<Array<Val>> {
		var e = SpecParser.parseFile(error, prog.files[0], prog.inputs[0]);
		heapSize = e.heapSize;
		var runs = Lists.toArray(e.runs);
		var result = Array<Array<Val>>.new(runs.length);
		for (i < runs.length) {
			result[i] = Lists.toArray(runs[i].input);
		}
		return result;
	}
	def addRoots() {
		var typeCon = mach.prog.typeEnv.lookup("RiRuntime");
		if (V3Component_TypeCon.?(typeCon)) {
			// some RiRuntime methods are called by compiled code
			var ctype = V3Component_TypeCon.!(typeCon).componentDecl.getDeclaredType();
			var ic = mach.prog.ir.getIrClass(ctype);
			for (meth in ic.methods) {
				if (meth == null) continue;
				var name = meth.source.name();
				if (CLOptions.RT_GC.get() && Strings.equal(name, "gc")) ri_gc = addRoot(ctype, meth);
				else if (Strings.equal(name, "init")) ri_init = addRoot(ctype, meth);
				else if (Strings.equal(name, "signal")) ri_signal = addRoot(ctype, meth);
				else if (Strings.equal(name, "exit")) ri_exit = addRoot(ctype, meth);
			}
		}
	}
	def getRiGc() -> IrMethod {
		return getRoot(ri_gc);
	}
	def getRiInit() -> IrMethod {
		return getRoot(ri_init);
	}
	def getRiSignal() -> IrMethod {
		return getRoot(ri_signal);
	}
	def getRiExit() -> IrMethod {
		return getRoot(ri_exit);
	}
	private def getRoot(index: int) -> IrMethod {
		return if(index >= 0, mach.prog.ir.roots[index].spec.asMethod());
	}
	private def addRoot(ctype: Type, meth: IrMethod) -> int {
		return mach.prog.ir.addRoot(null, IrSpec.new(ctype, [ctype], meth));
	}
}
