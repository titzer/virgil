// Copyright 2017 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A utility to introduce explicit loads and stores of local variables
// between instructions for stack machines such as WASM and the JVM.
class MachStackifier(gen: ArchCodeGen) {
	private def stack = Vector<Operand.Use>.new();	
	private def defs = Vector<Operand.Def>.new();
	private def parmoveDests = Vector<(VReg, List<Operand.Def>)>.new();

	private def defStack = Vector<Operand.Def>.new();
	private def loads = Vector<Operand.Use>.new();
	private def insertPoint = Vector<ArchInstr>.new();
	private def insertLoads = Vector<(ArchInstr, int, int)>.new();
	private def moveStack = Vector<int>.new();

	var out: ArchInstrBuffer;

	def stackify(uid: int, pre: ArchInstr, end: ArchInstr, dumpRefsOnKill: bool) {
		if (out != null) {
			out.format1("#stackify #%1 {\n", uid);
			for (i = pre.next; i != end; i = i.next) out.putInstrV(i);
			out.puts("--process:\n");
		}

		setDefStackLength(0);
		loads.length = 0;
		insertPoint.length = 0;
		insertLoads.length = 0;

		insertPoint.set(0, pre.next);
		// Process instructions forward, tracking definitions on the abstract stack
		var start = pre.next;
		var i = start;
		while (true) {
			if (i == end) break;
			if (out != null) out.putInstrV(i);
			if (i.opcode() == ArchInstrs.ARCH_PARMOVE) {
				prepareParMove(i);
				finishInstrs(start, i);
				start = i.next;
				lowerParMove(i);
				i = start;
			} else {
				processInstr(i);
				i = i.next;
			}
		}
		finishInstrs(start, end);
		setDefStackLength(0);
		if (out != null) {
			out.puts("--finish:\n");
			for (i = pre.next; i != end; i = i.next) out.putInstrV(i);
			out.puts("}\n");
		}
	}
	def finishInstrs(start: ArchInstr, end: ArchInstr) {
		if (out != null) out.puts("---insertLoads:\n");
		// Insert loads at points determined from previous pass
		for (j = insertLoads.length - 1; j >= 0; j--) {
			var t = insertLoads[j], next = t.0;
			for (k = t.1; k < t.2; k++) loadVRegOntoStack(loads[k].vreg, next);
		}
		loads.length = 0;
		insertLoads.length = 0;
		// Insert pops or tees for anything not consumed from stack
		if (out != null) out.puts("---finishDefs:\n");
		var next: ArchInstr;
		for (i = start; i != end; i = next) {
			next = i.next;
			finishDefs(i);
		}
	}
	def processInstr(i: ArchInstr) {
		var operands = i.operands;
		var loadStart = loads.length;
		var nextHeight = defStack.length;
		var maxStackIndex = -1;
		// Search for operands on the definition stack
		for (j < operands.length) {
			if (out != null) out.format1("\t\toperands[%1]", j);
			var o = operands[j];
			if (!Operand.Use.?(o)) {
				if (out != null) out.format1(" (skip)\n", j);
				continue;
			}
			var u = Operand.Use.!(o), ssa = u.ssa, vreg = u.vreg, s = vreg.stackIndex;
			if (out != null) out.format3(" m=%1 n=%2 s=%3", maxStackIndex, nextHeight, s);
			if (s < -1) {
				// vreg never seen before (e.g. constant, phi, param, in other block), cannot be on stack
				if (out != null) out.format1(" ->loads[%1]\n", loads.length - loadStart);
				loads.put(u);
				continue;
			}
			if (s <= maxStackIndex) {
				// defined before something already used from stack; load everything remaining
				if (out != null) out.puts(" not on stack\n");
				loads.put(u);
				for (k = j+1; k < operands.length; k++) {
					var o = operands[k];
					if (Operand.Use.?(o)) {
						if (out != null) out.format1("\t\toperands[%1] load\n", k);
						loads.put(Operand.Use.!(o));
					}
				}
				break;
			}
			if (out != null) out.format1(" onstack @%1\n", s);
			if (defStack[s].vreg != vreg) System.error("MachStackifier", "def not on stack");
			insertLoadsAfter(s, loadStart);
			if (nextHeight == defStack.length) nextHeight = s;
			loadStart = loads.length;
			vreg.consumedFromStack = true;
			maxStackIndex = s;
		}
		insertLoadsAfter(defStack.length, loadStart);
		setDefStackLength(nextHeight);
		// Process defs
		var next = i.next;
		for (o in i.operands) {
			if (!Operand.Def.?(o)) continue;
			var d = Operand.Def.!(o);
			if (out != null) out.format2("\t\tdef v%1=%2\n", d.vreg.varNum, defStack.length);
			d.vreg.stackIndex = defStack.length;
			defStack.put(d);
			insertPoint.set(defStack.length, next);
		}
		insertPoint.set(defStack.length, next);
	}
	def setDefStackLength(v: int) {
		for (i = v; i < defStack.length; i++) {
			defStack[i].vreg.stackIndex = -1;
		}
		defStack.length = v;
	}
	// Prepare to lower a parallel move. Load all move sources that are on the
	// definition stack to the move stack and mark them as consumed on the stack.
	def prepareParMove(i: ArchInstr) {
		gatherParMoveDests(i);
		var next = i.next;
		moveStack.length = 0;
		for (i < defStack.length) {
			var vreg = defStack[i].vreg;
			pushOntoMoveStack(vreg, null);
		}
		setDefStackLength(0);
	}
	// Replace a parallel move with individual moves in the right order.
	// The definition stack has already been used to initialize the move stack.
	def lowerParMove(i: ArchInstr) {
		var next = i.next;
		processMoveStack(next);
		// Push any remaining move sources onto the move stack and process them
		for (i < parmoveDests.length) {
			pushOntoMoveStack(parmoveDests[i].0, next);
			processMoveStack(next);
		}
		// Lastly, remove this instruction from the block
		i.remove();
	}
	def pushOntoMoveStack(vreg: VReg, next: ArchInstr) -> bool {
		if (vreg != null && vreg.parmoveState > 0) {
			moveStack.put(vreg.parmoveState - 1);
			vreg.parmoveState = 0 - moveStack.length;
			if (next != null) loadVRegOntoStack(vreg, next);
			else vreg.consumedFromStack = true;
			return true;
		}
		return false;
	}
	// Use the move stack to implement a non-recursive post-order traversal of the move
	// graph. Always recurse on the first destination of the top of stack. If that
	// is already done or on the stack, generate a move, otherwise push it.
	def processMoveStack(next: ArchInstr) {
		while (moveStack.length > 0) {
			var index = moveStack[moveStack.length - 1];
			var t = parmoveDests[index], vreg = t.0, destList = t.1;
			var done = true;
			while (destList != null) {
				var dest = destList.head.vreg;
				if (pushOntoMoveStack(dest, next)) {
					done = false;
					parmoveDests[index] = (vreg, destList);  // remember remaining moves
					break;
				} else {
					var pop = destList.tail == null;  // pop if last in list
					gen.insertStoreLocal(dest, pop, next);
				}
				destList = destList.tail;
			}
			if (done) {
				moveStack.length--;     // pop off move stack
				vreg.parmoveState = 0;  // signify it's done
				parmoveDests[index] = (null, null); // defensive
			}
		}
	}
	def finishDefs(i: ArchInstr) {
		var operands = i.operands;
		for (j < operands.length) {  // skip everything consumed on stack with useCount == 1
			var o = operands[j];
			if (!Operand.Def.?(o)) continue;
			var d = Operand.Def.!(o);
			if (!d.vreg.consumedFromStack || useCount(d) != 1) return finishRemainingDefs(operands, j, i.next);
		}
	}
	def finishRemainingDefs(operands: Array<Operand>, j: int, next: ArchInstr) {
		var mustPop = false;
		while (j < operands.length) {  // process all that need tee, pop, or store
			var o = operands[j++];
			if (!Operand.Def.?(o)) continue;
			var d = Operand.Def.!(o), c = useCount(d);
			if (c == 0) {
				gen.insertPop(d.vreg, next);
			} else {
				var pop = mustPop || !d.vreg.consumedFromStack;
				gen.insertStoreLocal(d.vreg, pop, next);
				d.vreg.consumedFromStack = false;
			}
			mustPop = true;
		}
	}
	def insertLoadsAfter(height: int, start: int) {
		var count = loads.length - start;
		if (count > 0) {
			if (out != null) out.format1("\t\tinsertLoadsAfter @%1\n", height);
			insertLoads.put(insertPoint[height], start, loads.length);
		}
	}
	def loadVRegOntoStack(vreg: VReg, next: ArchInstr) {
		if (SsaConst.?(vreg.ssa)) {
			var const = SsaConst.!(vreg.ssa);
			gen.insertLoadConst(const.vtype, const.val, next);
		} else {
			gen.insertLoadLocal(vreg, next);
		}
	}
	def gatherParMoveDests(i: ArchInstr) {
		parmoveDests.length = 0;
		var operands = i.operands;
		// XXX: Construct the vreg -> List<Def> mapping at instruction selection
                var max = operands.length / 2;
		for (i < max) {
			var d = Operand.Def.!(operands[i]), u = Operand.Use.!(operands[i + max]);
			var v = u.vreg;
			if (v.parmoveState <= 0) {
				var index = parmoveDests.length;
				v.parmoveState = 1 + index;
				parmoveDests.put(v, List.new(d, null));
			} else {
				var index = v.parmoveState - 1;
				var prev = parmoveDests[index].1;
				parmoveDests[index] = (v, List.new(d, prev));
			}
		}
	}
	def useCount(d: Operand.Def) -> int {
		// XXX: get this information from the VReg.
		if (d.ssa == null) return 1;
		if (d.ssa.useList == null) return 0;
		if (d.ssa.useList.next == null) return 1;
		return 2;
	}
	private def printStack() {
		out.format1("  #stack |%1| ", stack.length);
		for (i < stack.length) {
			var u = stack[i];
			if (u.ssa != null) out.format1("@%1 ", u.ssa.uid);
			else out.format1("#%1 ", u.vreg.varNum);
		}
		out.ln();
	}
}
