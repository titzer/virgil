// Copyright 2026 Virgil authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Generates an adapter so that calls from Virgil (with receiver) are forwarded to the external calling
// convention (without receiver).
class MachImportAdapterGen {
	def compiler: Compiler;
	def mach: MachProgram;
	def getExtCallConv: (MachProgram, Array<Type>, Array<Type>) -> MachCallConv; // XXX: use Signature
	def context = SsaContext.new(compiler, mach.prog);
	var curBlock: SsaBuilder;

	new(compiler, mach, getExtCallConv) {
	}

	def doMethod(m: IrMethod, addr: Addr) {
		var graph = SsaGraph.new(m.ssa.params, m.ssa.returnType);
		m.ssa = graph;
		context.enterMethod(m);
		curBlock = SsaBuilder.new(context, graph, graph.startBlock);

		var spec = IrSpec.new(m.receiver, [m.receiver], m);
		var bound_rep = mach.getFuncRep(spec.getBoundType());
		var ext_rep = bound_rep.dup();
		ext_rep.callConv = getExtCallConv(mach, bound_rep.paramTypes, bound_rep.returnTypes);

		var args = Array<SsaInstr>.new(graph.params.length);
		args[0] = graph.valConst(ext_rep.machType, addr);
		for (i < args.length) args[i] = graph.params[i + 1]; // skip receiver
		var op = V3Op.newCallAddress(ext_rep);
		var call = curBlock.add(op, args, Facts.NONE);
		curBlock.addReturn([call]);
	}
}
