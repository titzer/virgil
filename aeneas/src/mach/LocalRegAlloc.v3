// Copyright 2022 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Local register allocator that spills everything to the stack between basic blocks.
def verbose = false;
def paranoid = true;
class LocalRegAlloc(regSet: MachRegSet, codegen: SsaMachGen) {
	def regState = RegState.new(regSet, codegen);
	def parmoves = ParMoveDests.new();
	def moveResolver = MoveResolver.new(codegen.mach.prog.ERROR);
	def moves = MoveSet.new(regSet);
	var buf = ArchInstrBuffer.new(codegen, codegen.mach.prog, regSet);
	var prev: ArchInstr;
	var cur: ArchInstr;
	var next: ArchInstr;

	def allocate() {
		regState.clear();
		cur = codegen.last;
		while (cur != null) {
			if (verbose) regState.dumpState("before instr");
			if (verbose) Terminal.put(buf.putInstrV(cur).toString());
			if (paranoid) regState.sanityCheck();
			regState.pos++;
			prev = cur.prev;
			next = cur.next;
			var opcode = cur.opcode();
			if (opcode == ArchInstrs.ARCH_PARMOVE) { // perform parallel move
				cur.remove();
				parmoves.entries.resize(0);
				codegen.gatherParallelMoveDests(cur, parmoves.entries);
				for (j < parmoves.entries.length) parmoves.emitMoves(codegen, j, next);
				cur = prev;
				continue;
			}
			if (opcode == ArchInstrs.ARCH_BLOCK) {
				var block = Operand.Label.!(cur.operands[0]).block;
				if (block.preds.length != 0) {  // not the entry block
					for (i < regState.cursor) { // Load all live registers out of their spill slots
						var a = regState.allocated[i];
						var vreg = a.0;
						regState.regState[vreg.reg] = -1;
						codegen.allocSpill(vreg);
						codegen.insertRestoreLocal(vreg, vreg.reg, next);
						vreg.reg = 0;
					}
					regState.cursor = 0;
				}
				cur = prev;
				continue;
			}
			if (opcode == ArchInstrs.ARCH_BLOCK_END) {
				cur = prev;
				continue;
			}
			var operands = cur.operands;
			// process defs and kills
			moves.clear();
			// first pass on defs; free registers of anything defined in this instruction, but don't clear vreg.reg
			for (j < operands.length) match (operands[j]) {
				Def(vreg, constraint) 			=> regState.freeReg(vreg.reg, false);
				Overwrite(vreg, use, constraint) 	=> regState.freeReg(vreg.reg, false);
				_ => ;
			}
			// second pass on defs; allocate (new) registers and shuffle into previous vreg.reg
			for (j < operands.length) match (operands[j]) {
				Def(vreg, constraint) => {
					var loc = allocDefReg(vreg, constraint);
					operands[j] = Operand.Def(vreg, loc);
				}
				Overwrite(vreg, use, constraint) => {
					var loc = allocDefReg(vreg, constraint);
					operands[j] = Operand.Overwrite(vreg, use, loc);
				}
				_ => ;
			}
			regState.pos++;
			if (verbose) regState.dumpState("after defs  ");
			// process uses
			for (j < operands.length) match (operands[j]) {
				Def(vreg, assignment) => {
					regState.freeReg(assignment, true);
				}
				Overwrite(dst, use, assignment) => {
					regState.freeReg(assignment, true);
					allocUseReg(use, assignment);
				}
				Kill(constraint) => {
					if (constraint < regSet.regSets.length) {
						for (reg in regSet.regSets[constraint]) {
							spillReg(reg, next);
							regState.freeReg(reg, true);
						}
					}
				}
				Use(vreg, constraint) => {
					var loc = allocUseReg(vreg, constraint);
					operands[j] = Operand.Use(vreg, loc);
				}
				_ => ;
			}
			moves.emit(codegen, cur.next);
			if (opcode == ArchInstrs.ARCH_ENTRY) {
				for (j < regState.cursor) {
					var vreg = regState.allocated[j].0;
					if (vreg.isConst()) codegen.insertMoveConstLoc(SsaConst.!(vreg.ssa), (vreg, vreg.reg), vreg.regClass, next);
				}
			}
			cur = prev;
		}
	}
	def allocUseReg(vreg: VReg, constraint: int) -> int {
		var prevReg = vreg.reg, loc: int = prevReg;
		if (regSet.isStack(constraint)) {
			if (vreg.isConst()) {
				codegen.insertMoveConstLoc(SsaConst.!(vreg.ssa), (vreg, constraint), vreg.regClass, prev.next);
			} else {
				if (prevReg != 0) {
					regState.updateUsePos(prevReg);
					codegen.insertMoveLocLoc((vreg, prevReg), (vreg, constraint), vreg.regClass, prev.next);
				} else {
					codegen.allocSpill(vreg); // insert a save instruction before and alloc reg independently for it
					codegen.insertMoveLocLoc((vreg, vreg.spill), (vreg, constraint), vreg.regClass, prev.next);
				}
			}
			return constraint;
		} else if (isOk(vreg, constraint)) {
			regState.updateUsePos(prevReg);
		} else {
			if (regState.isUsedInThisInstr(prevReg)) {
				regState.freeReg(prevReg, true);
				loc = reassignReg(vreg, findBestLoc(vreg.regClass, vreg.hint, constraint));
				codegen.insertMoveLocLoc((vreg, loc), (vreg, prevReg), vreg.regClass, prev.next);
			} else {
				spillReg(prevReg, next);
				regState.freeReg(prevReg, true);
				loc = reassignReg(vreg, findBestLoc(vreg.regClass, vreg.hint, constraint));
			}
		}
		if (paranoid) regState.sanityCheck();
		return loc;
	}
	def allocDefReg(vreg: VReg, constraint: int) -> int {
		var prevReg = vreg.reg;
		vreg.reg = 0;
		var loc = reassignReg(vreg, findBestLoc(vreg.regClass, prevReg, constraint));
		if (prevReg != 0) moves.addRegMove(vreg, loc, prevReg);
		if (vreg.spill != 0) moves.saves.put(vreg, loc, vreg.spill);
		if (paranoid) regState.sanityCheck();
		return loc;
	}
	def spillReg(reg: byte, next: ArchInstr) {
		var i = regState.regState[reg];
		if (i < 0) return;
		var vreg = regState.allocated[i].0;
		codegen.allocSpill(vreg);
		codegen.insertRestoreLocal(vreg, reg, next);
	}
	def isOk(vreg: VReg, constraint: int) -> bool {
		if (vreg.reg == 0) return false;
		if (constraint >= regSet.regSets.length) return false;
		if (constraint == 0) return true;
		return regSet.isInRegSet(vreg.reg, constraint);
	}
	def findBestLoc(regClass: RegClass, hint: int, constraint: int) -> int {
		if (constraint >= regSet.regSets.length) return constraint; // constraint is for spill slot
		if (constraint == 0) constraint = regSet.regClasses[regClass.tag];
		if (hint != 0) { // try hint register first
			var i = regState.regState[hint];
			if (i < 0 && regSet.isInRegSet(hint, constraint)) return hint; // hint register is free
		}
		return regState.chooseRegister(constraint);
	}
	def reassignReg(vreg: VReg, reg: int) -> int {
		if (reg > regSet.physRegs) return reg;
		var old = regState.reassignReg(vreg, reg);
		if (old != null) {
			codegen.allocSpill(old);
			moves.reloads.put(old, reg);
			old.reg = 0;
		}
		return vreg.reg = byte.!(reg);
	}
}
