// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A JVM type, including its name, its kind, and its type descriptor
class JvmType(name: string, kind: JvmKind) {
	var desc: string; // lazily created
	private var arrayTypeRef: JvmArray_Type;

	def getDescriptor() -> string {
		if (desc == null) desc = buildDescriptor();
		return desc;
	}
	def arrayType() -> JvmArray_Type {
		if (arrayTypeRef == null) arrayTypeRef = JvmArray_Type.new(this);
		return arrayTypeRef;
	}
	def buildDescriptor() -> string;
}
// A Jvm primitive type
class JvmPrimitive extends JvmType {
	def edesc: string;
	new(name: string, desc: string, edesc, kind: JvmKind) super(name, kind) {
		this.desc = desc;
	}
}
// A Jvm array type
class JvmArray_Type extends JvmType {
	def elementType: JvmType;

	new(elementType) super(StringBuilder.new()
				.puts(elementType.name)
				.puts("[]").toString(),
			JvmTypes.KIND_OBJECT) {
	}
	def buildDescriptor() -> string {
        	var edesc = if(JvmPrimitive.?(elementType), JvmPrimitive.!(elementType).edesc, elementType.getDescriptor());
		return StringBuilder.new().putc('[').puts(edesc).toString();
	}
}
// A Jvm class type
class JvmClass extends JvmType {
	def superName: string;
	var defaultRecord: Record;
	new(name: string, superName) super(name, JvmTypes.KIND_OBJECT) { }
	def buildDescriptor() -> string {
		var buffer = StringBuilder.new().putc('L');
		for (i < name.length) {
			if (name[i] == '.') buffer.putc('/');
			else buffer.putc(name[i]);
		}
		return buffer.putc(';').toString();
	}
}
// A Java bytecode "kind" knows the opcodes for various operations and their stack widths
class JvmKind(
	LOAD: byte,
	LOAD_0: byte,
	STORE: byte,
	STORE_0: byte,
	ALOAD: byte,
	ASTORE: byte,
	RETURN: byte,
	NEWARRAY: byte,
	width: int) {
}
def paramsSizeOf(paramTypes: Array<JvmType>) -> int {
	var paramsSize = 0;
	for (t in paramTypes) {
		paramsSize = paramsSize + t.kind.width;
	}
	return paramsSize;
}
// A representation of a Jvm method signature
class JvmSig(paramTypes: Array<JvmType>, returnType: JvmType) {
	def paramsSize = paramsSizeOf(paramTypes);
	var desc: string;

	def stackDelta(instance: bool) -> int {
		if (instance) return returnType.kind.width - 1 - paramsSize;
		return returnType.kind.width - paramsSize;
	}
	def localsSize(instance: bool) -> int {
		if (instance) return 1 + paramsSize;
		return paramsSize;
	}
	def getDescriptor() -> string {
		if (desc == null) {
			var buffer = Strings.builderOf("(");
			for (t in paramTypes) {
				buffer.puts(t.getDescriptor());
			}
			buffer.puts(")");
			buffer.puts(returnType.getDescriptor());
			desc = buffer.toString();
		}
		return desc;
	}
}
// Important Jvm types and kinds.
component JvmTypes {
	def none = '\x00';
	def KIND_VOID	= JvmKind.new(none, none, none, none, none, none, JvmBytecode.RETURN, none, 0);
	def KIND_BOOLEAN= JvmKind.new(JvmBytecode.ILOAD, JvmBytecode.ILOAD_0,
				      JvmBytecode.ISTORE, JvmBytecode.ISTORE_0,
				      JvmBytecode.BALOAD, JvmBytecode.BASTORE,
				      JvmBytecode.IRETURN, 4, 1);
	def KIND_BYTE	= JvmKind.new(JvmBytecode.ILOAD, JvmBytecode.ILOAD_0,
				      JvmBytecode.ISTORE, JvmBytecode.ISTORE_0,
				      JvmBytecode.BALOAD, JvmBytecode.BASTORE,
				      JvmBytecode.IRETURN, 8, 1);
	def KIND_CHAR	= JvmKind.new(JvmBytecode.ILOAD, JvmBytecode.ILOAD_0,
				      JvmBytecode.ISTORE, JvmBytecode.ISTORE_0,
				      JvmBytecode.CALOAD, JvmBytecode.CASTORE,
				      JvmBytecode.IRETURN, 5, 1);
	def KIND_SHORT	= JvmKind.new(JvmBytecode.ILOAD, JvmBytecode.ILOAD_0,
				      JvmBytecode.ISTORE, JvmBytecode.ISTORE_0,
				      JvmBytecode.SALOAD, JvmBytecode.SASTORE,
				      JvmBytecode.IRETURN, 9, 1);
	def KIND_INT	= JvmKind.new(JvmBytecode.ILOAD, JvmBytecode.ILOAD_0,
				      JvmBytecode.ISTORE, JvmBytecode.ISTORE_0,
				      JvmBytecode.IALOAD, JvmBytecode.IASTORE,
				      JvmBytecode.IRETURN, 10, 1);
	def KIND_LONG	= JvmKind.new(JvmBytecode.LLOAD, JvmBytecode.LLOAD_0,
				      JvmBytecode.LSTORE, JvmBytecode.LSTORE_0,
				      JvmBytecode.LALOAD, JvmBytecode.LASTORE,
				      JvmBytecode.LRETURN, 11, 2);
	def KIND_FLOAT	= JvmKind.new(JvmBytecode.FLOAD, JvmBytecode.FLOAD_0,
				      JvmBytecode.FSTORE, JvmBytecode.FSTORE_0,
				      JvmBytecode.FALOAD, JvmBytecode.FASTORE,
				      JvmBytecode.FRETURN, 6, 1);
	def KIND_DOUBLE	= JvmKind.new(JvmBytecode.DLOAD, JvmBytecode.DLOAD_0,
				      JvmBytecode.DSTORE, JvmBytecode.DSTORE_0,
				      JvmBytecode.DALOAD, JvmBytecode.DASTORE,
				      JvmBytecode.DRETURN, 7, 2);
	def KIND_OBJECT	= JvmKind.new(JvmBytecode.ALOAD, JvmBytecode.ALOAD_0,
				      JvmBytecode.ASTORE, JvmBytecode.ASTORE_0,
				      JvmBytecode.AALOAD, JvmBytecode.AASTORE,
				      JvmBytecode.ARETURN, none, 1);

	def BOOLEAN = JvmPrimitive.new("boolean", "Z", "Z", KIND_BOOLEAN);
	def BYTE    = JvmPrimitive.new("byte", "B", "B", KIND_BYTE);
	def UBYTE   = JvmPrimitive.new("byte", "C", "B", KIND_BYTE);
	def CHAR    = JvmPrimitive.new("char", "C", "C", KIND_CHAR);
	def SHORT   = JvmPrimitive.new("short", "S", "S", KIND_SHORT);
	def INT     = JvmPrimitive.new("int", "I", "I", KIND_INT);
	def LONG    = JvmPrimitive.new("long", "J", "J", KIND_LONG);
	def FLOAT   = JvmPrimitive.new("float", "F", "F", KIND_FLOAT);
	def DOUBLE  = JvmPrimitive.new("double", "D", "D", KIND_DOUBLE);
	def VOID    = JvmPrimitive.new("void", "V", "V", KIND_VOID);

	def jlo = "java/lang/Object";
	def jln = "java/lang/Number";
	def systemClassName = "V3S_System";

	def java_lang_Object				= JvmClass.new(jlo, null);
	def java_lang_Integer				= JvmClass.new("java/lang/Integer", jlo);
	def java_lang_Math				= JvmClass.new("java/lang/Math", jlo);
	def java_lang_String				= JvmClass.new("java/lang/String", jlo);
	def java_lang_System				= JvmClass.new("java/lang/System", jlo);
	def java_lang_Float				= JvmClass.new("java/lang/Float", jln);
	def java_lang_Double				= JvmClass.new("java/lang/Double", jln);
	def java_lang_AbstractMethodError		= JvmClass.new("java/lang/AbstractMethodError", jlo);
	def java_lang_ClassCastException		= JvmClass.new("java/lang/ClassCastException", jlo);
	def java_lang_ArrayIndexOutOfBoundsException	= JvmClass.new("java/lang/ArrayIndexOutOfBoundsException", jlo);
	def java_lang_NegativeArraySizeException	= JvmClass.new("java/lang/NegativeArraySizeException", jlo);
	def java_lang_ArithmeticException		= JvmClass.new("java/lang/ArithmeticException", jlo);
	def java_lang_Exception				= JvmClass.new("java/lang/Exception", jlo);

	def BYTE_ARRAY: JvmArray_Type   = BYTE.arrayType();
	def STRING_ARRAY: JvmArray_Type = JvmTypes.java_lang_String.arrayType();

	def SIG_VOID                 = JvmSig.new([], VOID);
	def SIG_VOID_INT             = JvmSig.new([], INT);
	def SIG_EQUALS               = JvmSig.new([java_lang_Object, java_lang_Object], BOOLEAN);
	def SIG_EQUALV               = JvmSig.new([java_lang_Object], BOOLEAN);
	def SIG_INT_VOID             = JvmSig.new([INT], VOID);
	def SIG_INT_INT              = JvmSig.new([INT], INT);
	def SIG_INT_INT_INT          = JvmSig.new([INT, INT], INT);
	def SIG_INT_INT_INT_INT      = JvmSig.new([INT, INT, INT], INT);
	def SIG_FLOAT_FLOAT          = JvmSig.new([FLOAT], FLOAT);
	def SIG_FLOAT_FLOAT_BOOLEAN  = JvmSig.new([FLOAT, FLOAT], BOOLEAN);
	def SIG_DOUBLE_DOUBLE        = JvmSig.new([DOUBLE], DOUBLE);
	def SIG_DOUBLE_DOUBLE_BOOLEAN = JvmSig.new([DOUBLE, DOUBLE], BOOLEAN);
	def SIG_DOUBLE_FLOAT         = JvmSig.new([DOUBLE], FLOAT);
	def SIG_INT_FLOAT            = JvmSig.new([INT], FLOAT);
	def SIG_INT_BOOLEAN          = JvmSig.new([INT], BOOLEAN);
	def SIG_LONG_FLOAT           = JvmSig.new([LONG], FLOAT);
	def SIG_LONG_DOUBLE          = JvmSig.new([LONG], DOUBLE);
	def SIG_LONG_BOOLEAN         = JvmSig.new([LONG], BOOLEAN);
	def SIG_FLOAT_INT            = JvmSig.new([FLOAT], INT);
	def SIG_DOUBLE_LONG          = JvmSig.new([DOUBLE], LONG);
	def SIG_DOUBLE_BOOLEAN       = JvmSig.new([DOUBLE], BOOLEAN);
	def SIG_LONG_BYTE_BYTE_LONG  = JvmSig.new([LONG, BYTE, BYTE], LONG);
	def SIG_LONG_LONG_INT        = JvmSig.new([LONG, LONG], INT);
	def SIG_LONG_LONG_LONG       = JvmSig.new([LONG, LONG], LONG);
	def SIG_LONG_LONG_LONG_LONG  = JvmSig.new([LONG, LONG, LONG], LONG);
	def SIG_INT_INTEGER          = JvmSig.new([INT], java_lang_Integer);
	def SIG_STRING_VOID          = JvmSig.new([java_lang_String], VOID);
	def SIG_JAVA_MAIN            = JvmSig.new([STRING_ARRAY], JvmTypes.VOID);
	def SIG_VOID_BYTE_ARRAY      = JvmSig.new([], JvmTypes.BYTE_ARRAY);
	def SIG_DOUBLE_DOUBLE_DOUBLE_LONG  = JvmSig.new([DOUBLE, DOUBLE, DOUBLE], LONG);
	def SIG_DOUBLE_DOUBLE_DOUBLE_BOOLEAN  = JvmSig.new([DOUBLE, DOUBLE, DOUBLE], BOOLEAN);
	def SIG_BYTE_ARRAY_INT_INT_BYTE 	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, INT], BYTE);
	def SIG_BYTE_ARRAY_INT_INT_SHORT 	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, INT], SHORT);
	def SIG_BYTE_ARRAY_INT_INT_INT 		= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, INT], INT);
	def SIG_BYTE_ARRAY_INT_INT_LONG 	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, INT], LONG);
	def SIG_BYTE_ARRAY_INT_INT_FLOAT	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, INT], FLOAT);
	def SIG_BYTE_ARRAY_INT_INT_DOUBLE	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, INT], DOUBLE);
	def SIG_BYTE_ARRAY_INT_BYTE_INT_VOID	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, BYTE, INT], VOID);
	def SIG_BYTE_ARRAY_INT_SHORT_INT_VOID	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, SHORT, INT], VOID);
	def SIG_BYTE_ARRAY_INT_INT_INT_VOID	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, INT, INT], VOID);
	def SIG_BYTE_ARRAY_INT_LONG_INT_VOID	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, LONG, INT], VOID);
	def SIG_BYTE_ARRAY_INT_FLOAT_INT_VOID 	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, FLOAT, INT], VOID);
	def SIG_BYTE_ARRAY_INT_DOUBLE_INT_VOID 	= JvmSig.new([JvmTypes.BYTE_ARRAY, INT, DOUBLE, INT], VOID);
}
