// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

component JvmComponent {
	def RT_PATH    = Aeneas.options.newStringOption("jvm.rt-path", null);
	def GEN_SCRIPT = Aeneas.options.newBoolOption("jvm.script", false);
	def ARGS       = Aeneas.options.newStringOption("jvm.args", null);
	new() {
		Aeneas.registerTarget(JvmTarget.new("jvm-test", false, true));
		Aeneas.registerTarget(JvmTarget.new("jar", true, false));
	}
	def makeJvmRtFileName(fileName: string, ext: string) -> string {
		return Paths.assemble(RT_PATH.get(), fileName, ext);
	}
}

class JvmTarget extends Target {
	def jar: bool;
	def test: bool;
	new(name: string, jar, test) super(name) {
		this.typedMain = jar;
	}
	def emit(compiler: Compiler, prog: Program) {
		var jprog = JvmProgram.new(compiler, prog);
		jprog.makeJar = jar;
		jprog.test = test;
		jprog.build();
	}
	def configureCompiler(compiler: Compiler) {
		compiler.Reachability = true;
		compiler.PartialBoundsChecks = false;
		compiler.PartialSpecialization = false;
		compiler.RaDevirtualize = false;
		compiler.GenVariantEqual = true;
		compiler.MixedArrays = false;
//		compiler.setSignatureLimits(10000, 1);
	}
	def configureProgram(compiler: Compiler, prog: Program) {
		SystemCalls.install(prog);
	}
}
