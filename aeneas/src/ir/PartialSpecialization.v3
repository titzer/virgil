// Copyright 2012 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// tokens used to distinguish the ends of one polymorphic part from another
def TUPLE_MARK = '\xFF';
def RETURN = '\xFE';
def EQUAL = '\xFD';

// Analyzes polymorphic methods and specializes them according to the polymorphic
// operations they perform.
class Specializer(ra: ReachabilityAnalyzer, rn: ReachabilityNormalizer) {
	var uid = 1;
	var env: IrSpec;
	def specMap = IrUtil.newIrSpecMap<SpecMethod>();
	def virtMap = IrUtil.newIrSpecMap<SpecMethod>();
	def specSet = IrUtil.newIrItemMap<SpecSet>();
	var virtList: List<(RaClass, List<RaMethod>)>;
	def valMap = LazyMap.new(Values.hash, Values.equal, uniquify<Val>);
	def typeMap = LazyMap.new(Type.hash, Type.==, uniquify<Type>);
	def queue = WorkQueue.new();

	def specialize() {
		// create SpecMethod and SpecSet for each method of each class
		ra.classes.apply(createSpecs);
		// create the virtual specs
		Lists.apply(virtList, createVirtualSpec);
		virtList = null;
		// compute initial specialization sets based on operators
		specSet.apply(initialSplitBasedOnOperators);
		// iteratively specialize methods based on call graph
		queue.drain();
		// create the normalized/specialized versions of the method
		ra.liveMethods.apply(createNewIrMethod);
	}
	def createSpecs(rc: RaClass) {
		for (ml in rc.methods) {
			var vlist: List<RaMethod>;
			for (l = ml; l != null; l = l.tail) {
				var rm = l.head;
				if (rm.spec == null) continue;
				if (rm.isLive()) createInitialSpec(rm);
				if (rm.isVirtual()) vlist = List.new(rm, vlist);
			}
			if (vlist != null) virtList = List.new((rc, vlist), virtList);
		}
	}
	def createInitialSpec(rm: RaMethod) {
		var specMethod = SpecMethod.new(uid++, rm);
		specMap[rm.spec] = specMethod;
		// create initial SpecSet and add method
		var set = specSet[rm.orig];
		if (set == null) {
			set = SpecSet.new(uid++, rm.orig);
			specSet[rm.orig] = set;
		}
		set.put(specMethod);
	}
	def createVirtualSpec(rc: RaClass, list: List<RaMethod>) {
		// make the spec methods for all instantiations of this virtual method
		// in the same class instantiation
		if (list.tail == null) return; // only one instantiation; doesn't matter
		var m = list.head.orig, set = SpecSet.new(uid++, m), length = 0;
		// create a "virtual" node that calls all the implementations directly
		for (l = list; l != null; l = l.tail) {
			var sm = SpecMethod.new(uid++, l.head);
			virtMap[l.head.spec] = sm;
			sm.set = set;
			set.instantiations = List.new(sm, set.instantiations);
			// set up direct calls to all live implementations
			var calls = Vector<SpecEdge>.new();
			for (k = l.head.virtual.impls; k != null; k = k.tail) {
				calls.put(SpecEdge.new(calls.length, sm, specMap[k.head.spec]));
			}
			length = calls.length;
			sm.calls = calls.extract();
		}
		// treat the set of virtual nodes like other sets
		set.setDirty(length);
		queueMethod(set);
		set.dirtyAll();
	}
	def createNewIrMethod(rm: RaMethod) {
		if (rm.spec == null) return;
		var specMethod = specMap[rm.spec];
		if (specMethod.set == null) return;
		if (specMethod.set.newMethod != null) return;
		specMethod.set.getNewIrSpec(ra, rn);
		rm.norm = specMethod.set.newMethod;
	}
	def normCode(context: SsaContext, rm: RaMethod) -> bool {
		var spec = specMap[rm.spec];
		if (spec.set == null) return false;
		if (spec.set.newMethod.ssa != null) return true; // already done
		// normalize the code for the entire method set
		context.spec = spec.set.newIrSpec;
		var n = SsaRaNormalizer.new(context, rn);
		n.specSet = spec.set;
		n.build(spec.set.newMethod);
		rn.newIr.methods.put(spec.set.newMethod);
		return true;
	}
	def rankOf(it: IntType) -> byte {
		match (it.rank) {
			I32, SUBI32, U32, SUBU32 => return 0;
			_ => return 1;
		}
	}
	def polySignature(apply: SsaApplyOp, sig: SpecSignature) {
		// add any polymorphic keys to the current key array
		var exp = apply.op, ta = exp.typeArgs;
		var t0 = if(ta.length > 0, normType(ta[0]));
		match (exp.opcode) {
			OverloadedEq,
			VariantEq => {
				var kind = t0.oldType.typeCon.kind;
				match (kind) {
					ENUM => { sig.put(Kind.INT.tag); sig.put(rankOf(V3.getVariantTagType(t0.oldType))); }
					ENUM_SET => { sig.put(Kind.INT.tag); sig.put(rankOf(V3.getVariantTagType(t0.oldType))); }
					INT => { sig.put(Kind.INT.tag); sig.put(rankOf(IntType.!(t0.oldType))); }
					FLOAT => { sig.put(Kind.FLOAT.tag); sig.put(if(V3.isDouble(t0.oldType), 1, 0)); }
					POINTER,
					ARRAY,
					CLASS,
					FUNCREF => sig.put(Kind.CLASS.tag);
					VARIANT,
					TUPLE => {
						sig.put(kind.tag);
						sig.addOldType(this, t0);
					}
					_ => sig.put(kind.tag);
				}
			}
			TypeCast => {
				var t1 = normType(ta[1]);
				var cast = TypeSystem.newTypeCast(t0.oldType, t1.oldType);
				sig.put(cast.tag);
				match (cast) {
					TRUE,
					SUBSUME,
					THROW_IF_NOT_NULL,
					THROW => ; // these dont depend on types
					_ => {
						sig.addOldType(this, t0);
						sig.addOldType(this, t1);
					}
				}
			}
			TypeQuery => {
				var t1 = normType(ta[1]);
				var query = TypeSystem.newTypeQuery(t0.oldType, t1.oldType);
				sig.put(query.tag);
//				Terminal.put3("specializer: tquery %q.?<%q> = %s\n", t0.oldType.render, t1.oldType.render, query.name);
				match (query) {
					TRUE,
					TRUE_IF_NOT_NULL,
					FALSE => ;  // these dont depend on types
					_ => {
						sig.addOldType(this, t0);
						sig.addOldType(this, t1);
					}
				}
			}
			TypeSubsume => {
				sig.putType(this, t0);
				sig.putType(this, normType(ta[1]));
			}
			ArrayAlloc,
			ArrayInit,
			ArrayGetElem,
			ArraySetElem => {
				sig.putMemory(normType(V3Array.elementType(ta[0])).newType);
			}
			NullCheck,
			BoundsCheck,
			ArrayGetLength => {
				sig.putInt(t0.size);
			}
			ClassAlloc(method) => {
				// XXX: could be improved if the class representation is known
				sig.putType(this, t0);
				if (sig.first) {
					var spec = V3Op.extractIrSpec(exp, method);
					if (spec.member != null) sig.addDirectCall(spec);
				}
			}
			ClassGetField(field) => putField(apply, field, sig);
			ClassInitField(field) => putField(apply, field, sig);
			ClassSetField(field) => putField(apply, field, sig);
			ClassGetVirtual(method) => {
				sig.putType(this, t0);
				if (sig.first) sig.addVirtualCall(V3Op.extractIrSpec(exp, method));
			}
			ClassGetMethod(method) => {
				if (sig.first) sig.addDirectCall(V3Op.extractIrSpec(exp, method));
			}
			CreateClosure(method) => {
				if (sig.first) sig.addDirectCall(V3Op.extractIrSpec(exp, method));
			}
			TupleCreate => {
				sig.putInt(t0.size);
			}
			TupleGetElem(index) => {
				var tn = TupleNorm.!(t0);
				sig.putInt(tn.offsets[index]); // add offset
				sig.putInt(tn.nested[index].size); // add length
			}
			CallClassVirtual(method) => {
				sig.putType(this, t0);
				if (sig.first) sig.addVirtualCall(V3Op.extractIrSpec(exp, method));
				putCallConv(sig, exp.sig);
			}
			CallMethod(method) => {
				if (sig.first) sig.addDirectCall(V3Op.extractIrSpec(exp, method));
				putCallConv(sig, exp.sig);
			}
			CallClosure => {
				putCallConv(sig, exp.sig);  // TODO XXX: can ignore closure?
			}
			SystemCall => {
				sig.putType(this, t0); // full specialization
			}
			_ => ;
		}
	}
	def putField(apply: SsaApplyOp, field: IrField, sig: SpecSignature) {
		var rf = ra.makeField(apply, field, env);
		var nf = rf.liveFields(ra);
		if (nf.length == 0) {
			sig.putInt(-1);
			sig.putVal(this, rf.val);
		} else {
			sig.putInt(nf[0].asField().machOffset);
			sig.putMemory(rf.typeNorm.newType);
		}
	}
	def putCallConv(spec: SpecSignature, sig: Signature) {
		// TODO: should use actual calling convention from backend
		for (p in sig.paramTypes) {
			if (p.open()) spec.addParam(normType(p).newType);
		}
		spec.put(RETURN);
		for (p in sig.returnTypes) {
			if (p.open()) spec.addParam(normType(p).newType);
		}
	}
	def normType(t: Type) -> TypeNorm {
		return rn.norm(env.instantiateType(t));
	}
	def splitBasedOnCalls(poly: SpecSet) {
//		Terminal.put1("checkCalls %d\n", poly.uid);
		poly.queued = false;
		if (poly.split) return;
		var sig = SpecSignature.new();
		for (lm = poly.instantiations; lm != null; lm = lm.tail) {
			processCalls(sig, poly, lm.head);
		}
		poly.clean();
		if (sig.root.splits != null) {
			sig.root.apply(splitSpecSet(poly, false, _));
		}
	}
	def initialSplitBasedOnOperators(m: IrItem, poly: SpecSet) {
		var list = poly.instantiations;
		if (list.tail == null) return; // only a single instantiation

		// add all the polymorphic properties to the splitter
		var sig = SpecSignature.new();
		for (l = list; l != null; l = l.tail) {
			// for each instantiation
			this.env = l.head.raMethod.spec;
			// add calling convention of specialized method
			putCallConv(sig, poly.method.sig);
			for (op = ra.polyMap[env.asMethod()]; op != null; op = op.tail) {
				// visit each polymorphic operator
				if (op.head != null) polySignature(op.head, sig);
			}
			sig.finish(l.head);
		}
		poly.allocCalls(sig);
		if (sig.root.splits != null) {
			// not all polymorphic signatures match; split into subsets
			sig.root.apply(splitSpecSet(poly, true, _));
		} else {
			// all polymorphic signatures match; don't need to split
			poly.attachToSpecMethods();
			queueMethod(poly);
			poly.dirtyAll();
		}
		if (poly.dirty.length == 0) return; // no direct or indirect calls
		for (l = list; l != null; l = l.tail) {
			var meth = l.head;
			if (meth.set == null) continue; // method is already fully specialized
			var calls = Vector<SpecEdge>.new().grow(poly.dirty.length);
			addDirectCallEdges(sig, calls, meth);
			addVirtualCallEdges(sig, calls, meth);
			meth.calls = calls.extract();
		}
	}
	def addDirectCallEdges(sig: SpecSignature, calls: Vector<SpecEdge>, meth: SpecMethod) {
		for (c in sig.directCalls.copy()) { // XXX: no need to make a copy
			var dest = specMap[meth.raMethod.spec.instantiateIrSpec(c)];
			calls.put(SpecEdge.new(calls.length, meth, dest));
			Terminal.put2("direct edge %q -> %q\n", meth.raMethod.orig.render, dest.raMethod.orig.render);
		}
	}
	def addVirtualCallEdges(sig: SpecSignature, calls: Vector<SpecEdge>, meth: SpecMethod) {
		for (c in sig.virtualCalls.copy()) { // XXX: no need to make a copy
			var dest = virtMap[meth.raMethod.spec.instantiateIrSpec(c)];
			calls.put(SpecEdge.new(calls.length, meth, dest));
			Terminal.put2("virtual edge %q -> %q\n", meth.raMethod.orig.render, dest.raMethod.orig.render);
		}
	}
	// split a SpecSet into a new subset that only includes the given list
	def splitSpecSet(orig: SpecSet, dirty: bool, list: List<SpecMethod>) {
		orig.split = true;
		Lists.apply(list, dirtyIncomingEdges);
		if (list.tail == null) {
			list.head.set = null; // only a single instantiation
			return;
		}
		var poly = SpecSet.new(uid++, orig.method);
		poly.instantiations = list;
		poly.setDirty(orig.dirty.length);
		poly.attachToSpecMethods();
		if (dirty) {
			queueMethod(poly);
			poly.dirtyAll();
		}
	}
	def dirtyIncomingEdges(dst: SpecMethod) {
		for (edge = dst.useList; edge != null; edge = SpecEdge.!(edge.next)) {
			var src = edge.src.set;
			if (src != null) {
				src.dirty[edge.index] = true;
				queueMethod(src);
			}
		}
	}
	def processCalls(sig: SpecSignature, poly: SpecSet, src: SpecMethod) {
		var dirty = poly.dirty;
		for (i < dirty.length) {
			if (!dirty[i]) continue;
			var meth = src.calls[i].dest;
			if (meth == null) sig.putInt(0);
			else sig.putInt(if(meth.set == null, meth.uid, meth.set.uid));
		}
		sig.finish(src);
	}
	def queueMethod(poly: SpecSet) {
		if (!poly.queued && poly.dirty.length > 0) {
			poly.queued = true;
			queue.add(splitBasedOnCalls, poly);
		}
	}
}
// Represents a potentially specialized method, which may or may not be in a set.
class SpecMethod(uid: int, raMethod: RaMethod) {
	var calls: Array<SpecEdge>;
	var set: SpecSet;
	var useList: SpecEdge;
	def getNormIndex() -> int {
		return if(set != null, set.normIndex, raMethod.normIndex);
	}
	def setNormIndex(i: int) {
//		Terminal.put2("  setNormIndex %q = %d\n", raMethod.getSpec().render, i);
		if (set != null && set.normIndex == -1) {
			set.normIndex = i;
			if (set.newMethod != null) set.newMethod.index = i; // TODO
		}
		if (raMethod.normIndex < 0) raMethod.normIndex = i;
		if (raMethod.norm.index < 0) raMethod.norm.index = i;
	}
}
// A set of polymorphic instantiations of a method.
class SpecSet(uid: int, method: IrMethod) {
	var newMethod: IrMethod;		// new IrMethod
	var newIrSpec: IrSpec;			// IrSpec with sets substituted in
	var instantiations: List<SpecMethod>;	// instantiations of the method
	var dirty: Array<bool>;			// which outgoing call edges are dirty
	var queued: bool;			// currently on the queue
	var split: bool;			// this set has been split
	var normIndex = -1;			// new method index

	def put(meth: SpecMethod) {
		instantiations = List.new(meth, instantiations);
	}
	def allocCalls(sig: SpecSignature) {
		setDirty(sig.directCalls.length + sig.virtualCalls.length);
	}
	def setDirty(length: int) {
		dirty = Array<bool>.new(length);
	}
	def attachToSpecMethods() {
		for (l = instantiations; l != null; l = l.tail) l.head.set = this;
	}
	def dirtyAll() {
		if (dirty == null) return;
		for (i < dirty.length) dirty[i] = true;
	}
	def clean() {
		if (dirty == null) return;
		for (i < dirty.length) dirty[i] = false;
	}
	def first() -> IrSpec {
		return instantiations.head.raMethod.spec;
	}
	def getNewIrSpec(ra: ReachabilityAnalyzer, rn: ReachabilityNormalizer) -> IrSpec {
		if (newIrSpec != null) return newIrSpec;
		// gather two lists of type args from the instantiations
		var l1: List<TypeArgs>, l2: List<TypeArgs>;
		for (l = instantiations; l != null; l = l.tail) {
			var tt = l.head.raMethod.spec.getTypes().typeArgsList;
			l1 = List.new(tt.head, l1);
			if (tt.tail != null) l2 = List.new(tt.tail.head, l2);
		}
		var typeArgsList = List.new(buildNewTypeArgs(l1), null);
		if (l2 != null) typeArgsList = List.new(buildNewTypeArgs(l2), typeArgsList);
		var methodTypeArgs: TypeArgs;
		var typeArgs: Array<Type>;
		// find the one with method type arguments (if any)
		if (method.typeArgs != null) {
			for (l = typeArgsList; l != null; l = l.tail) {
				if (method.typeArgs.typeEnv == l.head.typeEnv) methodTypeArgs = l.head;
			}
			typeArgs = Array<Type>.new(1 + methodTypeArgs.types.length);
			Arrays.copyInto(methodTypeArgs.types, typeArgs, 1);
		} else {
			typeArgs = Array<Type>.new(1);
		}
		// instantiate IrSpecTypes
		var types = IrSpecTypes.new(typeArgsList, methodTypeArgs);
		var receiver = types.instantiateType(this.method.receiver);
		// instantiate IrSpec
		newIrSpec = IrSpec.new(typeArgs[0] = receiver, typeArgs, method);
		newIrSpec.types = types; // no need to rebuild IrSpecTypes next time

		var tn = FuncNorm.!(rn.norm(newIrSpec.getMethodType()));
		var newSig = FuncType.!(tn.sub[0]).sig();
		newMethod = IrMethod.new(receiver, methodTypeArgs, newSig);
		newMethod.source = method.source;
		for (l = instantiations; l != null; l = l.tail) {
			l.head.raMethod.norm = newMethod;
		}
		return newIrSpec;
	}
	def buildNewTypeArgs(list: List<TypeArgs>) -> TypeArgs {
		return list.head; // just return the first instantiation
	}
}
// A customized datastructure that divides a set of SpecMethod's based on
// equivalent "signatures", which are sequences of bytes representing the
// polymorphism of the operations.
class SpecSignature {
	def root = SpecSigNode.new();
	def directCalls = Vector<IrSpec>.new();
	def virtualCalls = Vector<IrSpec>.new();
	var cur = root;
	var first = true;

	def addDirectCall(spec: IrSpec) {
		directCalls.put(spec);
	}
	def addVirtualCall(spec: IrSpec) {
		virtualCalls.put(spec);
	}
	def put(key: byte) {
		cur = cur.put(key);
	}
	// add a type to the signature
	def putType(spec: Specializer, t: TypeNorm) {
		putInt(spec.typeMap[t.newType]);
	}
	def addOldType(spec: Specializer, t: TypeNorm) {
		putInt(spec.typeMap[t.oldType]);
	}
	def putVal(spec: Specializer, v: Val) {
		if (v == null) putInt(0);
		else putInt(spec.valMap[v]);
	}
	// add an integer to the signature
	def putInt(h: int) {
		put(byte.!(h));
		put(byte.!(h >>> 8));
		put(byte.!(h >>> 16));
		put(byte.!(h >>> 24));
	}
	// add a memory reference to the signature
	def putMemory(t: Type) {
		match (t.typeCon.kind) {
			BOOL => put('\x01');
			INT => put(byte.!(IntType.!(t).byteSize));
			ARRAY => put('\x09');
			CLASS => put('\x09');
			CLOSURE => put('\x0a');
			FUNCREF => put('\x0b'); // XXX: could be equal to the word size of target.
			COMPONENT,
			VOID => put('\x00');
			FLOAT => put(if(V3.isDouble(t), '\x0c', '\x0d'));
			TUPLE => {
				put(TUPLE_MARK);
				Lists.apply(Tuple.toTypeList(t), putMemory);
				put(TUPLE_MARK);
			}
			_ => ; // TODO
		}
	}
	// add a parameter type to the signature
	def addParam(t: Type) {
		match (t.typeCon.kind) {
			BOOL => put('\x04');
			INT => put(if(IntType.!(t).byteSize > 4, '\x06', '\x04')); // XXX: machine-dependent
			ARRAY => put('\x05');
			CLASS => put('\x05');
			CLOSURE => put('\x08');
			FUNCREF => put('\x04');
			FLOAT => put(if(V3.isDouble(t), '\x0c', '\x0d'));
			TUPLE => {
				put(TUPLE_MARK);
				Lists.apply(Tuple.toTypeList(t), addParam);
				put(TUPLE_MARK);
			}
			_ => ; // TODO
		}
	}
	// finish the current array and associate it with the given item
	def finish(item: SpecMethod) {
		cur.finish(item);
		first = false;
		cur = root;
		cur.cursor = 0;
	}
}
class SpecSigNode {
	var list: List<SpecMethod>;
	var keys = Vector<byte>.new();
	var splits: Vector<List<SpecSigNode>>;
	var cursor: int;
	// start the current key
	// add one key to the current array
	def put(key: byte) -> SpecSigNode {
		if (list == null) {
			// no elements in the set yet, just add the key
			keys.put(key);
			return this;
		}
		// reached the end of the keys?
		if (cursor == keys.length) return split(key);
		// doesn't match the current key?
		if (keys[cursor] != key) return split(key);
		cursor++;
		// matches the current key, everything is fine
		return this;
	}
	def split(key: byte) -> SpecSigNode {
		if (splits == null) {
			// no splits yet
			splits = Vector.new();
		}
		if (splits.length < cursor + 1) {
			// splits don't extend to cover this position
			splits.grow(cursor + 1);
			splits.length = cursor + 1;
		}
		for (l = splits[cursor]; l != null; l = l.tail) {
			// search for an existing split with a matching key
			if (l.head.keys[cursor] == key) {
				l.head.cursor = cursor + 1;
				return l.head;
			}
		}
		// no existing split found, create a new one
		var set = SpecSigNode.new(), nkeys = Vector<byte>.new();
		set.keys = nkeys.grow(keys.length);
		for (i < cursor) {
			nkeys.put(keys[i]);
		}
		nkeys.put(key);
		set.cursor = cursor + 1;
		// insert new split into the list
		splits[cursor] = List.new(set, splits[cursor]);
		return set;
	}
	// finish the current array and associate it with the given item
	def finish(item: SpecMethod) {
		// TODO: check against the length
		list = List.new(item, list);
	}
	// visit all the collected sets of items
	def apply(f: List<SpecMethod> -> void) {
		if (list != null) f(list);
		if (splits != null) {
			for (i < splits.length) {
				for (l = splits[i]; l != null; l = l.tail) {
					l.head.apply(f);
				}
			}
		}
	}
}
// An edge representing a direct use (e.g. a Call*Method or Get*Method) between methods.
class SpecEdge extends Edge<SpecMethod> {
	def index: int;
	new(index, src: SpecMethod, dst: SpecMethod) super(src, dst) { }
	def getListHead(dest: SpecMethod) -> SpecEdge { return dest.useList; }
	def setListHead(dest: SpecMethod, e: Edge<SpecMethod>) { dest.useList = SpecEdge.!(e); }
}
def uniquify<T>(v: T) -> int {
	return ++UID.next;
}
