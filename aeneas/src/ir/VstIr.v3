// Copyright 2013 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Builds IR classes and variants from VST classes and variants.
class IrBuilder(ctype: Type, parent: IrClass) {
	def fields = Vector<IrField>.new();
	def methods = Vector<IrMethod>.new();

	new() {
		if (parent == null) {
			methods.add(null); // reserve index 0 for constructor
			methods.add(null); // reserve index 1 for variant equals method
		} else {
			fields.addN(parent.fields);
			methods.addN(parent.methods);
			methods[0] = null; // overwrite super constructor
		}
	}
	def buildClass(decl: VstCompound) -> IrClass {
		fields.grow(decl.numFields);
		methods.grow(decl.numMethods + 1);
		var isVariant = VstClass.?(decl) && VstClass.!(decl).isVariant();
		for (list = decl.members; list != null; list = list.tail) {
			var m = list.head;
			if (VstField.?(m)) addVstField(VstField.!(m), isVariant);
			else if (VstNew.?(m)) addVstNew(VstNew.!(m), isVariant);
			else if (VstMethod.?(m)) addVstMethod(VstMethod.!(m));
		}
		return build();
	}
	def addVstField(f: VstField, isVariant: bool) {
		var ir = IrField.new(ctype, f.getType());
		ir.source = f;
		if (f.isReadOnly) ir.setFact(Fact.F_VALUE | Fact.O_FOLDABLE);
		if (isVariant) ir.setFact(Fact.F_VALUE | Fact.O_FOLDABLE | Fact.O_PURE);
		addIrField(ir);
		f.index = ir.index;
	}
	def addVstNew(m: VstNew, isVariant: bool) {
		// constructors always occupy slot 0
		m.index = 0;
		if (m.nontrivial()) {
			// only generate an IrMethod if the constructor is nontrivial
			var sig = getSignature(m, true);
			var ir = IrMethod.new(ctype, null, sig);
			ir.source = m;
			ir.facts |= Fact.M_NEW;
			if (isVariant) ir.setFact(Fact.O_PURE | Fact.M_INLINE | Fact.V_NON_ZERO);
			setIrMethod(0, ir);
		}
	}
	def addVstMethod(m: VstMethod) {
		var ir = newIrMethod(m);
		var r = m.root;
		if (r == m) {
			// this is the root method of a method family
			addIrMethod(ir);
		} else {
			// this is an override
			parent.methods[r.index].facts |= Fact.M_OVERRIDDEN;
			ir.facts |= Fact.M_OVERRIDE;
			setIrMethod(r.index, ir);
		}
		m.index = ir.index;
	}
	def newIrMethod(m: VstMethod) -> IrMethod {
		var typeArgs: TypeArgs;
		if (m.typeParams != null) typeArgs = TypeArgs.new(m.typeEnv, null);
		var sig = getSignature(m, m.returnThis);
		var ir = IrMethod.new(ctype, typeArgs, sig);
		ir.source = m;
		if (EmptyStmt.?(m.body)) ir.facts |= Fact.M_EMPTY;
		return ir;
	}
	def getSignature(m: VstMethod, returnThis: bool) -> Signature {
		var vec = Vector<Type>.new();
		for (l = m.params.list; l != null; l = l.tail) {
			vec.add(l.head.vtype);
		}
		var paramTypes = vec.extract();
		var returnType = if(returnThis, m.receiver.getDeclaredType(), m.rettype.getType());
		return Signature.new(null, paramTypes, [returnType]);
	}
	def build() -> IrClass {
		var typeArgs: TypeArgs;
		if (V3.isPolymorphicClass(ctype)) {
			var decl = V3.classDecl(ctype);
			typeArgs = TypeArgs.new(decl.typeEnv, null);
		}
		return IrClass.new(ctype, typeArgs, parent, fields.extract(), methods.extract());
	}
	def addIrField(f: IrField) {
		setIndex(fields.length, f);
		fields.add(f);
	}
	def addIrMethod(m: IrMethod) {
		if (m.facts.M_NEW) {
			methods[0] = m;
			setIndex(0, m);
		} else {
			setIndex(methods.length, m);
			methods.add(m);
		}
	}
	def setIrMethod(index: int, m: IrMethod) {
		var p = methods[index];
		if (p != null && p != m) {
			p.facts |= Fact.M_OVERRIDDEN;
			m.facts |= Fact.M_OVERRIDE;
		}
		methods[index] = m;
		setIndex(index, m);
	}
	def setIndex(index: int, m: IrMember) {
		if (m.index >= 0) {
			if (m.index != index) return V3.fail(Strings.format2("member %1 at wrong index %2", m.render, index));
		} else {
			m.index = index;
		}
	}
}
