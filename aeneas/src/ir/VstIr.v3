// Copyright 2013 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Builds IR classes and variants from VST classes and variants.
component VstIr {
	def typeVarElim = TypeVarEliminator.new(null, Void.TYPE);
	def elimTypeVars(t: Type) -> Type {
		if (t == null) return t;
		if (0 == (t.hash & TypeConst.TYPEVAR_MASK)) return t;
		return t.substitute(typeVarElim);
	}
	def specOf(ir: IrModule, receiver: Type, member: VstMember, typeArgs: TypeArgs) -> IrSpec {
		receiver = elimTypeVars(receiver);
		var ic = ir.makeIrClass(receiver), m: IrMember;
		if (VstField.?(member)) m = ic.fields[member.index];
		else m = ic.methods[member.index];

		var newTa = [receiver];
		if (typeArgs != null) {
			// add method type parameters to the type arguments array
			var oldTa = typeArgs.types;
			newTa = Array<Type>.new(1 + oldTa.length);
			newTa[0] = receiver;
			for (i < oldTa.length) {
				newTa[i + 1] = elimTypeVars(oldTa[i]);
			}
		}
		return IrSpec.new(receiver, newTa, m);
	}
}
class IrBuilder(ctype: Type, parent: IrClass) {
	def fields = Vector<IrField>.new();
	def methods = Vector<IrMethod>.new();

	new() {
		if (parent == null) {
			methods.put(null); // reserve index 0 for constructor
			methods.put(null); // reserve index 1 for variant equals method
		} else {
			fields.puta(parent.fields);
			methods.puta(parent.methods);
			methods[0] = null; // overwrite super constructor
		}
	}
	def buildClass(decl: VstCompound) -> IrClass {
		fields.grow(decl.numFields);
		methods.grow(decl.numMethods + 1);
		var boxing = Boxing.AUTO, isVariant = false;
		match (decl) {
			cdecl: VstClass => {
				isVariant = cdecl.isVariant();
				if (cdecl.repHints != null) {
					for (l = cdecl.repHints; l != null; l = l.tail) {
						match (l.head) {
							Boxed => boxing = Boxing.BOXED;
							Unboxed => boxing = Boxing.UNBOXED;
							_ => ;
						}
					}
				}
			}
		}
		for (list = decl.members; list != null; list = list.tail) {
			var m = list.head;
			if (VstField.?(m)) addVstField(VstField.!(m), isVariant);
			else if (VstNew.?(m)) addVstNew(VstNew.!(m), isVariant);
			else if (VstMethod.?(m)) addVstMethod(VstMethod.!(m));
		}
		var ic = build();
		ic.boxing = boxing;
		return ic;
	}
	def addVstField(f: VstField, isVariant: bool) {
		var ir = IrField.new(ctype, f.getType());
		ir.source = f;
		if (f.writability == Writability.READ_ONLY) ir.setFact(Fact.F_VALUE | Fact.O_FOLDABLE);
		if (isVariant) ir.setFact(Fact.F_VALUE | Fact.O_FOLDABLE | Fact.O_PURE);
		addIrField(ir);
		f.index = ir.index;
	}
	def addVstNew(m: VstNew, isVariant: bool) {
		// constructors always occupy slot 0
		m.index = 0;
		if (m.nontrivial()) {
			// only generate an IrMethod if the constructor is nontrivial
			var sig = makeSignature(m, true);
			var ir = IrMethod.new(ctype, null, sig);
			ir.source = m;
			ir.facts |= Fact.M_NEW;
			if (isVariant) ir.setFact(Fact.O_PURE | Fact.M_INLINE | Fact.V_NON_ZERO);
			setIrMethod(0, ir);
		}
	}
	def addVstMethod(m: VstMethod) {
		var ir = newIrMethod(m);
		if (m.importName != null) ir.facts |= Fact.M_NEVER_INLINE;
		var r = m.root;
		if (r == m) {
			// this is the root method of a method family
			addIrMethod(ir);
		} else {
			// this is an override
			parent.methods[r.index].facts |= Fact.M_OVERRIDDEN;
			ir.facts |= Fact.M_OVERRIDE;
			setIrMethod(r.index, ir);
		}
		m.index = ir.index;
	}
	def newIrMethod(m: VstMethod) -> IrMethod {
		var typeArgs: TypeArgs;
		if (m.typeParams != null) typeArgs = TypeArgs.new(m.typeEnv, null);
		var sig = makeSignature(m, m.returnThis);
		var ir = IrMethod.new(ctype, typeArgs, sig);
		ir.source = m;
		if (CLOptions.INLINE.val.matches(m)) ir.facts |= Fact.M_INLINE; // XXX: better location to set this?
		if (EmptyStmt.?(m.body)) ir.facts |= Fact.M_EMPTY;
		return ir;
	}
	def makeSignature(m: VstMethod, returnThis: bool) -> Signature {
		var vec = Vector<Type>.new();
		for (l = m.params.list; l != null; l = l.tail) {
			vec.put(l.head.vtype);
		}
		var paramTypes = vec.extract();
		var returnType = if(returnThis, m.receiver.getDeclaredType(), m.getReturnType());
		var returnTypes = if(returnType == Void.TYPE, TypeUtil.NO_TYPES, [returnType]);
		return Signature.new(null, paramTypes, returnTypes);
	}
	def build() -> IrClass {
		var typeArgs: TypeArgs;
		if (ctype.typeCon.arity > 0) {
			var decl = V3.classDecl(ctype);
			typeArgs = TypeArgs.new(decl.typeEnv, null);
		}
		return IrClass.new(ctype, typeArgs, parent, fields.extract(), methods.extract());
	}
	def addIrField(f: IrField) {
		setIndex(fields.length, f);
		fields.put(f);
	}
	def addIrMethod(m: IrMethod) {
		if (m.facts.M_NEW) {
			methods[0] = m;
			setIndex(0, m);
		} else {
			setIndex(methods.length, m);
			methods.put(m);
		}
	}
	def setIrMethod(index: int, m: IrMethod) {
		var p = methods[index];
		if (p != null && p != m) {
			p.facts |= Fact.M_OVERRIDDEN;
			m.facts |= Fact.M_OVERRIDE;
		}
		methods[index] = m;
		setIndex(index, m);
	}
	def setIndex(index: int, m: IrMember) {
		if (m.index >= 0) {
			if (m.index != index) return V3.fail(Strings.format2("member %q at wrong index %d", m.render, index));
		} else {
			m.index = index;
		}
	}
}
