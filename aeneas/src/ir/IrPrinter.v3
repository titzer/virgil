// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

class IrPrinter(prog: Program) {
	def buf = TerminalBuffer.new();
	var printIc: bool;
	var printSsa: bool;

	def print() {
		prog.ir.classes.apply(printIrClass);
	}
	def printIrClass(ic: IrClass) {
		buf.putc('#');
		buf.cyan().puti(ic.uid).end();
		if (V3.isClass(ic.ctype)) buf.puts(" class ");
		if (V3.isVariant(ic.ctype)) buf.puts(" variant ");
		if (V3.isComponent(ic.ctype)) buf.puts(" component ");
		buf.green().put1(ic.ctype.render).end().sp();
		var superType = V3.getSuperType(ic.ctype);
		if (superType != null) {
			buf.puts("extends ");
			buf.cyan().put1(superType.render).end().sp();
		}
		buf.puts("[").puti(ic.minClassId).csp().puti(ic.maxClassId).puts("] ");
		printFacts(ic.facts);
		buf.puts(" {\n");
		for (f in ic.fields) printField(f, ic);
		for (m in ic.methods) printMethod(m, ic);
		buf.puts("}");
		ln();
	}
	def printField(f: IrField, ic: IrClass) {
		if (f == null) return;
		buf.puts("    ");
		// XXX: "%cyan{$f.index}#${f.uid}: field %green{$f.render}: ${f.fieldType.render} "
		buf.cyan().puti(f.index).putc('#').puti(f.uid).end();
		buf.puts(": field  ");
		printLong(ic, f);
		buf.puts(": ");
		buf.cyan().put1(f.fieldType.render).end().sp();
		printFacts(f.facts);
		ln();
	}
	def printMethod(m: IrMethod, ic: IrClass) {
		if (m == null) return;
		printMethodName(m, ic);
		if (printSsa && m.ssa != null) doSsa(m);
		if (printIc && m.ic != null) doIc(m.ic);
	}
	def printMethodName(m: IrMethod, ic: IrClass) {
		buf.puts("    ");
		buf.cyan().puti(m.index).putc('#').puti(m.uid).end();
		buf.puts(": method ");
		printLong(ic, m);
		buf.puts(": ");
		buf.cyan().put1(m.sig.funcType().render).end().sp();
		printFacts(m.facts);
		ln();
	}
	def ln() {
		buf.ln();
		Terminal.putb(buf);
		buf.reset();
	}
	def doIc(icm: IcMethod) {
		var p = IcPrinter.new(null);
		p.printConsts(icm.consts);
		p.printCode(icm.iccode);
	}
	def doSsa(m: IrMethod) {
		SsaPrinter.new().printGraph(null, null, m.ssa, m.sig);
	}
	def printFacts(facts: Fact.set) {
		if (facts != Facts.NONE) {
			buf.putc('[');
			buf.yellow();
			Facts.renderFacts(buf, facts);
			buf.end().putc(']');
		}
	}
	def printLong(ic: IrClass, m: IrMember) {
		if (ic != null && ic.inherits(m)) {
			m.renderLong(buf.sp());
		} else {
			buf.yellow().putc('*');
			m.renderLong(buf.green()).end();
		}
	}
}
