// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

class IrPrinter(prog: Program) {
	def buf = TerminalBuffer.new();
	var printSsa: bool;

	def print() {
		prog.ir.classes.apply(printIrClass);
	}
	def printIrClass(ic: IrClass) {
		buf.putc('#');
		buf.cyan().putd(ic.uid).end();
		if (V3.isVariant(ic.ctype)) buf.puts(" variant ");
		else if (V3.isClass(ic.ctype)) buf.puts(" class ");
		if (V3.isComponent(ic.ctype)) buf.puts(" component ");
		ic.ctype.render(buf.green());
		buf.end().sp();
		var superType = V3.getSuperType(ic.ctype);
		if (superType != null) {
			buf.puts("extends ");
			superType.render(buf.cyan());
			buf.end().sp();
		}
		buf.puts("[").putd(ic.minClassId).csp().putd(ic.maxClassId).puts("] ");
		printFacts(ic.facts);
		buf.puts(" {\n");
		for (f in ic.fields) printField(f, ic);
		for (m in ic.methods) printMethod(m, ic);
		buf.puts("}");
		buf.outln();
	}
	def printField(f: IrField, ic: IrClass) {
		if (f == null) return;
		buf.puts("    ");
		// XXX: "%cyan{$f.index}#${f.uid}: field %green{$f.render}: ${f.fieldType.render} "
		buf.cyan().putd(f.index).putc('#').putd(f.uid).end();
		buf.puts(": field  ");
		printLong(ic, f);
		buf.puts(": ");
		f.fieldType.render(buf.cyan());
		buf.end().sp();
		printFacts(f.facts);
		buf.outln();
	}
	def printMethod(m: IrMethod, ic: IrClass) {
		if (m == null) return;
		printMethodName(m, ic);
		if (printSsa && m.ssa != null) doSsa(m);
	}
	def printMethodName(m: IrMethod, ic: IrClass) {
		buf.puts("    ");
		buf.cyan().putd(m.index).putc('#').putd(m.uid).end();
		buf.puts(": method ");
		printLong(ic, m);
		buf.puts(": ");
		m.sig.funcType().render(buf.cyan());
		buf.end().sp();
		printFacts(m.facts);
		buf.outln();
	}
	def doSsa(m: IrMethod) {
		SsaPrinter.new().printGraph(null, null, m.ssa, m.sig);
	}
	def printFacts(facts: Fact.set) {
		if (facts != Facts.NONE) {
			buf.putc('[');
			buf.yellow();
			Facts.renderFacts(buf, facts);
			buf.end().putc(']');
		}
	}
	def printLong(ic: IrClass, m: IrMember) {
		if (ic != null && ic.inherits(m)) {
			m.renderLong(buf.sp());
		} else {
			buf.yellow().putc('*');
			m.renderLong(buf.green());
			buf.end();
		}
	}
}
