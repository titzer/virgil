// Copyright 2014 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def eax = X86Regs.EAX;
def ebx = X86Regs.EBX;
def ecx = X86Regs.ECX;
def edx = X86Regs.EDX;
def edi = X86Regs.EDI;
def esi = X86Regs.ESI;
def ebp = X86Regs.EBP;
def esp = X86Regs.ESP;

def gen_i64_divmod_stub(mach: MachProgram, addr: Addr, u: MachDataEncoder, codeStartOffset: int, op: WideDivision) {
	var asm = X86MacroAssembler.new(mach, u, codeStartOffset);
	asm.wdiv_full(op, null, true);
	asm.ret();
}

// An extended X86Assembler that has additional machine-level utilities, such as
// recording patch locations and translating between regset locations and x86
// registers/memory.
class X86MacroAssembler extends X86Assembler {
	def mach: MachProgram;		  // machine program
	def machEncoder: MachDataEncoder; // machine-level encoder
	def codeStartOffset: int;

	new(mach, machEncoder, codeStartOffset) super(machEncoder) { }
	def codeOffset() -> int {
		return encoder.pos - codeStartOffset;
	}
	// call an absolute address and record the patch location
	def call_addr(addr: Addr) {
		call(X86Addrs.REL_CONST);
		machEncoder.recordPatch(addr, machEncoder.pos - 4);
	}
	// jump (conditionally) to an absolute address and record the patch location
	def jmpx_addr(cond: X86Cond, addr: Addr) {
		jmpx(cond, X86Addrs.REL_CONST);
		machEncoder.recordPatch(addr, machEncoder.pos - 4);
	}
	// a macro to move between any two register/memory operands
	def movd_rm_rm(d: X86Rm, s: X86Rm, scratch: X86Reg) {
		if (s == d) return;
		if (X86Reg.?(d)) return movd_r_rm(X86Reg.!(d), s);
		if (X86Reg.?(s)) return movd_rm_r(d, X86Reg.!(s));
		if (scratch != null) {
			movd_r_rm(scratch, s);
			movd_rm_r(d, scratch);
		}
		// XXX: use XMM register for memory-memory move instead of stack
		push(s);            // push value from source memory onto the stack
		pop(X86Addr.!(d));  // pop value off stack into destination memory
	}
	// a macro to move a value into a location
	def movd_l_val(frame: MachFrame, loc: int, val: Val) {
		var d = loc_rm(frame, loc);
		if (val == null) {
			movd_rm_i(d, 0);
		} else if (Addr.?(val)) {
			var pos = encoder.pos;
			movd_rm_i(d, X86Addrs.ABS_CONST);
			recordPatch(pos, Addr.!(val));
		} else {
			movd_rm_i(d, V3.unboxIntegral(val));
		}
	}
	// convert a location into an x86 register
	def loc_r(frame: MachFrame, loc: int) -> X86Reg {
		match(loc) {
			X86MachRegs.EAX: return eax;
			X86MachRegs.EBX: return ebx;
			X86MachRegs.ECX: return ecx;
			X86MachRegs.EDX: return edx;
			X86MachRegs.ESI: return esi;
			X86MachRegs.EDI: return edi;
			X86MachRegs.EBP: return ebp;
		}
		return failLocation("required x86 register", loc, frame.conv.regSet);
	}
	// convert a location into an x86 register/memory reference
	def loc_rm(frame: MachFrame, loc: int) -> X86Rm {
		match(loc) {
			0: return failLocation("unassigned location", loc, frame.conv.regSet);
			X86MachRegs.EAX: return eax;
			X86MachRegs.EBX: return ebx;
			X86MachRegs.ECX: return ecx;
			X86MachRegs.EDX: return edx;
			X86MachRegs.ESI: return esi;
			X86MachRegs.EDI: return edi;
			X86MachRegs.EBP: return ebp;
		}
		var regSet = frame.conv.regSet, wordSize = mach.data.addressSize, offset: int;
		if (loc >= regSet.calleeStart) offset = wordSize * (loc - regSet.calleeStart);
		else if (loc >= regSet.callerStart) offset = frame.size() + (wordSize * (loc - regSet.callerStart));
		else if (loc >= regSet.spillStart) offset = wordSize * (loc - regSet.spillStart + frame.spillArgs);
		else return failLocation("invalid spill location", loc, frame.conv.regSet);
		return esp.plus(offset);
	}
	def failLocation(msg: string, loc: int, regSet: MachRegSet) -> X86Reg {
		mach.fail(Strings.format2("%1: %2", msg, regSet.identify(loc)));
		return X86MachRegs.SCRATCH;
	}
	// patch an absolute address, scanning backwards up to "start"
	def recordPatch(start: int, target: Addr) {
		// scan backwards, looking for the absolute constant
		var pos = findAbsConst(start);
		if (pos >= 0) return machEncoder.recordPatch(target, pos);
	}
	def findAbsConst(start: int) -> int {
		var data = encoder.array;
		for (i = encoder.pos; i >= start; i--) {
			if (data(i-4) != X86Addrs.ABS_CONST0) continue;
			if (data(i-3) != X86Addrs.ABS_CONST1) continue;
			if (data(i-2) != X86Addrs.ABS_CONST2) continue;
			if (data(i-1) != X86Addrs.ABS_CONST3) continue;
			return i - 4;
		}
		mach.fail("Could not find absolute constant to patch");
		return -1;
	}
	def jmpl_near(cond: X86Cond, label: Label) {
		var off = 0;
		if (cond == null) jmp(off);
		else j(off, 0x70 + cond.index, 0x80 + cond.index);
		if (label.pos >= 0) {
			off = label.pos - pos();
			encoder.at(pos() - 1).i1(byte.!(off));
			encoder.atEnd();
		} else {
			label.near_uses = List.new(pos(), label.near_uses);
		}
	}
	def bind(label: Label) {
		label.pos = pos();
		for (l = label.near_uses; l != null; l = l.tail) {
			encoder.at(l.head - 1).i1(byte.!(label.pos) - l.head);
		}
		encoder.atEnd();
	}
	// edx:eax / a
	def idivmod_checked(source: Source, zeroCheck: bool, negCheck: bool, a: X86Rm, div: bool) {
		var label: Label;
		if (negCheck) {
			cmp.rm_i(a, -1);
			jnz(4); // if b != -1, branch to division (both cases are 4 bytes of code)
			if (div) neg(eax); // a / -1 == 0 - a
			else xor.rm_r(edx, edx); // a % -1 == 0
			label = Label.new();
			jmpl_near(X86Conds.ALWAYS, label); // jump past division to end
		}
		cdq();
		var off = codeOffset();
		idiv(a);
		if (label != null) bind(label);
		if (zeroCheck) mach.runtime.src.recordSource(off, source);
	}
	// edx:eax / a
	def udivmod_checked(source: Source, zeroCheck: bool, a: X86Rm) {
		xor.rm_r(edx, edx);
		var off = codeOffset();
		div(a);
		if (zeroCheck) mach.runtime.src.recordSource(off, source);
	}
	def neg64(a: X86Reg, b: X86Reg) {
		var no_carry = Label.new();
		neg(a);
		jmpl_near(X86Conds.NC, no_carry);
		add.rm_i(b, 1);
		bind(no_carry);
		neg(b);
	}
	def wdiv(op: WideDivision, source: Source) {
		if (!op.signed && op.small_divisor) {
			// generate the division inline.
			return u64_divmod_small_divisor(op, source, false);
		}
		// generate a call to a stub.
		var name = op.name;
		var addr = mach.layout.stubMap.get(name).0;
		if (addr == null) {
			addr = Address.new(mach.layout.codeRegion, name);
			mach.layout.stubMap.set(name, (addr, gen_i64_divmod_stub(mach, _, _, codeStartOffset, op)));
		}
		call_addr(addr);
		if (op.zeroCheck) mach.runtime.src.recordReturnSource(codeOffset(), source);
	}
	// (%eax, %edx) =  n0,n1 (%esi,%eax) /% d0,d1 (%edi, %edx) [kill all regs]
	def wdiv_full(op: WideDivision, source: Source, stub: bool) {
		var scratch = X86MachRegs.SCRATCH;
		if (op.signed) {
			// XXX: negative checks for numerator/denom can be folded
			xor.rm_r(scratch, scratch);
			// If n < 0, negate
			var n_positive = Label.new();
			test_rm_r(eax, eax);
			jmpl_near(X86Conds.NS, n_positive);
			neg64(esi, eax);
			movd_rm_i(scratch, 1);
			bind(n_positive);

			// if d < 0, negate
			if (!op.small_divisor) {
				var d_positive = Label.new();
				test_rm_r(edx, edx);
				jmpl_near(X86Conds.NS, d_positive);
				neg64(edi, edx);
				if (!op.mod) xor.rm_i(scratch, 1);
				bind(d_positive);
			}
		}

		// perform unsigned division based on reduction to u64_div_u32:
		// if (d1 == 0) {
		//      // u64_div_small_divisor
		//      var u = u64_div_u32(to_u64(u32_mod(n1, d0), n0), d0);
		// 	return to_u64(u32_div(n1, d0)), u);
		// } else {
		//      // u64_div_large_divisor
		// 	var s = num_leading_zeroes(d1);
		// 	var q1 = u64_div_u32(u64_shr(n, 1), high_u32(u64_shl(d, s)));
		// 	var q2 = u32_shr(q1, 31 - s);
		// 	if (u64_sub(n, u64_mul_u32(d, q2)) < 0) return to_u64(0, q2 - 1);
		// 	return to_u64(0, q2);
		// }

		var done = Label.new();
		var small_divisor = Label.new();
		if (!op.large_divisor) {
			cmp.rm_i(edx, 0);
			jmpl_near(X86Conds.Z, small_divisor);
		}

		u64_divmod_big_divisor(op, source);

		if (!op.large_divisor) {
			jmpl_near(X86Conds.ALWAYS, done);
			// fast case of d1 == 0
			bind(small_divisor);
			u64_divmod_small_divisor(op, source, stub);
		}

		bind(done);

		if (op.signed) {
			// negate result if necessary
			var done = Label.new();
			test_rm_i(scratch, 1);
			jmpl_near(X86Conds.Z, done);
			neg64(eax, edx);
			bind(done);
		}
	}
	def u64_divmod_big_divisor(op: WideDivision, source: Source) {
		// slow case of d1 != 0
		var spilled_n0 = esp.plus(0), spilled_n1 = esp.plus(4);
		var spilled_d0 = esp.plus(8), spilled_d1 = esp.plus(12);
		push(edx);
		push(edi);
		push(eax);
		push(esi);
		xchg(eax, edx);
		xchg(eax, esi);
		shrd_i(eax, edx, 1);
		shr_i(edx, 1);
		bsr(ebx, esi);
		lea(ecx, ebx.plus(-31));
		neg(ecx);
		shld_cl(esi, edi);
		div(esi);
		movd_rm_r(ecx, ebx);
		shr_cl(eax);
		// quotient adjustment
		movd_r_rm(ecx, spilled_d1);
		imul_r_rm(ecx, eax);
		xchg(edi, eax);
		mul(edi);
		if (op.mod) {
			add.rm_r(ecx, edx);
			movd_r_rm(ebx, eax);
			movd_r_rm(eax, spilled_n0);
			movd_r_rm(edx, spilled_n1);
			sub.r_rm(eax, ebx);
			sbb.r_rm(edx, ecx);
			var mod_done = Label.new();
			jmpl_near(X86Conds.NC, mod_done);
			add.r_rm(eax, spilled_d0);
			adc.r_rm(edx, spilled_d1);
			bind(mod_done);
		} else {
			add.rm_r(edx, ecx);
			sub.rm_r(spilled_n0, eax);
			sbb.rm_r(spilled_n1, edx);
			movd_rm_r(eax, edi);
			var div_done = Label.new();
			jmpl_near(X86Conds.NC, div_done);
			sub.rm_i(eax, 1);
			bind(div_done);
			xor.rm_r(edx, edx);
		}
		add.rm_i(esp, 16);
	}
	def u64_divmod_small_divisor(op: WideDivision, source: Source, stub: bool) {
		var start = codeOffset(), frame: MachFrame;
		if (op.zeroCheck && stub && mach.runtime.src != null) {
			var frame = MachFrame.new(null);
			frame.frameSize = 0;
			mach.runtime.src.recordStubStart(start, op.name, frame);
		}
		div(edi);
		if (op.zeroCheck && mach.runtime.src != null) mach.runtime.src.recordSource(start, source);
		if (op.mod) {
			movd_r_rm(eax, esi);
			div(edi);
			movd_r_rm(eax, edx);
			xor.r_rm(edx, edx);
		} else {
			xchg(eax, esi);
			div(edi);
			movd_rm_r(edx, esi);
		}
		if (frame != null) mach.runtime.src.recordFrameEnd(codeOffset());
	}
}
// A helper for generating shift operations for a given V3 opcode.
class X86Shifter(opcode: Opcode) {
	// Checked shift {ra} by the amount in CL into {rd}. May clobber scratch register.
	def sh_checked(asm: X86MacroAssembler, rd: X86Rm, ra: X86Rm, scratch: X86Reg) {
		var out_of_range = Label.new(), done = Label.new();
		asm.cmp.rm_i(ecx, 32);
		asm.jmpl_near(X86Conds.NC, out_of_range);

		if (rd != ra) {
			if (rd == ecx) {
				// don't overwrite ECX before shift, use scratch
				asm.movd_r_rm(scratch, ra);
				sh_cl(asm, scratch);
				asm.movd_rm_r(rd, scratch);
			} else {
				// move shiftor into destination first
				if (X86Reg.?(ra)) {
					asm.movd_rm_r(rd, X86Reg.!(ra));
				} else if (X86Reg.?(rd)) {
					asm.movd_r_rm(X86Reg.!(rd), ra);
				} else {
					asm.movd_r_rm(scratch, ra);
					asm.movd_rm_r(rd, scratch);
				}
				sh_cl(asm, rd);
			}
		} else {
			// no moves necessary
			sh_cl(asm, rd);
		}
		asm.jmpl_near(X86Conds.ALWAYS, done);
		asm.bind(out_of_range);
		if (opcode == Opcode.IntSar) {
			// should be the sign of the input.
			if (rd != ra) {
				if (X86Reg.?(ra)) {
					asm.movd_rm_r(rd, X86Reg.!(ra));
				} else if (X86Reg.?(rd)) {
					asm.movd_r_rm(X86Reg.!(rd), ra);
				} else {
					asm.movd_r_rm(scratch, ra);
					asm.movd_rm_r(rd, scratch);
				}
			}
			asm.sar_i(rd, 31);
		} else {
			// should be zero.
			asm.movd_rm_i(rd, 0);
		}
		asm.bind(done);
	}
	// Checked wide shift {rdl,rdh} in place by the amount in CL.
	def wsh_checked(asm: X86MacroAssembler, rdl: X86Reg, rdh: X86Reg, sh: X86Rm) {
		var out_of_range = Label.new(), done = Label.new(), big = Label.new();
		asm.cmp.rm_i(sh, 0);
		asm.jmpl_near(X86Conds.NZ, out_of_range);
		// perform the 32 bit shift.
		wsh_cl(asm, rdl, rdh);
		asm.cmp.rm_i(ecx, 32);
		asm.jmpl_near(X86Conds.A, big);
		asm.jmpl_near(X86Conds.L, done);
		// shift amount was actually >= 32.
		asm.bind(big);
		asm.cmp.rm_i(ecx, 64);
		asm.jmpl_near(X86Conds.GE, out_of_range);
		wsh_adjust(asm, rdl, rdh);
		asm.jmpl_near(X86Conds.ALWAYS, done);
		// Handle out-of-range shift value.
		asm.bind(out_of_range);
		if (opcode == Opcode.IntSar) {
			// should be the sign of the input.
			asm.sar_i(rdh, 31);
			asm.movd_rm_r(rdl, rdh);
		} else {
			// should be zero.
			asm.xor.rm_r(rdh, rdh);
			asm.xor.rm_r(rdl, rdl);
		}
		// done.
		asm.bind(done);
	}
	// Shift by the amount in CL.
	def sh_cl(asm: X86MacroAssembler, dest: X86Rm) {
		match (opcode) {
			IntShr: asm.shr_cl(dest);
			IntSar: asm.sar_cl(dest);
			IntShl: asm.shl_cl(dest);
			_: ;
		}
	}
	// Shift by an immediate.
	def sh_i(asm: X86MacroAssembler, dest: X86Rm, imm: int) {
		if (imm == 0) return;
		if (u32.!(imm) >= u32.!(32)) {
			if (opcode == Opcode.IntSar) return asm.sar_i(dest, 31);
			else return asm.movd_rm_i(dest, 0);
		}
		match (opcode) {
			IntShr: asm.shr_i(dest, imm);
			IntSar: asm.sar_i(dest, imm);
			IntShl: asm.shl_i(dest, imm);
			_: ;
		}
	}
	// Wide shift by the amount in CL.
	def wsh_cl(asm: X86MacroAssembler, rdl: X86Reg, rdh: X86Reg) {
		match (opcode) {
			IntShr: {
				asm.shrd_cl(rdl, rdh);
				asm.shr_cl(rdh);
			}
			IntSar: {
				asm.shrd_cl(rdl, rdh);
				asm.sar_cl(rdh);
			}
			IntShl: {
				asm.shld_cl(rdh, rdl);
				asm.shl_cl(rdl);
			}
			_: ;
		}
	}
	// Adjust an already shifted result if the shift amount in CL was 32 <= x < 64.
	private def wsh_adjust(asm: X86MacroAssembler, rdl: X86Reg, rdh: X86Reg) {
		match (opcode) {
			IntShr: {
				asm.movd_rm_r(rdl, rdh);
				asm.xor.rm_r(rdh, rdh);
			}
			IntSar: {
				asm.movd_rm_r(rdl, rdh);
				asm.shl_i(rdh, 31);
			}
			IntShl: {
				asm.movd_rm_r(rdh, rdl);
				asm.xor.rm_r(rdl, rdl);
			}
			_: ;
		}
	}
}
// Describes the properties of a signed/unsigned 64-bit modulus or division.
class WideDivision(mod: bool,		// modulus
		   signed: bool,	// numerator or denominator is signed
		   large_divisor: bool, // divisor is definitely > 32-bits
		   small_divisor: bool, // divisor is definitely <= 32 bits
		   zeroCheck: bool) {   // divisor may be zero
	var name: string;
	new() {
		var buf = StringBuffer.new();
		buf.putc(if(signed, 'i', 'u'));
		buf.puts("64_");
		buf.puts(if(mod, "mod", "div"));
		if (large_divisor) buf.putc('l');
		if (small_divisor) buf.putc('s');
		if (zeroCheck) buf.putc('z');
		name = buf.toString();
	}
}
