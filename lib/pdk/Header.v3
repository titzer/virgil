// `Header` represents an HTTP Request header.
// Multiple values for a single key are not deduped.
class Header {
	var m: HashMap<string, string>;

	new() {
		m = Strings.newMap<string>();
	}

	// hack to work around no inline function closures:
	var b: StringBuilder;
	var len: int;
	private def keyValueToJson(key: string, value: string, index: int) {
		if (index < len - 1) {
			b.put2("\"%s\":\"%s\",", key, value);
		} else {
			b.put2("\"%s\":\"%s\"", key, value);
		}
	}

	def toJson() -> string {
		len = m.length();
		b = StringBuilder.new();
		b.puts("{");
		m.applyWithIndex(keyValueToJson);
		b.puts("}");
		return b.toString();
	}

	// `add` adds a value to a named (by `key`) header field.
	// If the header key already exists, the value is appended after a comma.
	def add(key: string, value: string) {
		match (m.has(key)) {
			true => {
				var b = StringBuilder.new();
				b.puts(m[key]);
				b.puts(",");
				b.puts(value);
				m[key] = b.toString();
			}
			false => m[key] = value;
		}
	}

	// `set` overwrites a value to a named (by `key`) header field.
	def set(key: string, value: string) {
		m[key] = value;
	}
}