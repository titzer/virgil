// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Global unittest registry.
component UnitTests {
	private var buf = StringBuilder.new();
	private def expected = Strings.newMap<bool>(); // contains expected failures
	private var list: List<UnitTest>;
	private var renderers: Renderer; // list of custom renderers
	private var comparators: Comparator; // list of custom comparators

	var fatal: bool = false;
	var trace: bool = false;

	// Registration methods.
	def register(name: string, func: Tester -> ()) {
		list = List.new(UnitTest(name, func), list);
	}
	def registerT<T>(prefix: string, name: string, n: Tester -> T, f: T -> void) {
		if (prefix != null) name = buf.reset().puts(prefix).puts(name).extract();
		register(name, runNew<T>(_, n, f));
	}
	private def runNew<T>(t: Tester, n: Tester -> T, f: T -> ()) {
		return f(n(t));
	}

	// Run method, e.g. from command-line. Parses {args}.
	def run(args: Array<string>) -> int {
		var matchers = Vector<GlobMatcher>.new();
		// Parse options first
		for (i < args.length) {
			var arg = args[i];
			if (arg == null) continue;
			if (arg.length > 0 && arg[0] == '-') {
				if (Strings.equal(arg, "-fatal")) {
					fatal = true;
				} else if (Strings.startsWith(arg, "-expected=")) {
					loadExpectedFile(expected, Arrays.range(arg, "-expected=".length, arg.length));
				} else {
					System.puts("Unknown option: ");
					System.puts(arg);
					System.ln();
					return 1;
				}
			} else {
				matchers.put(GlobMatcher.new(arg));
			}

		}
		// Filter the registered tests with matchers
		var count = 0, r: List<UnitTest>;
		for (l = UnitTests.list; l != null; l = l.tail) { // count and reverse list
			var t = l.head;
			if (matchers.length > 0) {
				var skip = true;
				for (i < matchers.length) {
					if (skip) skip = !matchers[i].matches(t.name);
				}
				if (skip) continue;
			}
			r = List.new(l.head, r);
			count++;
		}
		// Run tests
		System.puts("##>");
		System.puti(count);
		System.puts(" unit tests\n");
		var fail = false;
		for (l = r; l != null; l = l.tail) {
			var u = l.head;
			var t = Tester.new(u.name);
			System.puts("##+");
			System.puts(u.name);
			System.ln();
			var before = if(trace, System.ticksUs());
			u.func(t);
			if (trace) {
				var diff = System.ticksUs() - before;
				System.puts("##@");
				System.puts(u.name);
				System.puts(" : ");
				System.puti(diff);
				System.puts(" us\n");
			}
			if (t.ok) {
				System.puts("##-ok\n");
			} else if (expected[u.name]) {
				System.puts("##-ok (ignored failure: ");
				System.puts(t.msg);
				System.puts(")\n");
			} else {
				fail = true;
				System.puts("##-fail (");
				System.puts(t.msg);
				System.puts(")\n");
			}
		}
		return if(fail, 1, 0);
	}
	// Register a custom rendering routine for the type {T}.
	def registerRenderer<T>(func: (T, StringBuilder) -> StringBuilder) {
		renderers = RendererOf<T>.new(func, renderers);
	}
	def render<T>(t: T, buf: StringBuilder) -> StringBuilder {
		for (l = renderers; l != null; l = l.next) match (l) {
			x: RendererOf<T> => return x.func(t, buf);
		}
		return buf.puts("?");
	}
	// Register a custom comparator for the type {T}.
	def registerComparator<T>(func: (T, T) -> bool) {
		comparators = ComparatorOf<T>.new(func, comparators);
	}
	def equal<T>(a: T, b: T) -> bool {
		if (a == b) return true;
		for (l = comparators; l != null; l = l.next) match (l) {
			x: ComparatorOf<T> => return x.func(a, b);
		}
		return false;
	}
}

// An individual unit test.
private type UnitTest(name: string, func: Tester -> ()) #unboxed;

// Custom renderers to make using assertions extensible.
private class Renderer(next: Renderer) {
}
private class RendererOf<T> extends Renderer {
	def func: (T, StringBuilder) -> StringBuilder;
	new(func, next: Renderer) super(next) { }
}
// Custom comparators to make using assertions extensible.
private class Comparator(next: Comparator) {
}
private class ComparatorOf<T> extends Comparator {
	def func: (T, T) -> bool;
	new(func, next: Comparator) super(next) { }
}

// Load a file that contains expected failures, one on each line
def loadExpectedFile(expected: Map<string, bool>, fileName: string) {
	var data = System.fileLoad(fileName);
	if (data == null) return;
	var line = 0, pos = 0;
	while (pos < data.length) {
		if (data[pos] == '\n') {
			var test = Arrays.range(data, line, pos);
			if (UnitTests.trace) {
				System.puts("ignore: ");
				System.puts(test);
				System.ln();
			}
			if (pos > line) expected[test] = true;
			line = pos + 1;
		}
		pos++;
	}
	if (pos > line) {
		var test = Arrays.range(data, line, pos);
		if (UnitTests.trace) {
			System.puts("ignore: ");
			System.puts(test);
			System.ln();
		}
		expected[test] = true;
		line = pos + 1;
	}
}
