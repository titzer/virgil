// Copyright 2025 Virgil authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// This (compact) prefix trie efficiently stores a mapping from keys of type Range<K> to values <V>,
// exploiting cases where the Range<K> exhibit common prefixes and thus do not require each to be
// stored separately. This also known as a radix tree.
class PrefixTrie<K, V> {
	def var max: int;	// maximum sequence length
	private var empty: PrefixTrieLeaf<K, V>;
	private var root: PrefixTrieBranch<K, V>;

	// Get the value for {seq}.
	def [seq: Range<K>] -> V {
		return make(seq).value;
	}
	// Set the value for {seq}.
	def [seq: Range<K>] = v: V {
		make(seq).value = v;
	}
	// Update the value for {seq} by applying {f} to the old value.
	def update(seq: Range<K>, f: V -> V) -> V {
		var entry = make(seq);
		return entry.value = f(entry.value);
	}
	// Iterate all seq/value pairs and apply {f}.
	def apply<R>(f: (Range<K>, V) -> R) {
		if (empty != null) f(null, empty.value);
		if (root == null) return;
		var buf = Array<K>.new(max);
		apply0(f, buf, 0, root);
	}

	// Recursively enumerate sequences and apply {f}.
	private def apply0<R>(f: (Range<K>, V) -> R, buf: Array<K>, pos: int, branch: PrefixTrieBranch<K, V>) {
		var p = branch.prefix;
		for (i < p.length) buf[pos + i] = p[i]; // copy the prefix into place
		var c = branch.choices, n = pos + p.length;
		for (i < c.length) { // XXX: could apply sorting here.
			var t = c[i];
			buf[n] = t.0;
			match (t.1) {
				x: PrefixTrieBranch<K, V> => {
					apply0(f, buf, n + 1, x);
				}
				x: PrefixTrieLeaf<K, V> => {
					f(buf[0 ... n + 1], x.value);
				}
			}
		}
	}
	// Make a leaf for the given sequence by finding or inserting it into the prefix trie.
	private def make(seq: Range<K>) -> PrefixTrieLeaf<K, V> {
		if (seq.length == 0) {
			if (empty == null) empty = PrefixTrieLeaf<K, V>.new();
			return empty;
		}
		if (seq.length > max) max = seq.length;
		if (root == null) {
			// No seq/values yet.
			var t = newBranchAndLeaf(seq);
			root = t.0;
			return t.1;
		}
		// Search all but the last element of the seq.
		var i = 0, branch = root, last = seq.length - 1;
		while (i < last) {
			var k = seq[i], prefix = branch.prefix;
			if (i == prefix.length) {
				// Matched entire branch prefix; search for a choice.
				var suffix = seq[i + 1 ...];
				var nbranch = branch.findChoice<PrefixTrieBranch<K, V>>(k);
				if (nbranch == null) {
					// No branch for this choice, create one.
					var t = newBranchAndLeaf(suffix);
					branch.choices.put((k, t.0));
					return t.1;
				}
				// A branch exists for this choice, follow it.
				branch = nbranch;
				seq = suffix;
				last = seq.length - 1;
				i = 0;
				continue;
			}
			if (k != prefix[i]) {
				// Mismatched in the middle of the branch prefix; split.
				branch.split(i);
				var suffix = seq[i + 1 ...];
				var t = newBranchAndLeaf(suffix);
				branch.choices.put((k, t.0));
				return t.1;
			}
			i++;
		}
		// Everything but the last element of the seq has been matched by the loop above.
		var k = seq[i];
		if (i == branch.prefix.length) {
			// Matched all of the branch's prefix; add a leaf.
			var leaf = branch.findChoice<PrefixTrieLeaf<K, V>>(k);
			if (leaf == null) branch.choices.put((k, leaf = PrefixTrieLeaf<K, V>.new()));
			return leaf;
		} else {
			// Matched only a portion of the branch prefix; split it and add a leaf.
			branch.split(i);
			var leaf = PrefixTrieLeaf<K, V>.new();
			branch.choices.put((k, leaf));
			return leaf;
		}
	}
	private def newBranchAndLeaf(seq: Range<K>) -> (PrefixTrieBranch<K, V>, PrefixTrieLeaf<K, V>) {
		var last = seq.length - 1, k = seq[last];
		var prefix = Ranges.dup(seq[0 ... last]);
		var branch = PrefixTrieBranch<K, V>.new(prefix);
		var leaf = PrefixTrieLeaf<K, V>.new();
		branch.choices = Vectors.of1((k, leaf));
		return (branch, leaf);
	}
}

private class PrefixTrieNode<K, V> {
}

private class PrefixTrieBranch<K, V> extends PrefixTrieNode<K, V> {
	var prefix: Array<K>;
	var choices: Vector<(K, PrefixTrieNode<K, V>)>;

	new(prefix) { }

	def findChoice<Q>(k: K) -> Q {
		var q: Q;
		if (choices == null) return q;
		for (i < choices.length) {
			var c = choices[i];
			if (c.0 == k && Q.?(c.1)) return Q.!(c.1);
		}
		return q;
	}
	def split(pos: int) {
		var k = prefix[pos];
		var suffix = Ranges.dup(prefix[pos + 1 ...]);
		var nbranch = PrefixTrieBranch<K, V>.new(suffix);
		nbranch.choices = choices;
		this.prefix = Ranges.dup(prefix[0 ... pos]);
		this.choices = Vectors.of1((k, nbranch));
	}
}

private class PrefixTrieLeaf<K, V> extends PrefixTrieNode<K, V> {
	var value: V;
}
