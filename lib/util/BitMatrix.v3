// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// An efficient data structure for a matrix of boolean values.
class BitMatrix(var numrows: int, var numcols: int) {
	private var width: int = (numcols + 31) >>> 5;      // width (in integers) of each row
	private var bits = Array<int>.new(numrows * width); // array that stores the data
	// get the value of the bit at {row, col}
	def [row: int, col: int] -> bool {
		var i = row * width + (col >>> 5), mask = 1 << u5.view(col);
		return (bits[i] & mask) != 0;
	}
	// set the bit at {row, col}
	def [row: int, col: int] = val: bool {
		var i = row * width + (col >>> 5), mask = 1 << u5.view(col);
		var prev = bits[i];
		if (val) {
			// set the bit if not already set.
			if ((prev & mask) == 0) bits[i] = prev | mask;
		} else {
			// clear the bit if it is set.
			if ((prev & mask) != 0) bits[i] = prev & ~mask;
		}
	}
	// set the bit at the specified position, returning its previous value
	def set(row: int, col: int) -> bool {
		var i = row * width + (col >> 5), mask = 1 << u5.view(col);
		var val = bits[i];
		if ((val & mask) == 0) {
			bits[i] = val | mask;
			return false;
		}
		return true;
	}
	// clear the bit at the specified position, returning its previous value
	def clear(row: int, col: int) -> bool {
		var i = row * width + (col >> 5), mask = 1 << u5.view(col);
		var val = bits[i];
		if ((val & mask) != 0) {
			bits[i] = val & (-1 ^ mask);
			return true;
		}
		return false;
	}
	// clear the row at the specified position
	def clearRow(row: int) {
		var i = row * width;
		for (j < width) bits[i + j] = 0;
	}
	// set the row at the specified position
	def setRow(row: int) {
		var i = row * width;
		for (j < width) bits[i + j] = 0xffffffff;
	}
	// flip the row from 1 to 0 and vice versa
	def flipRow(row: int) {
		var i = row * width;
		for (j < width) bits[i + j] = ~bits[i + j];
	}
	// OR rows {a} and {b} together, storing the result in row {a}
	def or(a: int, b: int) {
		var array = bits;
		var da = a * width, db = b * width;
		for (i < width) array[da + i] = array[da + i] | array[db + i];
	}
	// AND rows {a} and {b} together, storing the result in row {a}
	def and(a: int, b: int) {
		var array = bits;
		var da = a * width, db = b * width;
		for (i < width) array[da + i] = array[da + i] & array[db + i];
	}
	// copy row {b} into row {a}
	def copy(a: int, b: int) {
		var array = bits;
		var da = a * width, db = b * width;
		for (i < width) array[da + i] = array[db + i];
	}
	// extract a range of rows from {minRow} inclusive to {maxRow} exclusive into a new BitMatrix
	def range(minRow: int, maxRow: int) -> BitMatrix {
		var numrows = maxRow - minRow;
		var result = BitMatrix.new(numrows, numcols);
		var start = minRow * width, end = maxRow * width;
		var sbits = this.bits, rbits = result.bits;
		for (i = start; i < end; i++) rbits[i - start] = sbits[i];
		return result;
	}
	// apply {func} to every bit that is set in row {row}
	def apply<T>(row: int, func: (int, T) -> void, arg: T) {
		var start = width * row;
		for (i < width) {
			var col = i * 32;
			for (b = bits[i + start]; b != 0; b = b >>> 1) {
				if ((b & 1) != 0) func(col, arg);
				col++;
			}
		}
	}
	// Resize to {nrows} rows, growing if necessary.
	def grow(nrows: int) -> this {
		var words = nrows * width;
		if (bits.length < words) bits = Arrays.grow(bits, 10 + bits.length + words);
		numrows = nrows;
	}
	// Add a new row to this matrix, growing if necessary, and return it.
	def addRow() -> BitMatrixRow {
		var index = numrows;
		grow(index + 1);
		return BitMatrixRow(this, index);
	}
	// Resize to {ncols} columns, growing if necessary.
	def widen(ncols: int) -> this {
		if (ncols <= numcols) return;
		var nwidth = (ncols + 31) >>> 5;
		if (nwidth <= width) {
			numcols = ncols;
			return;
		}
		var nbits = Array<int>.new(numrows * nwidth);
		for (i < numrows) {
			for (j < width) {
				nbits[i * nwidth + j] = bits[i * width + j];
			}
		}
		numcols = ncols;
		width = nwidth;
		bits = nbits;
	}
	// Reset this bit matrix to all 0s with new dimensions, reusing internal storage
	// if possible.
	def reset(nrows: int, ncols: int, trim: bool) -> this {
		var nwidth = (ncols + 31) >>> 5;
		var nwords = nrows * nwidth;
		if (!trim && nwords <= bits.length && nwords <= bits.length) {
			// Reuse internal storage if already big enough.
			var words = numrows * width;
			for (i < words) bits[i] = 0;
		} else {
			// Allocate new storage.
			bits = Array.new(nwords);
		}
		this.numrows = nrows;
		this.numcols = ncols;
		this.width = nwidth;
	}
	// Trims the storage of this matrix to avoid wasting memory.
	def trim() -> this {
		var words = numrows * width;
		if (words < bits.length) bits = Arrays.range(bits, 0, words);
	}
	// Count the number of bits set in {row}.
	def count(row: int) -> int {
		var result = 0, max = (row + 1) * width;
		for (i = row * width; i < max; i++) {
			var val = bits[i]; // XXX: use popcount operation when available
			while (val != 0) {
				if ((val & 1) != 0) result++;
				val >>>= 1;
			}
		}
		return result;
	}
	// Return {true} if all bits in {row} are zero.
	def isZero(row: int) -> bool {
		var max = (row + 1) * width;
		for (i = row * width; i < max; i++) {
			if (bits[i] != 0) return false;
		}
		return true;
	}
	def row(index: int) -> BitMatrixRow {
		var a: BitMatrixRow;
		if (index >= numrows) return a;
		return BitMatrixRow(this, index);
	}
	def rowInts(index: int) -> Range<int> {
		if (index >= numrows) return null;
		return bits[(index * width) ..+ width];
	}
	def last() -> BitMatrixRow {
		var a: BitMatrixRow;
		if (numrows <= 0) return a;
		return BitMatrixRow(this, numrows - 1);
	}
	def render(buf: StringBuilder) -> StringBuilder {
		for (i < numrows) renderRow(i, buf);
		return buf;
	}
	def renderRow(row: int, buf: StringBuilder) -> StringBuilder {
		buf.putd(row);
		buf.puts(": ");
		var start = row * width, end = start + width;
		for (i = start; i < end; i++) {
			var w = bits[i];
			for (j < 32) {
				buf.putc(byte.view('0' + (w & 1)));
				w = w >> 1;
			}
			buf.sp();
		}
		buf.ln();
		return buf;
	}
}

// A wrapper around a row that simplifies indexing math of rows.
type BitMatrixRow(matrix: BitMatrix, index: int) #unboxed {
	// OR this row with {that} row from the same matrix.
	def or(that: BitMatrixRow) {
		if (matrix != that.matrix) System.error("", "");
		matrix.or(index, that.index);
	}
	// AND this row with {that} row from the same matrix.
	def and(that: BitMatrixRow) {
		if (matrix != that.matrix) System.error("", "");
		matrix.and(index, that.index);
	}
	// copy {that} row into this row.
	def copy(that: BitMatrixRow) {
		if (matrix != that.matrix) System.error("", "");
		matrix.copy(index, that.index);
	}
	// OR this row with row {a} from the same matrix.
	def ori(a: int) {
		matrix.or(index, a);
	}
	// AND this row with row {a} from the same matrix.
	def andi(a: int) {
		matrix.and(index, a);
	}
	// copy row {a} into this row
	def copyi(a: int) {
		matrix.copy(index, a);
	}
	// Set this row to all 1s.
	def set() {
		matrix.setRow(index);
	}
	// Flip this row from 1 to 0 and vice versa.
	def flip() {
		matrix.flipRow(index);
	}
	// Set this row to all 0s.
	def clear() {
		matrix.clearRow(index);
	}
	// Count the number of 1s in this row.
	def count() -> int {
		return matrix.count(index);
	}
	// Check if this row is all 0s.
	def isZero() -> bool {
		return matrix.isZero(index);
	}
	// Get the next row {delta} rows down.
	def plus(delta: int) -> BitMatrixRow {
		return matrix.row(index + delta);
	}
	// Apply the function {func} to every column in this row that is {1}, passing {arg}.
	def apply<T>(func: (int, T) -> void, arg: T) {
		matrix.apply(index, func, arg);
	}
	// Get column {col} in this row.
	def [col: int] -> bool {
		return matrix[index, col];
	}
	// Set column {col} in this row to {val}.
	def [col: int] = val: bool {
		matrix[index, col] = val;
	}
}
