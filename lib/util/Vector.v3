// Copyright 2010 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A utility class which represents an efficient, growable, appendable array
class Vector<T> {
	var array: Array<T>;
	var length: int;

	// Get the element at the specified index.
	def [index: int] -> T {
		return array[index];
	}
	// Update the element at the specified index.
	def [index: int] = element: T {
		array[index] = element;
	}
	// Set the element at the specified index, growing if necessary.
	def set(index: int, element: T) -> this {
		grow(index + 1);
		array[index] = element;
		if (index >= length) length = index + 1;
	}
	// Add an element to the end of this vector.
	def put(element: T) -> this {
		var a = array, s = length;
		if (a == null) array = a = Array<T>.new(s + 10);
		else if (s >= a.length) array = a = Arrays.grow(a, a.length + 10 + s);
		a[s] = element;
		length = s + 1;
	}
	// Add all the elements in the given array to the end of this vector.
	def puta(a: Array<T>) -> this {
		if (a != null) putk(a, 0, a.length);
	}
	// Add the elements from the given range in the array to the end of this vector.
	// #deprecated - prefer {Vector.putr} instead
	def putk(a: Array<T>, start: int, end: int) -> this {
		putr(a[start ... end]);
	}
	// Add the elements from the given range to the end of this vector.
	def putr(r: Range<T>) -> this {
		var curlen = length, rlen = r.length, newlen = curlen + rlen;
		if (array == null) array = Array<T>.new(newlen);
		else if (newlen > array.length) grow(curlen + newlen);

		var v = array[curlen ..+ rlen];
		for (i < rlen) v[i] = r[i];
		length = newlen;
	}
	// Append {v} {n} times to this vector.
	def putn(v: T, n: int) -> this {
		var max = length + n;
		grow(max);
		for (i = length; i < max; i++) array[i] = v;
		length += n;
	}
	// add all of the elements from the vector to the end of this vector
	def putv(v: Vector<T>) -> this {
		if (v.array != null) putk(v.array, 0, v.length);
	}
	// apply the specified function to each element in the vector
	def apply(f: T -> void) {
		var a = array;
		if (a != null) {
			var max = length, len = a.length;
			for (i < len) {
				if (i >= max) return;
				f(a[i]);
			}
		}
	}
	// grow the internal storage of this vector to the new length
	def grow(nlength: int) -> this {
		if (array == null) array = Array<T>.new(nlength);
		else if (nlength > array.length) array = Arrays.grow(array, nlength);
	}
	// copy this vector into a new, appropriately-sized array
	def copy() -> Array<T> {
		if (array == null) return [];
		return Arrays.copy(array, Array<T>.new(length));
	}
	// extracts all elements from this vector, leaving it empty
	// (more efficient than copy() if the array is sized exactly)
	def extract() -> Array<T> {
		if (array == null) return [];
		var result = array;
		if (length != result.length) result = Arrays.copy(result, Array<T>.new(length));
		array = null;
		length = 0;
		return result;
	}
	// Send the elements of this vector to the given function,
	// avoiding an intermediate copy. Note that it is implementation
	// dependent if {f} is called multiple times, e.g. if the internal
	// storage is fragmented.
	def send<R>(f: Range<T> -> R) -> R {
		return if(array != null, f(array[0 ... length]));
	}
	// Clear all elements to default value and set length to 0.
	def clear() -> this {
		length = 0;
		if (array == null) return;
		var x = array, d: T;
		for (i < x.length) x[i] = d;
	}
	// Resize this vector to be {nlength} elements.
	def resize(nlength: int) -> this {
		if (nlength > length) grow(nlength);
		length = nlength;
	}
}

// Utility methods for vectors
component Vectors {
	// efficient construction of a single-element vector
	def of1<T>(e: T) -> Vector<T> {
		var vec = Vector<T>.new();
		vec.array = [e];
		vec.length = 1;
		return vec;
	}
	// efficient construction of a N-element vector that shares the same storage
	def ofN<T>(a: Array<T>) -> Vector<T> {
		var vec = Vector<T>.new();
		vec.array = a;
		vec.length = a.length;
		return vec;
	}
	// Use a vector as a queue and drain all elements, applying {f} to each element,
	// which may add more elements to the vector
	def drain<T>(vec: Vector<T>, pos: int, f: T -> void) -> int {
		while (pos < vec.length) f(vec.array[pos++]);
		return pos;
	}
	def overwrite<T>(vec: Vector<T>, array: Array<T>) -> Vector<T> {
		vec.length = array.length;
		vec.array = array;
		return vec;
	}
}
