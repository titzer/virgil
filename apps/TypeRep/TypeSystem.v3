// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Relations between types.
component TypeSystem {
	// Returns {true} if {src} is a strict subtype of {dst}, meaning that every value
	// of type {src} can subsume without representation change to type {dst}.
	def isSubtype(src: Type, dst: Type) -> bool {
		// aliases might be subtypes of any type
		if (AliasType.?(src)) return isSubtype(AliasType.!(src).getAlias(), dst);

		match (dst) {
			y: ClassType => {
				// check subtyping of classes by searching up the inheritance tree.
				match (src) {
					x: NullType => return true;
					x: ClassType => {
						for (c = x; c != null; c = c.getSuperClassType()) {
							if (c == y) return true;
						}
						return false;
					}
					_ => return false;
				}
			}
			y: VariantType => {
				// check a variant case against its supertype.
				match (src) {
					x: VariantCaseType => return x.superType == y;
					_ => return false;
				}
			}
			y: ArrayType => {
				// the only subtype of an array type is NULL.
				return NullType.?(src);
			}
			y: TupleType => {
				// tuples are co-variantly typed.
				match (src) {
					x: TupleType => return isSubtypeArray(x.elemTypes, y.elemTypes);
					_ => return false;
				}
			}
			y: ClosureType => {
				// closures are co-variant in return and contra-variant in params
				match (src) {
					x: NullType => return true;
					x: ClosureType => return isSigSubtype(x.sig, y.sig);
					_ => return false;
				}
			}
			y: FuncType => {
				// functions are co-variant in return and contra-variant in params
				match (src) {
					x: NullType => return true;
					x: FuncType => return isSigSubtype(x.sig, y.sig);
					_ => return false;
				}
			}
		}
		return false;
	}
	private def isSigSubtype(s1: Signature, s2: Signature) -> bool {
		return isSubtypeArray(s1.results, s2.results) && isSubtypeArray(s2.params, s1.params);
	}
	private def isSubtypeArray = Arrays.allTrue(_, _, isSubtype);

	// Returns {true} if {src} is promotable to {dst}, meaning that every value
	// of type {src} can either subsume without representation change or be promoted
	// to type {dst} without any loss of information.
	def isPromotable(src: Type, dst: Type) -> bool {
		match (src) {
			x: IntType => match (dst) {
				y: IntType => {
					if (x.width > y.width) return false;
					if (x.width == y.width) return x.signed == y.signed;
					return y.signed || x.signed == y.signed;
				}
				y: FloatType => {
					if (x.signed) return x.width <= y.fraction_width;
					else return x.width <= 1 + y.fraction_width;
				}
				_ => return false;
			}
			x: FloatType => match (dst) {
				y: FloatType => return x.total_width <= y.total_width;
				_ => return false;
			}
			x: TupleType => match (dst) {
				y: TupleType => return Arrays.allTrue(x.elemTypes, y.elemTypes, isPromotable);
				_ => return false;
			}
			x: EnumType => match (dst) {
				y: EnumSetType => return x.decl == y.decl;
				_ => return false;
			}
			_ => return false;
		}
	}

	// Returns {true} if every value of type {src} can be assigned to {dst}, either
	// through subsumption or promotion.
	def isAssignable(src: Type, dst: Type) -> bool {
		return src == dst || isSubtype(src, dst) || isPromotable(src, dst);
	}

	// Compute the common superclass of {x} and {y}, if it exists, walking up the
	// inheritance hierarchy in lock-step fashion until the same declaration is met.
	def commonSuperClass(x: ClassType, y: ClassType) -> (ClassType, ClassType) {
		while (x.depth > y.depth) x = x.getSuperClassType();
		while (y.depth > x.depth) y = y.getSuperClassType();
		while (x != null) {
			if (x.decl == y.decl) return (x, y);
			x = x.getSuperClassType();
			y = y.getSuperClassType();
		}
		return (null, null);
	}

	// Returns {true} if there is some assignment of types to type parameters such
	// that {x} and {y} are exactly equivalent types.
	def maybeEqual(x: Type, y: Type) -> bool {
		return false; // TODO
	}

	// Return {true} if {t} is a reference type.
	def isRefType(t: Type) -> bool {
		match (t) {
			x: ClassType => return true;
			x: ArrayType => return true;
			x: ClosureType => return true;
			_ => return false;
		}
	}

	// Unify two types {a} and {b} (adjusting the solution to any type variables that may
	// appear in {a}) and return a type that both {a} and {b} are assignable to, if such
	// a type exists. Type variables in {b} are ignored.
	def widen = unify(_, _, UnifyMode.PROMOTE);

	// Unify two types {a} and {b} (adjusting the solution to any type variables that may
	// appear in {a}) and return a type that both {a} and {b} are subtypes of, if such
	// a type exists. Type variables in {b} are ignored.
	def upper = unify(_, _, UnifyMode.UP);
	// Guts of recursive unification.
	private def unify(a: Type, b: Type, mode: UnifyMode) -> Type {
		if (a == b) return a;

		match (a) {
			x: IntType => if (mode == UnifyMode.PROMOTE) {
				if (isPromotable(x, b)) return b;
				if (isPromotable(b, x)) return x;
			}
			x: FloatType => if (mode == UnifyMode.PROMOTE) {
				if (isPromotable(x, b)) return b;
				if (isPromotable(b, x)) return x;
			}
			x: ArrayType => match (b) {
				y: ArrayType => {
					var e = unify(x.elemType, y.elemType, UnifyMode.EQUAL);
					return if(e != null, ArrayTypeCache.get(e));
				}
				y: NullType => return x;
			}
			x: ClassType => match (b) {
				y: ClassType => match (mode) {
					UP, PROMOTE => {
						return unifySuperClasses(x, y);
					}
					EQUAL => {
						var u = unifySuperClasses(x, y);
						if (u == null) return null;
						return if(u.decl == x.decl && u.decl == y.decl, u);
					}
					DOWN => {
						var u = unifySuperClasses(x, y);
						if (u == null) return null;
						return if(x.depth > y.depth, x, y);
					}
				}
				y: NullType => return if(mode == UnifyMode.DOWN, null, x);
				_ => return null;
			}
			x: VariantType => match (b) {
				y: VariantType => {
					return unifyVariants(x, y);
				}
				y: VariantCaseType => {
					return unifyVariantCases(y.decl, x, y.superType, mode);
				}
			}
			x: VariantCaseType => match (b) {
				y: VariantType => {
					return unifyVariantCases(x.decl, x.superType, y, mode);
				}
				y: VariantCaseType => {
					var u = unifyVariants(x.superType, y.superType);
					if (u == null) return null;
					if (x.decl == y.decl) return u.getCase(x.decl);
					match (mode) {
						UP, PROMOTE => return u;
						_ => return null;
					}
				}
			}
			x: ClosureType => match (b) {
				y: ClosureType => {
					var pt = unifyArray(x.sig.params, y.sig.params, reverseUnifyMode(mode));
					var rt = unifyArray(x.sig.results, y.sig.results, mode);
					return if(pt != null && rt != null, ClosureTypeCache.get(Signature.new(pt, rt)));
				}
			}
			x: FuncType => match (b) {
				y: FuncType => {
					var pt = unifyArray(x.sig.params, y.sig.params, reverseUnifyMode(mode));
					var rt = unifyArray(x.sig.results, y.sig.results, mode);
					return if(pt != null && rt != null, FuncTypeCache.get(Signature.new(pt, rt)));
				}
			}
			x: TupleType => match(b) {
				y: TupleType => {
					var elemTypes = unifyArray(x.elemTypes, y.elemTypes, mode);
					if (elemTypes == null) return null;
					return TupleTypeCache.get(elemTypes);
				}
				_ => return null;
			}
			x: TypeVarType => {
				var lub = if(x.solution == null, b, unify(x.solution, b, mode));
				if (lub != null) {
					x.solution = lub;
					return x;
				}
				return null;
			}
			x: NullType => {
				if(isRefType(b)) return if(mode == UnifyMode.DOWN, null, b);
			}
			// TODO: type aliases
		}
		return null;
	}
	private def unifyArray(at: Array<Type>, bt: Array<Type>, mode: UnifyMode) -> Array<Type> {
		if (at.length != bt.length) return null;
		var rt = Array<Type>.new(at.length);
		for (i < rt.length) {
			var u = unify(at[i], bt[i], mode);
			if (u == null) return null;
			rt[i] = u;
		}
		return rt;
	}
	private def unifySuperClasses(x: ClassType, y: ClassType) -> ClassType {
		var t = commonSuperClass(x, y), xx = t.0, yy = t.1;
		if (xx == null) return null;
		var typeArgs = unifyArray(xx.typeArgs, yy.typeArgs, UnifyMode.EQUAL);
		if (typeArgs == null) return null;
		xx = xx.decl.getCachedType(typeArgs);
		return if(xx == yy, xx);
	}
	private def unifyVariants(x: VariantType, y: VariantType) -> VariantType {
		if (x.decl != y.decl) return null;
		var typeArgs = unifyArray(x.typeArgs, y.typeArgs, UnifyMode.EQUAL);
		if (typeArgs == null) return null;
		return x.decl.getCachedType(typeArgs);
	}
	private def unifyVariantCases(c: VariantCaseDecl, x: VariantType, y: VariantType, mode: UnifyMode) -> Type {
		match (mode) {
			UP, PROMOTE => {
				return unifyVariants(x, y);
			}
			EQUAL => {
				unifyVariants(x, y);
				return null;
			}
			DOWN => {
				var u = unifyVariants(x, y);
				if (u == null) return null;
				return u.getCase(c);
			}
		}
	}
	private def reverseUnifyMode(mode: UnifyMode) -> UnifyMode {
		match (mode) {
			UP => return UnifyMode.DOWN;
			DOWN => return UnifyMode.UP;
			_ => return mode;
		}
	}

	// Compute the query operation, if it exists, for the expression "dst.?(src)".
	def newTypeQuery(src: Type, dst: Type, legacy_intcast: bool) -> TypeQuery {
		if (NullType.?(src)) return TypeQuery.False;
		if (src == dst) return TypeQuery.True;
		if (TypeParamType.?(src)) return TypeQuery.Polymorphic;
		if (TypeVarType.?(src)) return TypeQuery.Polymorphic;

		var q: TypeQuery = TypeQuery.False;
		match (dst) {
			y: IntType => match (src) {
				x: IntType => q = if(legacy_intcast, TypeQuery.False, TypeQuery.IntQueryI);
				x: FloatType => q = TypeQuery.FloatQueryI;
			}
			y: FloatType => match (src) {
				x: FloatType => q = TypeQuery.FloatQueryD;
			}
			y: ClassType => match (src) {
				x: ClassType => {
					var t = commonSuperClass(x, y), xx = t.0, yy = t.1;
					if (xx == null) return TypeQuery.False;
					var query = if(x.depth < y.depth, TypeQuery.ClassQuery, TypeQuery.TrueExceptForNull);
					return maybeQuery(xx, yy, query, TypeQuery.False);
				}
				_ => return TypeQuery.False;
			}
			y: TupleType => match (src) {
				x: TupleType => {
					var xt = x.elemTypes, yt = y.elemTypes;
					if (xt.length != yt.length) return TypeQuery.False;
					var qa = Array<TypeQuery>.new(xt.length);
					for (i < qa.length) {
						var n = newTypeQuery(xt[i], yt[i], legacy_intcast);
						if (n == TypeQuery.False) return TypeQuery.False;
						qa[i] = n;
					}
					q = TypeQuery.TupleQuery(qa);
				}
			}
			y: TypeParamType => q = TypeQuery.Polymorphic;
			y: TypeVarType => q = TypeQuery.Polymorphic;
			y: VariantType => match (src) {
				x: VariantType => return maybeQuery(x, y, TypeQuery.True, TypeQuery.False);
				x: VariantCaseType => {
					if (x.superType.decl == y.decl) {
						return maybeQuery(x.superType, y, TypeQuery.True, TypeQuery.False);
					}
				}
			}
			y: VariantCaseType => match (src) {
				x: VariantType => {
					if (y.superType.decl == y.decl) {
						return maybeQuery(x, y.superType, TypeQuery.VariantQuery, TypeQuery.False);
					}
				}
				x: VariantCaseType => {
					if (x.decl == y.decl) {
						return maybeQuery(x.superType, y.superType, TypeQuery.True, TypeQuery.False);
					}
				}
			}
		}
		return if(isAssignable(src, dst), TypeQuery.True, q);
	}
	private def maybeQuery(x: Type, y: Type, t: TypeQuery, f: TypeQuery) -> TypeQuery {
		if (x == y) return t;
		if (maybeEqual(x, y)) return TypeQuery.Polymorphic;
		return f;
	}

	// Compute the cast operation, if it exists, for the expression "dst.!(src)".
	def newTypeCast(src: Type, dst: Type, legacy_intcast: bool) -> TypeCast {
		if (src == dst) return TypeCast.Identity;
		if (TypeParamType.?(src)) return TypeCast.Polymorphic;
		if (TypeVarType.?(src)) return TypeCast.Polymorphic;

		var q: TypeCast = TypeCast.Throw;
		match (dst) {
			y: IntType => match (src) {
				x: IntType => q = if(legacy_intcast, TypeCast.IntViewI, TypeCast.IntCastI);
				x: FloatType => q = TypeCast.FloatCastI;
			}
			y: FloatType => match (src) {
				x: FloatType => q = TypeCast.FloatCastD;
			}
			y: ClassType => match (src) {
				x: ClassType => {
					var t = commonSuperClass(x, y), xx = t.0, yy = t.1;
					if (xx == null) return TypeCast.ThrowIfNonNull;
					var cast = if(x.depth < y.depth, TypeCast.ClassCast, TypeCast.Subsume);
					return maybeCast(xx, yy, cast, TypeCast.ThrowIfNonNull);
				}
				_ => return TypeCast.Throw;
			}
			y: TupleType => match (src) {
				x: TupleType => {
					var xt = x.elemTypes, yt = y.elemTypes;
					if (xt.length != yt.length) return TypeCast.Throw;
					var qa = Array<TypeCast>.new(xt.length);
					for (i < qa.length) {
						var n = newTypeCast(xt[i], yt[i], legacy_intcast);
						if (n == TypeCast.Throw) return TypeCast.Throw;
						qa[i] = n;
					}
					q = TypeCast.TupleCast(qa);
				}
			}
			y: TypeParamType => q = TypeCast.Polymorphic;
			y: TypeVarType => q = TypeCast.Polymorphic;
			y: VariantType => match (src) {
				x: VariantType => return maybeCast(x, y, TypeCast.Identity, TypeCast.Throw);
				x: VariantCaseType => {
					if (x.superType.decl == y.decl) {
						return maybeCast(x.superType, y, TypeCast.Subsume, TypeCast.Throw);
					}
				}
			}
			y: VariantCaseType => match (src) {
				x: VariantType => {
					if (y.superType.decl == y.decl) {
						return maybeCast(x, y.superType, TypeCast.VariantCast, TypeCast.Throw);
					}
				}
				x: VariantCaseType => {
					if (x.decl == y.decl) {
						return maybeCast(x.superType, y.superType, TypeCast.Identity, TypeCast.Throw);
					}
				}
			}
		}
		return if(isAssignable(src, dst), TypeCast.Subsume, q); // TODO: subtype, promote
	}
	private def maybeCast(x: Type, y: Type, t: TypeCast, f: TypeCast) -> TypeCast {
		if (x == y) return t;
		if (maybeEqual(x, y)) return TypeCast.Polymorphic;
		return f;
	}
}

enum UnifyMode {
	UP, PROMOTE, EQUAL, DOWN
}

// The different types of type query operations.
type TypeQuery {
	case False;
	case Polymorphic;
	case True;
	case TrueExceptForNull;
	case FloatQueryI;
	case FloatQueryD;
	case IntQueryF;
	case IntQueryI;
	case ClassQuery;
	case VariantQuery;
	case TupleQuery(sub: Array<TypeQuery>);
}

// The different kinds of type cast operations.
type TypeCast {
	case Throw;
	case Polymorphic;
	case Value;
	case Identity;
	case ThrowIfNonNull;
	case FloatCastI;
	case FloatCastD;
	case FloatPromoteI;
	case FloatPromoteF;
	case IntCastI;
	case IntCastF;
	case IntPromoteI;
	case IntViewI;  // legacy
	case Subsume;
	case ClassCast;
	case VariantCast;
	case TupleCast(sub: Array<TypeCast>);
}
