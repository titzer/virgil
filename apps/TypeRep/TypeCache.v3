// Copyright 2020 Ben L. Titzer. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// Cache for global singleton types like "void" and "bool".
component GlobalTypeCache {
	def VOID = VoidType.new();
	def BOOL = BoolType.new();
}
// Cache for integer types "iNN" and "uNN".
component IntTypeCache {
	private def max = 64;
	private def cache = Array<IntType>.new(max * 2);

	def get(signed: bool, width: byte) -> IntType {
		var index = (width + if(signed, max)) - 1;
		var t = cache[index];
		if (t != null) return t;
		return cache[index] = IntType.new(signed, width);
	}
}
// Cache for floating point types "float" and "double".
component FloatTypeCache {
	def FLOAT = FloatType.new(32, 23);
	def DOUBLE = FloatType.new(64, 52);
}
// Cache for tuple types "(T, T)". Handles degenerate cases of nullary and unary
// tuples, which are "void" and identity, respectively.
component TupleTypeCache {
	private def cache = TupleTypeCacheImpl.new();
	def get(args: Array<Type>) -> Type {
		if (args.length == 0) return GlobalTypeCache.VOID;
		if (args.length == 1) return args[0];
		return cache.get(args);
	}
}
// Cache for all array types "Array<T>".
component ArrayTypeCache {
	def get(t: Type) -> ArrayType {
		if (t.array == null) return t.array = ArrayType.new(t);
		return t.array;
	}
}
// Cache for all closure types "A -> B".
component ClosureTypeCache {
	def get(sig: Signature) -> ClosureType; // TODO
}
// Cache for all internal function (code pointer) types "A ~> B".
component FuncTypeCache {
	def get(sig: Signature) -> FuncType; // TODO
}
// Cache for all internal set types.
component SetTypeCache {
	def get = SetTypeCacheImpl.new().get;
}

// A cache for a class type {decl}. Note that nullary class types should be cached
// directly in the decl.
class ClassTypeCache(decl: ClassDecl) extends TypeCache<ClassType>(Kind.CLASS, u25.view(decl.uid)) {
	def make(hash: u32, args: Array<Type>) -> ClassType {
		return ClassType.new(hash, decl, args);
	}
	def typeArgs(ct: ClassType) -> Array<Type> {
		return ct.typeArgs;
	}
}
// A cache for a variant type {decl}. Note that nullary variant types should be cached
// directly in the decl.
class VariantTypeCache(decl: VariantDecl) extends TypeCache<VariantType>(Kind.VARIANT, u25.view(decl.uid)) {
	def make(hash: u32, args: Array<Type>) -> VariantType {
		return VariantType.new(hash, decl, args);
	}
	def typeArgs(ct: VariantType) -> Array<Type> {
		return ct.typeArgs;
	}
}
// A cache for a alias type {decl}. Note that nullary alias types should be cached
// directly in the decl.
class AliasTypeCache(decl: AliasDecl) extends TypeCache<AliasType>(Kind.ALIAS, u25.view(decl.uid)) {
	def make(hash: u32, args: Array<Type>) -> AliasType {
		return AliasType.new(hash, decl, args);
	}
	def typeArgs(ct: AliasType) -> Array<Type> {
		return ct.typeArgs;
	}
}
// Implementation of tuple type cache.
class TupleTypeCacheImpl extends TypeCache<TupleType>(Kind.TUPLE, 0) {
	def make(hash: u32, args: Array<Type>) -> TupleType {
		return TupleType.new(hash, args);
	}
	def typeArgs(ct: TupleType) -> Array<Type> {
		return ct.elemTypes;
	}
}
// Implementation of set type cache.
class SetTypeCacheImpl extends TypeCache<SetType>(Kind.SET, 0) {
	def make(hash: u32, args: Array<Type>) -> SetType {
		return SetType.new(hash, args);
	}
	def typeArgs(ct: SetType) -> Array<Type> {
		return ct.types;
	}
}

// A hash-based cache for types. This is essentially a memoization of a type constructor
// applied to argument types. Subclasses override the {make} and {typeArgs} methods
// in order to construct specific kinds of types.
def INITIAL_SIZE = 4;
def TABLE_FACTOR = 4;
class TypeCache<T>(kind: Kind, salt: u25) {
	var vals = Array</*weak*/ T>.new(INITIAL_SIZE);
	var table = Array<u32>.new(INITIAL_SIZE * TABLE_FACTOR);
	var pos: u32;
	var mask = u32.view(INITIAL_SIZE * TABLE_FACTOR - 1);
	var shift: u5 = 3;
	def get(args: Array<Type>) -> T {
		var hash = TypeHash.doN(kind, salt, args);
		var start = hash & mask, i = start;
		// XXX: one-element cache for last hit index?
		while (true) {
			var entry = table[i];
			if ((entry & 1) != 0) {
				var v = vals[entry >> 1], t = Type.!(v);
				if (t.hash == hash && Arrays.equal(args, typeArgs(v))) return v;
			} else {
				if (pos == vals.length) grow();
				var v = make(hash, args);
				table[i] = (pos << 1) | 1u;
				vals[pos++] = v;
				return v;
			}
			i = (i + 1) & mask;
			if (i == start) break;
		}
		// Because {table} is at least 4x {vals}, it should have at least one free entry
		System.error("Unreachable", "TypeCache.table inexplicably full");
		var d: T;
		return d;
	}
	def grow() {
		var ovals = vals;
		vals = Array.new(ovals.length * 2);
		table = Array.new(table.length * 2);
		mask = (mask << 1) | 0x1u;
		for (j < u32.view(ovals.length)) { // reinsert all existing vals
			var v = ovals[j], t = Type.!(v), h = t.hash;
			vals[j] = v;
			for (i < j) { // find the next free slot in the table
				var k = (h + i) & mask;
				var entry = table[k];
				if (entry == 0) {
					table[k] = (j << 1) | 1;
					break;
				}
			}

		}
	}
	def make(hash: u32, args: Array<Type>) -> T; // subclass must override
	def typeArgs(t: T) -> Array<Type>; // subclass must override
}
