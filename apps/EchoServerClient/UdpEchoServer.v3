// Copyright 2022 Virgil Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// This implements a simple UDP echo server that sits and listens on
// 0.0.0.0:5555 or :::5555 for any connections.

def EXIT_FAILURE = -1;
def options = BasicOptions.new();
def help    = options.newBoolOption("h", false);
def v6      = options.newBoolOption("v6", false);

def usage() {
	System.puts("Usage: UdpEchoServer [-h] [-v6]\n");
	System.puts("  -h   Print this option summary\n");
	System.puts("  -v6  Use IPv6 instead of IPv4\n");
	RiOs.exit(EXIT_FAILURE);
}

def main(args: Array<string>) {
	args = options.parse(args);
	if (help.get()) {
		usage();
	}

	var packet: UdpPacket = null;
	def sock = UdpSocket.new(5555, v6.get());
	def msgBuf = Array<byte>.new(512);
	def buf = StringBuilder.new();

	sock.open().expect("Unable to open socket");
	buf.put1("Hello! Socket opened on fd %d\n", sock.fd);
	System.puts(buf.toString());

	while (true) {
		var ret = sock.recvfrom(msgBuf, packet).expect("Receive message failed");
		def numRead = ret.0;
		packet = ret.1;

		buf.put2("Connection from %q:%d\n", packet.addr.render, packet.port);
		System.puts(buf.toString());

		def msg: string = Arrays.range(msgBuf, 0, numRead);
		buf.put1(
			"recv msg: \"%s\"\n",
			Strings.strip(msg)
		);
		System.puts(buf.toString());

		sock.sendto(msg, packet).expect("Send message failed");
		System.puts("Echoed msg!\n");
	}
	sock.close();
}
