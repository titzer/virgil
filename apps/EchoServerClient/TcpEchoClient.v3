// Copyright 2022 Virgil Authors. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// This implements a simple echo client that connects to 127.0.0.1:5555 or
// ::1:5555 and sends the message provided by the user from the commandline.

def EXIT_FAILURE = -1;
def options = BasicOptions.new();
def help    = options.newBoolOption("h", false);
def v6      = options.newBoolOption("v6", false);

def usage() {
	System.puts("Usage: TcpEchoClient [-h] [-v6] \"<message string>\"\n");
	System.puts("  -h   Print this option summary\n");
	System.puts("  -v6  Use IPv6 instead of IPv4\n");
	RiOs.exit(EXIT_FAILURE);
}

def main(args: Array<string>) {
	args = options.parse(args);
	if (help.get() || args.length == 0) {
		usage();
	}

	def localhost = if(v6.get(), Net.LOCALHOST_V6, Net.LOCALHOST_V4);
	def sock = ClientSocket.new(localhost, 5555, SocketType.Tcp);
	def msgBuf = Array<byte>.new(512);
	def buf = StringBuilder.new();

	sock.open().expect("Unable to open socket");
	buf.put1("Hello! Socket opened on fd %d\n", sock.fd);
	System.puts(buf.toString());

	sock.connect().expect("Socket connection failed");
	buf.put3(
		"Created socket connection from %q:%d to %q",
		sock.addr.render,
		sock.localPort,
		sock.addr.render
	);
	buf.put1(":%d\n", sock.port);
	System.puts(buf.toString());

	def msg = args[0];
	buf.put1("send msg: \"%s\"\n", Strings.strip(msg));
	System.puts(buf.toString());
	sock.send(args[0]).expect("Send message failed");

	def numRead = sock.recv(msgBuf).expect("Receive message failed");
	def recvMsg: string = Arrays.range(msgBuf, 0, numRead);
	if (!Strings.equal(msg, recvMsg)) {
		buf.put1("Received different msg than sent: \"%s\"\n", recvMsg);
	} else {
		buf.puts("Received same msg!\n");
	}
	System.puts(buf.toString());
	sock.close();
}
