// Copyright 2011 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

// A simple semispace copying collector.
component Semispace {
	var toSpace_start: Pointer;	// start of to-space region
	var toSpace_dirty: Pointer;	// end of dirty part of to-space region
	var toSpace_end: Pointer;	// end of to-space region
	var alloc_ptr: Pointer;		// allocation point in to-space region
	var fromSpace_start: Pointer;	// start of from-space region
	var fromSpace_end: Pointer;	// end of frame-space region
	var collecting = false;		// to prevent reentry
        def D = RiGc.D;

	new() {
		// install initialization and collection with runtime
		RiGc.scanRoot = scanSlot;
		RiRuntime.gcInit = init;
		RiRuntime.gcCollect = collect;
		GcStats.gc_current_allocated = fromSpaceAllocated;
	}
	// initialize the garbage collection spaces for runtime
	def init() {
		var heapSize = CiRuntime.HEAP_END - CiRuntime.HEAP_START;
		var heapMid = CiRuntime.HEAP_START + ((heapSize >> 1) & 0xFFFFFFF0);
		fromSpace_start = CiRuntime.HEAP_START;
		fromSpace_end = heapMid;
		toSpace_start = heapMid;
		toSpace_dirty = toSpace_start;
		alloc_ptr = fromSpace_start;
		toSpace_end = CiRuntime.HEAP_END;
		CiRuntime.heapCurLoc.store(fromSpace_start);
		CiRuntime.heapEndLoc.store(fromSpace_end);

		if (RiGc.verbose) {
                	D.puts("CiRuntime.DATA_START = ")
                         .putp(CiRuntime.DATA_START)
                         .ln();
                	D.puts("CiRuntime.DATA_END = ")
                         .putp(CiRuntime.DATA_END)
                         .ln();
                	D.puts("CiRuntime.HEAP_START = ")
                         .putp(CiRuntime.HEAP_START)
                         .ln();
                	D.puts("CiRuntime.HEAP_END = ")
                         .putp(CiRuntime.HEAP_END)
                         .ln();
                	D.puts("CiRuntime.heapCur = ")
                         .putp(CiRuntime.heapCurLoc.load<Pointer>())
                         .ln();
                	D.puts("CiRuntime.heapEnd = ")
                         .putp(CiRuntime.heapEndLoc.load<Pointer>())
                         .ln();
                }
	}
	// scan a slot and update it if necessary
	def scanSlot(ref_loc: Pointer) {
		var ref = ref_loc.load<Pointer>();
		if (ref == Pointer.NULL) return;
		if (ref < fromSpace_end) {
			if (ref >= fromSpace_start) {
				// ref lies in from-space, relocate
				var newref = ref.load<Pointer>(); // read forwarding pointer
				if (newref >= toSpace_start && newref < alloc_ptr) {
					// object has already been moved
					if (newref != ref) {
						ref_loc.store(newref);
						if (RiGc.debug) {
							D.puts("[").putp(ref_loc)
							 .puts("] = ").putp(ref).puts(" moved to ")
							 .putp(newref).ln();
						}
					}
				} else {
					// object hasn't been moved, copy it to to-space
					var size = RiGc.objectSize(ref);
					newref = alloc_ptr;
					if (RiGc.debug) {
						D.puts("[").putp(ref_loc)
						 .puts("] = ").putp(ref).puts(" copied to ")
						 .putp(newref).puts(", ").puti(size).puts(" bytes\n");
					}
					alloc_ptr = alloc_ptr + size;
					RiGc.memCopy(newref, ref, size);
					ref_loc.store(newref);
					ref.store(newref); // write forwarding pointer
				}
				return;
			}
		}
		if (ref < CiRuntime.DATA_END && ref >= CiRuntime.DATA_START) return;
		D.puts("!GcError: invalid reference @ ").putp(ref_loc).puts(" -> ").putp(ref);
		System.error("GcError", "fatal");
	}
	def statsBefore() -> int {
		var before = System.ticksUs();
		GcStats.collected_bytes = GcStats.collected_bytes + fromSpaceUsed();
		GcStats.allocated_bytes = GcStats.allocated_bytes + fromSpaceAllocated();
		if (RiGc.verbose) {
			D.puts("Begin GC, ").puti(fromSpaceUsed() / 1024).puts("K\n");
		}
		return before;
	}
	def statsAfter() {
		GcStats.survived_bytes = GcStats.survived_bytes + (alloc_ptr - toSpace_start);
	}
	def statsTime(before: int) {
		var diff = (System.ticksUs() - before);
		if (RiGc.debug || RiGc.verbose) {
			D.puts("End   GC, ").puti(fromSpaceUsed() / 1024)
			 .puts("K (").puti(diff).puts(" us)\n");
		}
		GcStats.collection_us = GcStats.collection_us + diff;
	}
	// perform a collection
	def collect(size: int, ip: Pointer, sp: Pointer) -> Pointer {
		if (collecting) RiRuntime.fatalException("GcError", "reentrant call to SemiSpace.collect", ip, sp);
		collecting = true;

		if (RiGc.debug) {
			D.puts(RiGc.CTRL_YELLOW);
			D.puts("\n===== begin Semispace.collect() =============================================================\n");
			D.puts(RiGc.CTRL_DEFAULT);
			D.puts("fromSpace_start = ").putp(fromSpace_start).ln();
			D.puts("heapCur         = ").putp(CiRuntime.heapCurLoc.load<Pointer>()).ln();
			D.puts("fromSpace_end   = ").putp(fromSpace_end).ln();
			D.puts("toSpace_start   = ").putp(toSpace_start).ln();
			D.puts("topSpace_end    = ").putp(toSpace_end).ln();
		}

		var before = if(RiGc.stats, statsBefore());
		var old_alloc_ptr = CiRuntime.heapCurLoc.load<Pointer>();
		alloc_ptr = toSpace_start;
		// scan global and stack roots
		RiGc.scanGlobals();
		RiGc.scanStack(ip, sp);
		// main loop: scan the objects copied from roots
		var scan = toSpace_start;
		while (scan < alloc_ptr) {
			// Scan all objects in the to space first
			while (scan < alloc_ptr) scan = scan + RiGc.scanObject(scan);
			//================================================================
			// USER CODE: Run user scanners and try again (TODO: need allocation?)
			if (RiGc.debug) {
				D.puts(RiGc.CTRL_YELLOW);
				D.puts("\n  -- call user scanners ----------------------------\n");
				D.puts(RiGc.CTRL_DEFAULT);
			}
			RiGc.runScanners(relocCallback);
		}
		RiGc.finishScanners();
		// everything copied, check to see if enough space remains
		if ((toSpace_end - scan) < size) return fatalOutOfMemory(size, ip, sp);
		// zero the remaining portion of the to-space if used previously
		RiGc.memClear(alloc_ptr, toSpace_dirty);
		// switch allocation bounds during callbacks
		CiRuntime.heapCurLoc.store(scan);
		CiRuntime.heapEndLoc.store(toSpace_end);
		// Report stats immediately after collection
		if (RiGc.stats) statsAfter();
		//================================================================
		// USER CODE: Run finalizers before overwriting from_space
		RiGc.runFinalizers(relocCallback);
		//================================================================
		// weak callbacks finished, try to fulfill the request
		scan = alloc_ptr = CiRuntime.heapCurLoc.load();
		if ((toSpace_end - scan) < size) return fatalOutOfMemory(size, ip, sp);
		CiRuntime.heapCurLoc.store(scan + size);

		if (RiGc.paranoid) {
			// overwrite the old from-space with garbage to catch errors
			for (p = fromSpace_start; p < old_alloc_ptr; p += 4) p.store(0xFACED1ED);
		}

		// swap spaces for next collection
		var tmp = (toSpace_start, toSpace_end);
		toSpace_start = fromSpace_start;
		toSpace_dirty = old_alloc_ptr;
		toSpace_end = fromSpace_end;
		fromSpace_start = tmp.0;
		fromSpace_end = tmp.1;

		GcStats.gc_count++;
		if (RiGc.stats) statsTime(before);
		collecting = false;
		return scan;
	}
	def fatalOutOfMemory(size: int, ip: Pointer, sp: Pointer) -> Pointer {
		if (RiGc.stats) {
			D.puts("!HeapOverflow: ")
			 .puti(alloc_ptr - toSpace_start).puts(" bytes used, ")
			 .puti(size).puts(" requested, ")
			 .puti(toSpace_end - alloc_ptr).puts(" available\n");
		}
		RiRuntime.fatalException("HeapOverflow", "insufficient space after GC", ip, sp);
		return Pointer.NULL;
	}
	// Used by weak callbacks to check if a reference was live.
	def relocCallback(ref: Pointer) -> Pointer {
		if (ref >= fromSpace_end || ref < fromSpace_start) return ref;
		var newref = ref.load<Pointer>(); // read forwarding pointer
		return if(newref >= toSpace_start && newref < alloc_ptr, newref, Pointer.NULL);
	}
	// Total space used in the from space.
	def fromSpaceUsed() -> long {
		return CiRuntime.heapCurLoc.load<Pointer>() - fromSpace_start;
	}
	// Space used in the from space since the last GC.
	def fromSpaceAllocated() -> long {
		return CiRuntime.heapCurLoc.load<Pointer>() - alloc_ptr;
	}
}
