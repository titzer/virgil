// Copyright 2019 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

def OUT = RiGc.OUT;
// Encapsulates logic of finding roots in native stack frames by walking the callstack using metadata
// from the compiler.
component NativeStackScanner {
	new() {
        	RiGc.scanStack = scanStack; // register stack scanner with GC
        }
	// Scan the stack, walking frames up to the main method.
	def scanStack(i: Pointer, s: Pointer) {
		var ip = i, sp = s;
		while (true) {
			var frameWords = 0;
			if (RiGc.debug) {
				OUT.puts(RiGc.CTRL_DEFAULT)
				   .puts("\nscanStack @ ip = ").putp(ip).puts(" +")
				   .putd(ip - CiRuntime.CODE_START)
				   .puts(", sp = ").putp(sp).putc('\n');
				OUT.puts(RiGc.CTRL_CYAN);
				frameWords = NativeStackPrinter.printLoc(ip); // double-check frame size in debug mode
				OUT.puts(RiGc.CTRL_DEFAULT);
			}
			var stackmapEntry = RiTables.exactMatch(
				RiTables.searchTable(CiRuntime.GC_STACKMAP_PAGES, CiRuntime.GC_STACKMAP_TABLE, ip));
			if (stackmapEntry == Pointer.NULL) {
				if (RiRuntime.userCodeList != null) {
					var userCode = RiRuntime.findUserCode(ip);
					if (userCode != null) {
						userCode.scanFrame(ip, sp);
						var t = userCode.nextFrame(ip, sp);
						ip = t.0; sp = t.1;
						continue;
					}
				}
				if (RiGc.debug) {
					OUT.puts("(no stackmap information)\n");
					if (frameWords == 0) return;
					var t = RiOs.callerFrame(ip, sp, frameWords);
					ip = t.0; sp = t.1;
					continue;
				}
				return;
			}
			var stackMap = stackmapEntry.load<int>() >>> RiTables.PAGE_SHIFT;
			var frameSize = RiGc.scanStackFrame(sp, stackMap);
			if (RiGc.debug && frameSize != frameWords * RiGc.REF_SIZE) {
				OUT.puts("\n\n!GcError: mismatch between source frame size ")
				   .putd(frameWords * RiGc.REF_SIZE)
				   .puts(" and stackmap size of ")
				   .putd(frameSize)
				   .ln();
				RiOs.exit(255);
			}
			var t = RiOs.callerFrame(ip, sp, frameSize / RiGc.REF_SIZE);
			ip = t.0; sp = t.1;
		}
	}
}
