// Copyright 2019 Google Inc. All rights reserved.
// See LICENSE for details of Apache 2.0 license.

component NativeGlobalScanner {
	def D = RiGc.D;
	new() {
        	RiGc.scanGlobals = scanGlobals;
        }
	// scan all the global roots in the program
	def scanGlobals() {
		var p = CiRuntime.GC_ROOTS_START;
		if (RiGc.onScanGlobalsStart != null) RiGc.onScanGlobalsStart();
		while (p < CiRuntime.GC_ROOTS_END) {
			// for each (offset, refmap) pair
			var offset = p.load<int>();
			var addr = CiRuntime.DATA_START + offset;
			var bits = (p + RiGc.INT_SIZE).load<int>();

			if (RiGc.onScanGlobalsMap != null) RiGc.onScanGlobalsMap(offset, bits);
			for (map = bits; map != 0; map = map >>> 1) {
				// for each set bit, call the scan function
				if ((map & 1) != 0) {
					if (RiGc.onScanGlobal != null) RiGc.onScanGlobal(addr);
					RiGc.scanRoot(addr);
				}
				addr = addr + RiGc.REF_SIZE;
			}
			p = p + (RiGc.INT_SIZE + RiGc.INT_SIZE);
		}
		if (RiGc.onScanGlobalsEnd != null) RiGc.onScanGlobalsEnd();
	}
}
