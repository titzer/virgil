// Enforces the API for CiRuntime.
component CiRuntimeApi {
	def c11 = C.new(11);
	def c12 = C.new(12);
	def buffer = Array<byte>.new(128);
	def rbuffer: Range<byte> = buffer;

	var errors: int;

	def main() -> int {
		errors = 0;
		checkNonNull(CiRuntime.HEAP_START, "HEAP_START");
		checkNonNull(CiRuntime.HEAP_END,   "HEAP_END");
		checkNonNull(CiRuntime.CODE_START, "CODE_START");
		checkNonNull(CiRuntime.CODE_END,   "CODE_END");
		checkNonNull(CiRuntime.DATA_START, "DATA_START");
		checkNonNull(CiRuntime.DATA_END,   "DATA_END");
		checkNonNull(CiRuntime.signalStub, "signalStub");
		checkNonNull(CiRuntime.heapCurLoc, "heapCurLoc");
		checkNonNull(CiRuntime.heapEndLoc, "heapEndLoc");

		checkNonNull(CiRuntime.GC_STACKMAP_PAGES,     "GC_STACKMAP_PAGES");
		checkNonNull(CiRuntime.GC_STACKMAP_TABLE,     "GC_STACKMAP_TABLE");
		checkNonNull(CiRuntime.GC_EXTMAPS,            "GC_EXTMAPS");
		checkNonNull(CiRuntime.GC_ROOTS_START,        "GC_ROOTS_START");
		checkNonNull(CiRuntime.GC_ROOTS_END,          "GC_ROOTS_END");
		checkNonNull(CiRuntime.GC_TYPE_TABLE,         "GC_TYPE_TABLE");
		checkNonNull(CiRuntime.SRC_POINTS_PAGES,      "SRC_POINTS_PAGES");
		checkNonNull(CiRuntime.SRC_POINTS_TABLE,      "SRC_POINTS_TABLE");
		checkNonNull(CiRuntime.SRC_METHODS_PAGES,     "SRC_METHODS_PAGES");
		checkNonNull(CiRuntime.SRC_METHODS_TABLE,     "SRC_METHODS_TABLE");
		checkNonNull(CiRuntime.SRC_METHODS_TABLE_END, "SRC_METHODS_TABLE_END");
		checkNonNull(CiRuntime.SRC_CLASS_TABLE,       "SRC_CLASS_TABLE");
		checkNonNull(CiRuntime.SRC_STRINGS,           "SRC_STRINGS");
		checkNonNull(CiRuntime.EX_START,              "EX_START");
		checkNonNull(CiRuntime.EX_END,                "EX_END");
		checkNonNull(CiRuntime.EX_TABLE,              "EX_TABLE");
		checkNonNull(CiRuntime.EX_STRINGS,            "EX_STRINGS");

		// stack variables should exist
		var t0 = (CiRuntime.STACK_START, CiRuntime.STACK_END, CiRuntime.STACK_SIZE);

		var func1 = CiRuntime.forgeClosure<C, int, int>(Pointer.NULL, null);
		var x1 = dontCall(func1, 3);

		var func2 = CiRuntime.forgeClosure<void, int, int>(Pointer.NULL, ());
		var x2 = dontCall(func2, 3);

		var t = CiRuntime.unpackClosure<C, int, int>(c11.add);
		var func3 = t.0, obj3 = t.1;
		if (obj3 != c11) {
			System.puts("Error: CiRuntime.unpackClosure\n");
			errors++;
		}
		var x3 = CiRuntime.forgeClosure<C, int, int>(func3, obj3);
		if (x3(14) != 25) {
			System.puts("Error: CiRuntime.unpack/forgeClosure\n");
			errors++;
		}
		var x4 = CiRuntime.forgeClosure<C, int, int>(func3, c12);
		if (x4(24) != 36) {
			System.puts("Error: CiRuntime.unpack/forgeClosure\n");
			errors++;
		}
		var x5 = CiRuntime.forgeRange<byte>(Pointer.atContents(buffer), buffer.length);
		if (x5 != rbuffer) {
			System.puts("Error: CiRuntime.forgeRange\n");
			errors++;
		}
		if (CiRuntime.FEATURE_GOBBLETY) {
			System.puts("Error: CiRuntime.FEATURE_\n");
			errors++;
		}

		return errors;
	}
	def checkCallers() {
		checkNonNull(CiRuntime.callerIp(), "callerIp()");
		checkNonNull(CiRuntime.callerSp(), "callerSp()");
	}
	def checkNonNull(p: Pointer, name: string) {
		if (p == Pointer.NULL) {
			System.puts("Error: CiRuntime.");
			System.puts(name);
			System.puts(" == NULL\n");
			errors++;
		}
		System.puts(name);
		System.puts(": ");
		System.puti(int.view(p - Pointer.NULL));
		System.putc('\n');
	}
	def dontCall(f: int -> int, arg: int) -> int {
		return arg;
	}
}
class C(x: int) {
	def add(y: int) -> int {
		return x + y;
	}
}
