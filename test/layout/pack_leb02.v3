//@execute 0=true; 1=true; 2=true; 3=true; 4=true; 5=false
def main(a: u32) -> bool {
	match (a) {
		0 => return pack(11) == 0x0B;
		1 => return pack(1122) == 0x08E2; // E2 08
		2 => return pack(112233) == 0x06ECE9; // E9 EC 06
		3 => return pack(11223344) == 0x05AD82B0; // B0 82 AD 05
		4 => return pack(1122334455) == 0x049795EDF7; // F7 ED 95 97 04
	}
	return false;
}
def pack(x: u32) -> u40 {
	var x0 = u7.view(x);
	var x1 = u7.view(x >> 7);
	var x2 = u7.view(x >> 14);
	var x3 = u7.view(x >> 21);
	var x4 = u7.view(x >> 28);

	if (x <= u7.max) {
		return x0;
	} else if (x <= u14.max) {
		var b0 = u8.pack((1u1, x0));
		var b1 = u8.pack((0u1, x1));
		return u16.pack((b1, b0));
	} else if (x <= u21.max) {
		var b0 = u8.pack((1u1, x0));
		var b1 = u8.pack((1u1, x1));
		var b2 = u8.pack((0u1, x2));
		return u24.pack((b2, b1, b0));
	} else if (x <= u28.max) {
		var b0 = u8.pack((1u1, x0));
		var b1 = u8.pack((1u1, x1));
		var b2 = u8.pack((1u1, x2));
		var b3 = u8.pack((0u1, x3));
		return u32.pack((b3, b2, b1, b0));
	} else {
		var b0 = u8.pack((1u1, x0));
		var b1 = u8.pack((1u1, x1));
		var b2 = u8.pack((1u1, x2));
		var b3 = u8.pack((1u1, x3));
		var b4 = u8.pack((0u1, x4));
		return u40.pack((b4, b3, b2, b1, b0));
	}
}
