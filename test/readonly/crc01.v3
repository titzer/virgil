//@execute 0=-1859984806; 1=2085305745; 2=-1761468918; 3=1938594526
def table: Array<int> = Array<int>.new(256);
def a: array<byte> = "Supercalifrag";
def b: array<byte> = [byte.view(-1), byte.view(-12), byte.view(185)];
def c: array<byte> = "&^*!@$(*&))(*@#$JDSJNVJASK";
def x = init();
def init() {
	// calculate the CRC table
	var i = 0;
	var magic = -306674912; // 0xEDB88320
	while (i < 256) {
		var crc = i, j = 8;
		while (j > 0) {
			if (int.view(crc & 1) == 1) crc = (crc >>> 1) ^ magic;
			else crc = crc >>> 1;
			j = j - 1;
		}
		table[i] = crc;
		i = i + 1;
	}
}

def main(arg: int) -> int {
	var data: array<byte> = "foo";
	if (arg == 0) data = a;
	else if (arg == 1) data = b;
	else if (arg == 2) data = c;
		
	var crc = -1;
	var i = 0, max = data.length;
	while (i < max) {
		var t1 = crc >>> 8;
		var t2 = table[(crc ^ ('\xFF' & data[i])) & 255];
		crc = t1 ^ t2;
		i = i + 1;
	}
	return crc;
}
